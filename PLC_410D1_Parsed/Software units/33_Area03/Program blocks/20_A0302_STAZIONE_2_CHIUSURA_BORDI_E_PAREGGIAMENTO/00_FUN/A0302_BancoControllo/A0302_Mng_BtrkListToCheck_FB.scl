FUNCTION_BLOCK "A0302_Mng_BtrkListToCheck_FB"
{ S7_Optimized_Access := 'TRUE' ; Published := 'FALSE' }
VERSION : 0.1
   VAR_IN_OUT 
      BTrk : Array[1.."BTRK_A03_EXIT_TOCHECK_ITEMS"] of "BTrk_item";
      FullList : Bool;
   END_VAR

   VAR 
      i { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Int;
      j : Int;
      Control : Struct
         NoPlaceEmpty : USInt;
         StopCount : Bool;
         Pls_ClearArchive : Bool;
      END_STRUCT;
   END_VAR

   VAR_TEMP 
      ClearBtrk : "BTrk_item";
   END_VAR


BEGIN
	REGION CONTROLLO DATABASE
	    //
	    IF #BTrk[1].B_ID <> 0 AND NOT #FullList THEN
	    //
	        REGION Controlla numero righe vuote
	        // Controllo spazi vuoti
	        IF NOT #Control.StopCount THEN
	            #Control.NoPlaceEmpty := 0;
	            #FullList := FALSE;
	            FOR #i := 1 TO "BTRK_A03_EXIT_TOCHECK_ITEMS" BY 1 DO
	                IF #BTrk[#i].B_ID = 0 THEN
	                    #Control.NoPlaceEmpty += 1;
	                END_IF;
	                IF #i >= "BTRK_A03_EXIT_TOCHECK_ITEMS" THEN
	                    #Control.StopCount := TRUE;
	                END_IF;
	            END_FOR;
	            IF #Control.NoPlaceEmpty = 0 THEN
	                #FullList := TRUE;
	            END_IF;
	        END_IF;
	    END_REGION
	    //
	        REGION Controlla contenuto righe database
	            FOR #i := "BTRK_A03_EXIT_TOCHECK_ITEMS" TO 1 BY -1 DO
	                IF #BTrk[#i].B_ID = 0 THEN
	                    // Cerca registro pieno
	                    FOR #j := #i - 1 TO 1 BY -1 DO
	                        // Controllo indice array
	                        IF #j <= 0 THEN
	                            EXIT;
	                        END_IF;
	                        // Copia riga piena su riga vuota
	                        IF #BTrk[#j].B_ID <> 0 THEN
	                            #BTrk[#i] := #BTrk[#j];
	                            #BTrk[#j] := #ClearBtrk;
	                            EXIT;
	                        END_IF;
	                    END_FOR;
	                END_IF;
	            END_FOR;
	        END_REGION
	    //
	        REGION Scrivi nuovo tracking
	            FOR #i := "BTRK_A03_EXIT_TOCHECK_ITEMS" TO 1 BY -1 DO
	                IF #BTrk[#i].B_ID = 0 THEN
	                    #BTrk[#i] := #BTrk[1];
	                    #BTrk[1] := #ClearBtrk;
	                    EXIT;
	                END_IF;
	            END_FOR;
	        END_REGION
	    ELSE
	        #Control.StopCount := FALSE;
	    END_IF;
	    //
	    REGION Resetta tutto l'archivio
	        IF #Control.Pls_ClearArchive THEN
	            FOR #i := 1 TO "BTRK_A03_EXIT_TOCHECK_ITEMS" DO
	                #BTrk[#i] := #ClearBtrk;
	            END_FOR;
	        END_IF;
	    END_REGION
	    //
	END_REGION
	
	
END_FUNCTION_BLOCK

