FUNCTION_BLOCK "Push_MSG411_Update_Coil_Aspo_In_Linea"
{ S7_Optimized_Access := 'TRUE' ; Published := 'FALSE' }
VERSION : 0.1
   VAR_INPUT 
      PT_RETRY : Time := T#5s;
   END_VAR

   VAR_IN_OUT 
      Msg411_Err : "MES_Err_&_Msg";
      Prev_Data : "Coil_Item";
   END_VAR

   VAR 
      Msg411_Send : Struct
         Status : Int;
         Exe : Bool;
         RETVAL : DInt;
         TON_Retry {InstructionName := 'TON_TIME'; LibVersion := '1.0'; S7_SetPoint := 'False'} : TON_TIME;
      END_STRUCT;
   END_VAR

   VAR CONSTANT 
      MSG_XXX_SEND_IDLE : Int := 0;
      MSG_XXX_SEND_EXE : Int := 2;
      MSG_XXX_SEND_PAUSE : Int := 3;
   END_VAR


BEGIN
	
	
	
	REGION PUSH MSG411
	    
	(*
	PLC → MES – Topic = 411: Stato rotolo nell’aspo in linea 
	*)
	    
	    
	    CASE #Msg411_Send.Status OF
	            
	        #MSG_XXX_SEND_IDLE:
	            
	            IF "Production".Coil_Data.ASPO_IN_LINEA.C_ID <> #Prev_Data.C_ID THEN
	                #Msg411_Send.Status := #MSG_XXX_SEND_EXE;
	            END_IF;
	            
	            IF "Production".Coil_Data.ASPO_IN_LINEA.MES_IDP <> #Prev_Data.MES_IDP THEN
	                #Msg411_Send.Status := #MSG_XXX_SEND_EXE;
	            END_IF;
	            
	            IF "Production".Coil_Data.ASPO_IN_LINEA.Decoiler_N <> #Prev_Data.Decoiler_N THEN
	                #Msg411_Send.Status := #MSG_XXX_SEND_EXE;
	            END_IF;
	            
	            
	            IF "Production".Coil_Data.ASPO_IN_LINEA.MES_CN <> #Prev_Data.MES_CN
	            THEN
	                #Msg411_Send.Status := #MSG_XXX_SEND_EXE;
	            END_IF;
	            
	            IF "Production".Coil_Data.ASPO_IN_LINEA.MES_HN <> #Prev_Data.MES_HN
	            THEN
	                #Msg411_Send.Status := #MSG_XXX_SEND_EXE;
	            END_IF;
	            
	            IF "Production".Coil_Data.ASPO_IN_LINEA.Stato_Rotolo <> #Prev_Data.Stato_Rotolo
	            THEN
	                #Msg411_Send.Status := #MSG_XXX_SEND_EXE;
	            END_IF;
	            
	            
	            IF "Production".Coil_Data.ASPO_IN_LINEA.Provini_Richiesti <> #Prev_Data.Provini_Richiesti
	            THEN
	                #Msg411_Send.Status := #MSG_XXX_SEND_EXE;
	            END_IF;
	            
	            IF "Production".Coil_Data.ASPO_IN_LINEA.Provini_Eseguiti <> #Prev_Data.Provini_Eseguiti
	            THEN
	                #Msg411_Send.Status := #MSG_XXX_SEND_EXE;
	            END_IF;
	            
	            IF (ABS(#Prev_Data.Diametro_Rotolo - "Production".Coil_Data.ASPO_IN_LINEA.Diametro_Rotolo) > 100.0)
	                AND
	                "Production".Coil_Data.ASPO_IN_LINEA.Stato_Rotolo = 1
	            THEN
	                #Msg411_Send.Status := #MSG_XXX_SEND_EXE;
	            END_IF;
	            
	            IF "Production".Coil_Data.ASPO_IN_LINEA.Spessore_Errore <> #Prev_Data.Spessore_Errore THEN
	                #Msg411_Send.Status := #MSG_XXX_SEND_EXE;
	            END_IF;
	            
	            IF "Production".Coil_Data.ASPO_IN_LINEA.Spessore_materiale_TAV <> #Prev_Data.Spessore_materiale_TAV AND "Sys".ByPass THEN
	                #Msg411_Send.Status := #MSG_XXX_SEND_EXE;
	            END_IF;
	            
	            IF "Production".Coil_Data.ASPO_IN_LINEA.Spessore_materiale_TEV <> #Prev_Data.Spessore_materiale_TEV AND "Sys".ByPass  THEN
	                #Msg411_Send.Status := #MSG_XXX_SEND_EXE;
	            END_IF;
	            
	            IF "Production".Coil_Data.ASPO_IN_LINEA.Spessore_Deroga_TKD <> #Prev_Data.Spessore_Deroga_TKD AND "Sys".ByPass THEN
	                #Msg411_Send.Status := #MSG_XXX_SEND_EXE;
	            END_IF;
	            
	            RETURN;
	            
	            
	        #MSG_XXX_SEND_EXE:
	            
	            
	            #Msg411_Send.RETVAL := "MES_Msg411_Push"(MES_IDI := 0, Err := #Msg411_Err);
	            
	            
	            IF #Msg411_Send.RETVAL < "PROD_FUN_RESULT_OK" THEN
	                
	                #Msg411_Send.TON_Retry (IN := FALSE,
	                                        PT := #PT_RETRY);
	                
	                #Msg411_Send.Status := #MSG_XXX_SEND_PAUSE;
	                
	                RETURN;
	                
	            END_IF;
	            
	            
	            
	            #Prev_Data := "Production".Coil_Data.ASPO_IN_LINEA;
	            
	            #Msg411_Send.Status := #MSG_XXX_SEND_IDLE;
	            
	            
	            RETURN;
	            
	            
	            
	        #MSG_XXX_SEND_PAUSE:
	            
	            
	            #Msg411_Send.TON_Retry (IN := TRUE,
	            PT := #PT_RETRY);
	            
	            
	            IF #Msg411_Send.TON_Retry.Q THEN
	                #Msg411_Send.Status := #MSG_XXX_SEND_IDLE;
	            END_IF;
	            
	            
	        ELSE
	            
	            #Msg411_Send.Status := #MSG_XXX_SEND_IDLE;
	            
	    END_CASE;
	END_REGION
	
	
	
END_FUNCTION_BLOCK

