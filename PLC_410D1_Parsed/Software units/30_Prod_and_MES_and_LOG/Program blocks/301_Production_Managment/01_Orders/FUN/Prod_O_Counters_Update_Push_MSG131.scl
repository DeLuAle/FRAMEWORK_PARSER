FUNCTION_BLOCK "Prod_O_Counters_Update_Push_MSG131"
{ S7_Optimized_Access := 'TRUE' ; Published := 'FALSE' }
VERSION : 0.1
   VAR_INPUT 
      O_ID : DInt;
      PART_PROD_Halted : "Prod_Halted";
      BEAM_PROD_Halted : "Prod_Halted";
      PT_RETRY : Time := T#1s;
   END_VAR

   VAR_IN_OUT 
      Exe : Bool;
      Msg131_Err : "MES_Err_&_Msg";
   END_VAR

   VAR 
      Msg131_Send : Struct
         Status : Int;
         Exe : Bool;
         RETVAL : DInt;
         TON_Retry {InstructionName := 'TON_TIME'; LibVersion := '1.0'; S7_SetPoint := 'False'} : TON_TIME;
      END_STRUCT;
   END_VAR

   VAR CONSTANT 
      MSG_XXX_SEND_IDLE : Int := 0;
      MSG_XXX_SEND_EXE : Int := 2;
      MSG_XXX_SEND_PAUSE : Int := 3;
   END_VAR


BEGIN
	
	
	REGION SEND MSG131
	    
	(*
	PLC → MES – Topic = 131: PLC → MES – Topic = 131: Stato ordine di produzione
	*)
	    
	    
	    CASE #Msg131_Send.Status OF
	            
	        #MSG_XXX_SEND_IDLE:
	            
	            
	            IF NOT #Exe THEN
	                RETURN;
	            END_IF;
	            
	            
	            #Msg131_Send.Status := #MSG_XXX_SEND_EXE;
	            
	            
	            
	        #MSG_XXX_SEND_EXE:
	            
	            
	            #Msg131_Send.RETVAL := "MES_Msg131_Push"(O_ID := #O_ID,
	                                                     MES_IDI := 0,
	                                                     PART_PROD_Halted:=#PART_PROD_Halted,
	                                                     BEAM_PROD_Halted:=#BEAM_PROD_Halted,
	                                                     Err => #Msg131_Err);
	            
	            
	            IF #Msg131_Send.RETVAL < "PROD_FUN_RESULT_OK" THEN
	                
	                #Msg131_Send.TON_Retry(IN := FALSE,
	                                       PT := #PT_RETRY);
	                
	                #Msg131_Send.Status := #MSG_XXX_SEND_PAUSE;
	                
	                RETURN;
	                
	            END_IF;
	            
	            
	            #Exe := FALSE;
	            
	            
	            #Msg131_Send.Status := #MSG_XXX_SEND_IDLE;
	            
	            
	            RETURN;
	            
	            
	            
	        #MSG_XXX_SEND_PAUSE:
	            
	            
	            #Msg131_Send.TON_Retry(IN := TRUE,
	                                   PT := #PT_RETRY);
	            
	            
	            IF #Msg131_Send.TON_Retry.Q THEN
	                #Msg131_Send.Status := #MSG_XXX_SEND_IDLE;
	            END_IF;
	            
	           
	            
	        ELSE
	            
	            #Msg131_Send.Status := #MSG_XXX_SEND_IDLE;
	            
	    END_CASE;
	END_REGION
	
	
	
	
END_FUNCTION_BLOCK

