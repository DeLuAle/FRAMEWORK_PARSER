FUNCTION "MES_Msg401_Decoder" : DInt
{ S7_Optimized_Access := 'TRUE' ; Published := 'FALSE' }
VERSION : 0.1
// DECODIFICA MESSAGGIO TOPIC = 401 (INVIO DATI ROTOLO NELLâ€™ASPO IN CARICO)
   VAR_INPUT 
      payLoad : String;
   END_VAR

   VAR_OUTPUT 
      Item : "Coil_Item";
   END_VAR

   VAR_TEMP 
      strValue : String;
      isValidNumber : Bool;
      tempDInt : DInt;
   END_VAR


BEGIN
	REGION INFO
	    (*
	    
	    MES → PLC – Topic = 401: Invio dati rotolo nell’aspo in carico
	    
	    Il messaggio viene inviato per notificare al PLC la presenza di un nuovo rotolo montato sul mandrino.
	    Il PLC memorizza le informazioni ricevute aggiornando la propria struttura dati interna.
	    
	    ACK = -2 → È già presente un rotolo nell’aspo in carico. 
	    
	    Payload
	    Chiave  Formato Descrizione
	    [CN]=   N0  Numero identificativo del rotolo
	    [HN]=   N0  Numero di colata
	    [PR]=   N0  Richiesta di esecuzione provinatura (No = 0; Sì ≥ 1)
	
	     *)
	    
	    
	END_REGION
	
	
	REGION INIT
	    
	    #MES_Msg401_Decoder := "PROD_FUN_RESULT_OK";
	    #Item := "Production".Coil_Data.Empty;
	    
	END_REGION
	
	
	
	
	
	REGION [CN] Estrai chiave [CN]
	    #strValue := "MES_Get_KeyValue_From_Payload"(payLoad := #payLoad, key := '[CN]=', delimiter := "MES_MSG_KEY_SEPARATOR");
	    
	    IF #strValue = '!ERROR!_DELIMITATORE_NON_TROVATO' THEN
	        #MES_Msg401_Decoder := "ERR_DEL_CN";
	        RETURN;
	    END_IF;
	    
	    IF #strValue = '!ERROR!_CHIAVE_NON_TROVATA' THEN
	        #MES_Msg401_Decoder := "ERR_KEY_CN";
	        RETURN;
	    END_IF;
	    
	    #Item.MES_CN := #strValue;
	    
	    (*
	    
	    // 2005-10-16 CN diventa da numero a stringa
	    //
	    
	    #isValidNumber := FALSE;
	    IF #strValue <> '' THEN
	        STRG_VAL(IN := #strValue,
	                 FORMAT := 0,
	                 P := 1,
	                 OUT => #tempDInt);
	        #isValidNumber := ENO;
	    END_IF;
	    
	    
	    
	    
	    IF NOT #isValidNumber THEN
	        #MES_Msg401_Decoder := "ERR_VAL_CN";
	        RETURN;
	    END_IF;
	    
	    #Item.Numero_Rotolo := #tempDInt;
	        
	    *)
	    
	    
	END_REGION
	
	REGION [HN] Estrai chiave [HN]
	    #strValue := "MES_Get_KeyValue_From_Payload"(payLoad := #payLoad, key := '[HN]=', delimiter := "MES_MSG_KEY_SEPARATOR");
	    
	    IF #strValue = '!ERROR!_DELIMITATORE_NON_TROVATO' THEN
	        #MES_Msg401_Decoder := "ERR_DEL_HN";
	        RETURN;
	    END_IF;
	    IF #strValue = '!ERROR!_CHIAVE_NON_TROVATA' THEN
	        #MES_Msg401_Decoder := "ERR_KEY_HN";
	        RETURN;
	    END_IF;
	    
	    #Item.MES_HN := #strValue;
	    
	    (*
	    
	    // 2005-10-16 CN diventa da numero a stringa
	    //
	    
	    #isValidNumber := FALSE;
	    IF #strValue <> '' THEN
	        STRG_VAL(IN := #strValue,
	                 FORMAT := 0,
	                 P := 1,
	                 OUT => #tempDInt);
	        #isValidNumber := ENO;
	    END_IF;
	    
	    IF NOT #isValidNumber THEN
	        #MES_Msg401_Decoder := "ERR_VAL_HN";
	        RETURN;
	    END_IF;
	    
	    #Item.Numero_Colata := #tempDInt;
	    *)
	    
	    
	END_REGION
	
	REGION [PR] Estrai chiave [PR]
	    #strValue := "MES_Get_KeyValue_From_Payload"(payLoad := #payLoad, key := '[PR]=', delimiter := "MES_MSG_KEY_SEPARATOR");
	    IF #strValue = '!ERROR!_DELIMITATORE_NON_TROVATO' THEN
	        #MES_Msg401_Decoder := "ERR_DEL_PR";
	        RETURN;
	    END_IF;
	    IF #strValue = '!ERROR!_CHIAVE_NON_TROVATA' THEN
	        #MES_Msg401_Decoder := "ERR_KEY_PR";
	        RETURN;
	    END_IF;
	    
	    #isValidNumber := FALSE;
	    IF #strValue <> '' THEN
	        STRG_VAL(IN := #strValue,
	                 FORMAT := 0,
	                 P := 1,
	                 OUT => #tempDInt);
	        #isValidNumber := ENO;
	    END_IF;
	    
	    IF NOT #isValidNumber THEN
	        #MES_Msg401_Decoder := "ERR_VAL_PR";
	        RETURN;
	    END_IF;
	    
	    IF #tempDInt >= 1 THEN
	        #Item.Provini_Richiesti := 1;
	    END_IF;
	        
	END_REGION
	
	
	
	
	
	
	
	
END_FUNCTION

