FUNCTION "MES_Msg413_Push" : DInt
{ S7_Optimized_Access := 'TRUE' ; Published := 'FALSE' }
VERSION : 0.1
   VAR_INPUT 
      MES_IDI : DInt;   // ID del messaggio MES→PLC che ha richiesto l’interrogazione
   END_VAR

   VAR_IN_OUT 
      Err : "MES_Err_&_Msg";
   END_VAR

   VAR_TEMP 
      RETVAL : DInt;   // Valore di ritorno della funzione di ricerca dati ItemProd usando ID_Prod (PLC) 
      strPayload : String;   // Stringa Payload per formare il messaggio
      Msg : "MES_Msg";
      CN : String;
      HN : String;
   END_VAR

   VAR CONSTANT 
      NUMERO_MESSAGGIO : UDInt := 413;
   END_VAR


BEGIN
	REGION INFO
	    
	    (*
	    
	    PLC → MES – Topic = 413: Stato rotolo nell’aspo in carico
	    
	    Il messaggio viene inviato:
	    •   non appena il rotolo sull’aspo in carico cambia stato, oppure
	    •   in risposta a una richiesta di interrogazione del MES (Topic = 412).
	    
	    Payload: Lo stesso del messaggio con Topic=411. Le uniche differenze logiche dipendono dal fatto che il ro-tolo si trova nel mandrino dell’aspo sul lato di carico e non in lavorazione.
	    
	    ________________________________________
	    
	    
	    Payload
	    
	    Chiave  Formato Descrizione
	    [IDI]   N0  ID del messaggio MES → PLC che ha richiesto l’interrogazione. Se = 0, il messaggio è stato inviato spontaneamente dal PLC.
	    [CN]    N0  Numero identificativo del rotolo.
	    [HN]    N0  Numero di colata.
	    [RDM]   N0  Diametro del rotolo (in mm), rilevato dal sensore. Il valore è aggiornato ogni volta che il diametro diminuisce di una certa soglia (es. 5 mm) rispetto alla misura prece-dente. Negli altri casi, il valore rappresenta il diametro rilevato al momento dell'in-vio del messaggio.
	    [SR]    N0  Stato del rotolo (vedi codici sotto)
	    [PRR]   N0  Numero di provini richiesti. Valore iniziale = 1 se [PR] = 1 nel messaggio con Topic = 401; incrementato di 1 per ogni messaggio ricevuto con Topic = 403.
	    [PRD]   N0  Numero di provini eseguiti. Deve essere uguale a [PRR] al completamento dei pro-vini richiesti.
	    [TNG]   N0  Errore spessore rilevato dal sensore (No = 0; Sì ≥ 1)
	    [TEV]   N2  Valore dello spessore fuori tolleranza (in mm). Viene aggiornato la prima volta che il valore supera la soglia, e successivamente solo in presenza di variazioni significa-tive (isteresi). Se lo spessore rientra nei limiti, [TEV] viene azzerato, così come [TNG].
	    [TKD]   N0  Uso del rotolo “in deroga” nonostante la presenza di errore spessore ([TNG] ≥ 1)
	    
	    Codici possibili per [SR] – Stato rotolo 
	    
	    •   0 – Nessun dato / vuoto
	    •   1 – In uso (assegnato a un ordine di lavoro)
	    •   2 – Fine lamiera
	    •   3 – Interruzione: il rotolo era in uso, ma è stato separato nel banco di giunzione per essere riavvolto
	    
	    
	    *)
	    
	END_REGION
	
	
	
	
	REGION INIT
	    
	    // RETURN value = 0 Fine esecuzione senza errori
	    // 
	    
	    #MES_Msg413_Push := "PROD_FUN_RESULT_OK";
	    #Err.Presence := FALSE;
	    
	END_REGION
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	REGION PayLoad creation
	    
	    
	    //@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
	    //
	    // CREAZIONE PAYLOAD
	    // 
	    //@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@    
	    
	    // Creazione payload
	    #strPayload := '';
	    
	    
	    
	    // [IDI]   ID del messaggio MES → PLC che ha richiesto l’interrogazione. Se = 0, il messaggio è stato inviato spontaneamente dal PLC.
	    // 
	    IF #MES_IDI > 0 THEN
	        
	        #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_IDI");
	        #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := #MES_IDI, NDec := 0));
	        #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    ELSE
	        #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_IDI");
	        #strPayload := CONCAT(IN1 := #strPayload, IN2 := '0');
	        #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    END_IF;
	    
	    
	    
	    
	    // [CN] Numero identificativo del rotolo
	    //
	    
	    #CN := "Production".Coil_Data.ASPO_IN_CARICO.MES_CN;
	    IF #CN = '' THEN
	        #CN := 'vuoto';
	    END_IF;
	    
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_CN");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := #CN);
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    // [HN] Numero di colata
	    // 
	    #HN := "Production".Coil_Data.ASPO_IN_CARICO.MES_HN;
	    IF #HN = '' THEN
	        #HN := 'vuoto';
	    END_IF;
	    
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_HN");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := #HN);
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    
	    
	    (*Codici possibili per [SR] – Stato rotolo 
	    •   0 – Nessun dato / vuoto
	    •   1 – In uso (assegnato a un ordine di lavoro)
	    •   2 – Fine lamiera
	    •   3 – Interruzione: il rotolo era in uso, ma è stato separato nel banco di giunzione per essere riavvolto    *)
	    
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_SR");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := "Production".Coil_Data.ASPO_IN_CARICO.Stato_Rotolo, NDec := 0));
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    
	    //[PRR]   Numero di provini richiesti. Valore iniziale = 1 se [PR] = 1 nel messaggio con Topic = 401; incrementato di 1 per ogni messaggio ricevuto con Topic = 403.
	    //
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_PRR");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := "Production".Coil_Data.ASPO_IN_CARICO.Provini_Richiesti, NDec := 0));
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    //[PRD]   Numero di provini eseguiti. Deve essere uguale a [PRR] al completamento dei pro-vini richiesti.
	    //
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_PRD");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := "Production".Coil_Data.ASPO_IN_CARICO.Provini_Eseguiti, NDec := 0));
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    
	    //[RDM]   Diametro del rotolo (in mm), rilevato dal sensore. Il valore è aggiornato ogni volta che il diametro diminuisce di una certa soglia (es. 5 mm) rispetto alla misura prece-dente. Negli altri casi, il valore rappresenta il diametro rilevato al momento dell'in-vio del messaggio.
	    //
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_RDM");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := "Production".Coil_Data.ASPO_IN_CARICO.Diametro_Rotolo, NDec := 0));
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    
	    
	    //[TNG]   Errore spessore rilevato dal sensore (No = 0; Sì ≥ 1)
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_TNG");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := "Production".Coil_Data.ASPO_IN_CARICO.Spessore_Errore, NDec := 0));
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    //[TEV]   Valore dello spessore fuori tolleranza (in mm). Viene aggiornato la prima volta che il valore supera la soglia, e successivamente solo in presenza di variazioni significa-tive (isteresi). Se lo spessore rientra nei limiti, [TEV] viene azzerato, così come [TNG].
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_TEV");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := "Production".Coil_Data.ASPO_IN_CARICO.Spessore_materiale_TEV, NDec := 2));
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    //[TKD]   Uso del rotolo “in deroga” nonostante la presenza di errore spessore ([TNG] ≥ 1)
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_TKD");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := "Production".Coil_Data.ASPO_IN_CARICO.Spessore_Deroga_TKD, NDec := 0));
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    
	    
	END_REGION ;
	
	
	
	REGION PUSH messaggio
	    
	    // Incrementa ultimo ID
	    // 
	    "MES_Send_Buffer_Msg_Topic4XX".lastId := "Inc_DINT_Circ"("MES_Send_Buffer_Msg_Topic4XX".lastId);
	    
	    
	    // Prepararazione messaggio
	    // 
	    #Msg.Id := "MES_Send_Buffer_Msg_Topic4XX".lastId;
	    #Msg.timestamp := "Conv_DT_to_STRING"("Now"());
	    #Msg.topic := #NUMERO_MESSAGGIO;
	    #Msg.payLoad := #strPayload;
	    
	    
	    // Push messaggio
	    // 
	    #RETVAL := "MES_Buffer_Push"(Msg := #Msg,
	                                 Evaluate_FULL := "ProductionMng_PERS".Pers.Data.MODE.MES_ON,
	                                 Buffer_msg := "MES_Send_Buffer_Msg_Topic4XX".msg);
	    
	    
	    
	    // Push messaggio LOG
	    // 
	    "IPC_Push_Log_MES"(Msg := #Msg,
	                       Ack := #RETVAL);
	    
	    
	    IF #RETVAL < "PROD_FUN_RESULT_OK" THEN
	        
	        #MES_Msg413_Push := #RETVAL;
	        
	        #Err := "MES_Error_Set"(msg_RETVAL := #RETVAL, msg := #Msg);
	        RETURN;
	    END_IF;
	    
	    
	    // Return value = OK
	    // 
	    #MES_Msg413_Push := "PROD_FUN_RESULT_OK";
	    RETURN;
	END_REGION
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
END_FUNCTION

