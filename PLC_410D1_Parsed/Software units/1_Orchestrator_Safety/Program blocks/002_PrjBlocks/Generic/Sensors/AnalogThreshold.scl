FUNCTION_BLOCK "AnalogThreshold"
{ S7_Optimized_Access := 'TRUE' ; Published := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      Signal : Int;
      Hi_Limit : Real := 100.0;
      Low_Limit : Real := 0.0;
      Bipolar : Bool := FALSE;
      Threshold : Real := 50.0;
      Hysteresis : Real := 0.0;
      FilterTau : Time := T#300ms;
   END_VAR

   VAR_OUTPUT 
      Q : Bool;
      DataValid : Bool;
      Value : Real;
   END_VAR

   VAR 
      RawValue : Real;
      FilteredValue : Real;
      filter { S7_SetPoint := 'False'} : "Filter_1°Order";
   END_VAR

   VAR_TEMP 
      retval : Word;
   END_VAR


BEGIN
	// Scale Analog Input
	#retval := SCALE(IN := #Signal, HI_LIM := #Hi_Limit, LO_LIM := #Low_Limit, BIPOLAR := #Bipolar, OUT => #RawValue);
	#DataValid := #retval = 0;
	
	// Value Filtering
	#filter(Sample := #RawValue,
	        Tau := #FilterTau,
	        Q => #Value);
	
	// Apply Threshold
	#Q := #Value > (#Threshold + SEL(G := #Q, IN0 := #Hysteresis, IN1 := - #Hysteresis));
	
	// Outputs
	ENO := #Q;
	
END_FUNCTION_BLOCK

