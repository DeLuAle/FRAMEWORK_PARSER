FUNCTION "LinearIn" : Void
{ S7_Optimized_Access := 'TRUE' ; Published := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      ValueIn : Real;
      x1 : Real;
      y1 : Real;
      x2 : Real;
      y2 : Real;
   END_VAR

   VAR_OUTPUT 
      retVal : Real;
   END_VAR

   VAR_TEMP 
      Change_in_Y : Real;
      Change_in_X : Real;
      m : Real;
      y : Real;
   END_VAR


BEGIN
	// The equation of a straight line is usually written this way:
	// "y = mx + c"
	// What does it stand for?
	//     y = how far up (rise)
	//     x = how far along (run)
	//     m = Slope or Gradient (how steep the line is)
	//          m =  (Change_in_Y) / (Change_in_X)
	//     c = value of y when x=0
	
	// The "point-slope" form of the equation of a straight line is:
	// "y − y1 = m(x − x1)"
	
	// Y-Intercept of a line form from the equation of "point-slope" and straight line is:
	// "y = m(x − x1) + y1"
	
	
	
	// THE END FORMULA WITH SUBSTITUTIONS IS:
	///////////////////////////////////////////////////////////////
	// "y = m * (x − x1) + y1"
	// LinearIn = m * (ValueIn − x1) + y1
	//          = (Change_in_Y / Change_in_X) * (ValueIn − x1) + y1
	//          = ((y2 - y1) / (x2 - x1)) * (ValueIn − x1) + y1
	///////////////////////////////////////////////////////////////
	
	#Change_in_Y := #y2 - #y1;
	#Change_in_X := #x2 - #x1;
	#m := #Change_in_Y / #Change_in_X;
	
	#y := (#m * (#ValueIn - #x1)) + #y1;
	
	IF #y > #y2 THEN // Limits check
	    #retVal:= #y2;
	ELSIF #y < #y1 THEN
	    #retVal := #y1;
	ELSE
	    #retVal := #y;
	END_IF;
	
	
END_FUNCTION

