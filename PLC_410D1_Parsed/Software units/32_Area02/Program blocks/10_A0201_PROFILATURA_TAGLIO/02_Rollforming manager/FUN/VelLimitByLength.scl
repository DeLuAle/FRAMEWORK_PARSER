FUNCTION "VelLimitByLength" : LReal
{ S7_Optimized_Access := 'TRUE' ; Published := 'FALSE' }
VERSION : 0.1
   VAR_INPUT 
      FeedingCruiseVel : LReal;   // LU/min
      PartLength : LReal;   // LU
      Synch_Acc : LReal;   // [lu/s^2]
      Synch_Jerk : LReal;   // [lu/s^3]
      BackHomeVel : LReal;   // LU/min
      Work_CycleTime : LReal;   // [s]
      DetachVel : LReal;   // LU/min
      DetachSpace : LReal;   // LU
      VelTolerance : LReal;   // LU/min
   END_VAR

   VAR_TEMP 
      Master_Vel : LReal;
      deltaVel : LReal;
      TimeToSync : LReal;
      SpaceToSync : LReal;
      TimeToDetach : LReal;
      OutboundTime : LReal;
      OutboundSpace : LReal;
      ReturnTime : LReal;
      ReturnSpace : LReal;
      TotalTime : LReal;
      TotalLeadSpace : LReal;
   END_VAR


BEGIN
	
	#Master_Vel := #deltaVel := #FeedingCruiseVel;
	
	  // Statement section REPEAT
	REPEAT 
	       // Time/Space to synchronization / Desynchronization
	        "Trapezoidal_RampSpace&Time"(VDelta:=#Master_Vel,
	                                     AccDec:=#Synch_Acc,
	                                     Jerk:=#Synch_Jerk,
	                                     Space=>#SpaceToSync,
	                                     Time_s=>#TimeToSync);
	        
	        // Detach time
	        IF #DetachSpace > 0.0 THEN 
	            #TimeToDetach := "Trapezoidal_MoveTime"(MoveSpace:=#DetachSpace, MaxVelocity:=#DetachVel, AccDec:=#Synch_Acc, Jerk:=#Synch_Jerk);
	        ELSE
	            #TimeToDetach := 0.0;
	        END_IF;
	
	        #OutboundSpace  :=  2.0 * #SpaceToSync 
	                            + #Master_Vel / 60.0 * (#Work_CycleTime + #TimeToDetach)
	                            + #DetachSpace;
	
	        #OutboundTime   := (2.0 * #TimeToSync) + #TimeToDetach + #Work_CycleTime;
	
	        // Time to go back
	        #ReturnTime     := "Trapezoidal_MoveTime"(MoveSpace:=#OutboundSpace, MaxVelocity:=#BackHomeVel, AccDec:=#Synch_Acc, Jerk:=#Synch_Jerk);
	
	        // Total Time
	        #TotalTime      := 2.0 * #TimeToSync + #Work_CycleTime + #TimeToDetach + #ReturnTime;
	        #TotalLeadSpace := #Master_Vel / 60.0 * #TotalTime;
	
	        #deltaVel       /= 2.0;
	        IF #PartLength > #TotalLeadSpace THEN
	            IF #Master_Vel >= #FeedingCruiseVel THEN
	                EXIT;
	            ELSE
	                #Master_Vel += #deltaVel;
	            END_IF;
	        ELSE
	            #Master_Vel -= #deltaVel;
	        END_IF;
	
	 UNTIL #deltaVel < #VelTolerance END_REPEAT; 
	
	 #VelLimitByLength  := #Master_Vel;
	
	        
	
	        
	        
END_FUNCTION

