// Block: RollLubr_Valve
// Title: Reset Alarms

FUNCTION_BLOCK "RollLubr_Valve"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
// Reset Alarms
   VAR_INPUT
      AreaInterface : AreaInterface;
      DataIn : Struct
         PbStartLubr : Bool;
         PbStopLubr : Bool;
         V_Fbk : Array[1..ROLL_LUBR_MAXVALVE] of Bool;
      END_STRUCT;
      CIn : Struct
         PumpRunning : Bool;
         PumpError : Bool;
         LubrReq : Bool;
         LubrRun : Bool;
         V_Enable : Array[1..ROLL_LUBR_MAXVALVE] of Bool;
      END_STRUCT;
      Config : Struct
         FbkExist : Bool;
         ManualCmdTimeOut : Time;
         V_Enabled_Mininum : Int;
      END_STRUCT;
   END_VAR

   VAR_OUTPUT
      MachineInterface : MachineInterface;
      COut : Struct
         PumpReq : Bool;
      END_STRUCT;
      DataOut : Struct
         V_Cmd : Array[1..ROLL_LUBR_MAXVALVE] of Bool;
      END_STRUCT;
   END_VAR

   VAR_IN_OUT
      Pers : udt_RollLubr_Valve_Pers;
   END_VAR

   VAR
      Ctrl : Struct
         ManualReq : Bool;
         LubrReq : Bool;
         ValveCmd : Bool;
      END_STRUCT;
      Sts : Struct
         AlarmsPresence : Bool;
         WarningsPresence : Bool;
         IsStandstill : Bool;
         WorkEnable : Bool;
         V_Enabled : Int;
         V_Alarm : Bool;
      END_STRUCT;
      Alarm : Struct
         V_Feedback : Array[1..ROLL_LUBR_MAXVALVE] of Bool;
         MinimumValveEnabled : Bool;
      END_STRUCT;
      Warning : Struct
         SystemNotON : Bool;
         V_Disabled : Array[1..ROLL_LUBR_MAXVALVE] of Bool;
      END_STRUCT;
      tonManualCycleReset : IEC_TIMER;
      tonValveOn : TonEna;
      tonValveOff : TonEna;
      Aux_PbStartLubr : Bool;
      Pe_PbStartLubr : Bool;
   END_VAR

   VAR_TEMP
      i : Int;
   END_VAR


BEGIN
   REGION "Reset Alarms"
      IF AreaInterface.RstAlarms THEN
          Alarm.MinimumValveEnabled := FALSE;
          FOR i := 1 TO ROLL_LUBR_MAXVALVE DO
              Alarm.V_Feedback[i] := FALSE;
          END_FOR;
      END_IF;

   END_REGION

   REGION "Pe"
      Pe_PbStartLubr := DataIn.PbStartLubr AND NOT Aux_PbStartLubr;
      Aux_PbStartLubr := DataIn.PbStartLubr;

   END_REGION

   REGION "Condition"
      Sts.WorkEnable := NOT AreaInterface.EStop AND NOT Sts.AlarmsPresence AND Pers.SystemOn;;

   END_REGION

   REGION "Manual"
      IF DataIn.PbStopLubr OR NOT AreaInterface.Man OR tonManualCycleReset.Q OR NOT Sts.WorkEnable THEN
          Ctrl.ManualReq := FALSE;
      ELSIF Pe_PbStartLubr AND AreaInterface.Man THEN
          Ctrl.ManualReq := TRUE;
      END_IF;

      tonManualCycleReset.(IN:=Ctrl.ManualReq,PT:=Config.ManualCmdTimeOut);
          

   END_REGION

   REGION "Valve Cycle"

      Ctrl.LubrReq := CIn.LubrReq OR Ctrl.ManualReq;

      IF tonValveOn.Q OR NOT Sts.WorkEnable OR NOT Ctrl.LubrReq THEN
          Ctrl.ValveCmd := FALSE;
      ELSIF Ctrl.LubrReq AND tonValveOff.Q AND CIn.PumpRunning THEN
          Ctrl.ValveCmd := TRUE;
      END_IF;

      tonValveOn(IN := Ctrl.ValveCmd,
                  PT := Pers.TimeOn,
                  ENA := CIn.LubrRun OR Ctrl.ManualReq);

      tonValveOff(IN := NOT Ctrl.ValveCmd,
                   PT := Pers.TimeOff,
                   ENA := CIn.LubrRun OR Ctrl.ManualReq);


      Sts.V_Enabled := 0;
      Sts.V_Alarm := FALSE;

      FOR i := 1 TO ROLL_LUBR_MAXVALVE DO
          DataOut.V_Cmd[i] := Ctrl.ValveCmd AND CIn.V_Enable[i] AND Pers.Enable_V[i];
          
          IF tonValveOn.Q AND
              DataOut.V_Cmd[i] AND
              NOT DataIn.V_Fbk[i] AND
              Config.FbkExist
          THEN
              Alarm.V_Feedback[i] := TRUE;
          END_IF;
          IF Alarm.V_Feedback[i] THEN
              Sts.V_Alarm := TRUE;
          END_IF;
        
          IF CIn.V_Enable[i] THEN
              Sts.V_Enabled += 1;
          END_IF;
          
          Warning.V_Disabled[i] := NOT Pers.Enable_V[i];
      END_FOR;

      IF Pers.SystemOn AND 
          Sts.V_Enabled < Config.V_Enabled_Mininum
      THEN
          Alarm.MinimumValveEnabled := TRUE;
      END_IF;

      Warning.SystemNotON := NOT Pers.SystemOn;

   END_REGION

   REGION "Status"
      Sts.AlarmsPresence := Alarm.MinimumValveEnabled OR Sts.V_Alarm OR CIn.PumpError;
      Sts.WarningsPresence := Warning.SystemNotON;
      Sts.IsStandstill := NOT Ctrl.ValveCmd;

   END_REGION

   REGION "Cout"
      COut.PumpReq := Ctrl.ValveCmd AND Pers.SystemOn;

   END_REGION

   REGION "Machine Interface"
      MachineInterface.AutReady := AreaInterface.Aut AND NOT Sts.AlarmsPresence;
      MachineInterface.Aborting := Sts.AlarmsPresence;
      MachineInterface.AckStopInPhase := AreaInterface.StopInPhase;
      MachineInterface.AckStopProgrammed := AreaInterface.StopProgrammed;
      MachineInterface.MotionsStandStill := Sts.IsStandstill;
      MachineInterface.AlarmsPresence := Sts.AlarmsPresence;
      MachineInterface.WarningPresence := Sts.WarningsPresence;

   END_REGION


END_FUNCTION_BLOCK
