FUNCTION_BLOCK "Prod_HALTED_Case_W_A_FB"
{ S7_Optimized_Access := 'TRUE' ; Published := 'FALSE' }
VERSION : 0.1
   VAR_INPUT 
      Check_MIssingCnd : Bool;   // Se il warning che informa motivo per cui non avanza la produzione è alto
      Check_Abort : Bool;   // Se un allarme ha mandato in abort la macchine e quindi interrompe il ciclo automatico
   END_VAR

   VAR_IN_OUT 
      Halted : "Prod_Halted";
      Messages : Array[*] of Bool;   // Intero array dei messaggi (warnings se Check_MIssingCnd; alarms se Check_Abort)
      Mask : Array[*] of Bool;   // Maschera dei messaggi da usare per gestione sospensione
   END_VAR

   VAR 
      i : Int;
      Indx_Messages_Min : Int;
      Indx_Messages_Max : Int;
      indx_Mask_Min : Int;
      indx_Mask_Max : Int;
   END_VAR


BEGIN
	IF #Halted.Code <> "IC_NonAttivo" THEN
	    RETURN;
	END_IF;
	
	IF NOT(#Check_MIssingCnd OR #Check_Abort) THEN
	    RETURN;
	END_IF;
	
	#Indx_Messages_Min := DINT_TO_INT(LOWER_BOUND(ARR := #Messages, DIM := 1));
	#Indx_Messages_Max := DINT_TO_INT(UPPER_BOUND(ARR := #Messages, DIM := 1));
	
	
	#indx_Mask_Min := DINT_TO_INT(LOWER_BOUND(ARR := #Mask, DIM := 1));
	#indx_Mask_Max := DINT_TO_INT(UPPER_BOUND(ARR := #Mask, DIM := 1));
	
	IF #indx_Mask_Min < #Indx_Messages_Min OR #indx_Mask_Max > #Indx_Messages_Max THEN
	    RETURN;
	END_IF;
	
	IF #Check_MIssingCnd THEN
	    
	    #Halted.Code := "IC_WariningMancanzaCondizioni";
	    
	ELSIF #Check_Abort THEN
	    #Halted.Code := "IC_PresenzaAllarme";
	END_IF;
	    
	    
	FOR #i := #indx_Mask_Min TO #indx_Mask_Max DO
	    IF #Messages[#i] AND #Mask[#i]  THEN
	        #Halted.Message_N := #i;
	        RETURN;
	    END_IF;
	END_FOR;
	
	
	
END_FUNCTION_BLOCK

