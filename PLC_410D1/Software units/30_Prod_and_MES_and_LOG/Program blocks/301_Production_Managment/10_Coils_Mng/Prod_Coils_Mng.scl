FUNCTION_BLOCK "Prod_Coils_Mng"
{ S7_Optimized_Access := 'TRUE' ; Published := 'FALSE' }
VERSION : 0.1
   VAR_INPUT 
      Coil_Production : "Prod_Coil_Area";
      Part_Production : "Prod_Part_Area";
      Par_CoilInterruption_ExtraMaterialNeedFor : Real;
   END_VAR

   VAR_IN_OUT 
      COut : "Prod_COut";
      Err_Msg411 : "MES_Err_&_Msg";
      Err_Msg413 : "MES_Err_&_Msg";
      Err_Msg423 : "MES_Err_&_Msg";
      Err_Msg431_FineLamieraRF : "MES_Err_&_Msg";
      Aspo_In_Linea_Prev_Values : "Coil_Item";
      Aspo_In_Carico_Prev_Values : "Coil_Item";
      Taglio_Finale_Prev_Values : "Coil_Item";
   END_VAR

   VAR 
      i : Int;
      NumToShift : UDInt;
      RETAVAL : Int;
      Aspo_In_Carico { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Int;
      Aspo_In_Linea { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Int;
      ton_Delete_AspoInCarico {InstructionName := 'TON_TIME'; LibVersion := '1.0'; S7_SetPoint := 'False'} : TON_TIME;
      ScartoFineRotolo_Esegui : Bool;
      ScartoFineRotolo_FAKE_Ptrk { S7_SetPoint := 'False'} : "PTrk_Item";
      MSG411_Update_Coil_Aspo_In_Linea : "Push_MSG411_Update_Coil_Aspo_In_Linea";
      MSG413_Update_Coil_Aspo_In_Carico : "Push_MSG413_Update_Coil_Aspo_In_Carico";
      MSG423_Update_Coil_Taglio_Finale : "Push_MSG423_Update_Coil_Taglio_Finale";
      MSG431_Update_Scarto_Fine_Lamiera : "Push_MSG431_Update_Scarto_Fine_Lamiera";
   END_VAR

   VAR_TEMP 
      Delete : Bool;
      Item_Swap : "Coil_Item";
   END_VAR


BEGIN
	
	IF #Coil_Production.Decoiler_InLine = 1
	THEN
	    #Aspo_In_Carico := 2;
	    #Aspo_In_Linea := 1;
	    
	ELSIF #Coil_Production.Decoiler_InLine = 2
	THEN
	    #Aspo_In_Carico := 1;
	    #Aspo_In_Linea := 2;
	END_IF;
	
	
	
	
	REGION COIL CHANGE IN-LINE / OUT-LINE
	    
	    
	    // Identifica Cambio aspo in carico / in linea
	    // 
	    // 
	    IF ("Production".Coil_Data.ASPO_IN_CARICO.C_ID > 0 OR "Production".Coil_Data.ASPO_IN_LINEA.C_ID > 0)
	        AND
	        "Production".Coil_Data.ASPO_IN_CARICO.Decoiler_N <> #Aspo_In_Carico
	        AND
	        "Production".Coil_Data.ASPO_IN_LINEA.Decoiler_N <> #Aspo_In_Linea
	    THEN
	        
	        // Aspo in Carico -> Aspo in linea
	        // 
	        #Item_Swap := "Production".Coil_Data.ASPO_IN_LINEA;
	        "Production".Coil_Data.ASPO_IN_LINEA := "Production".Coil_Data.ASPO_IN_CARICO;
	        "Production".Coil_Data.ASPO_IN_CARICO := #Item_Swap;
	        
	    END_IF;
	    
	    
	END_REGION
	
	
	
	
	REGION Assegna valori su coil dell'aspo in linea
	    
	    
	    IF "Production".Coil_Data.ASPO_IN_LINEA.C_ID > 0
	        AND
	        "Production".Coil_Data.ASPO_IN_LINEA.Decoiler_N = #Aspo_In_Linea
	    THEN
	        
	        IF #Coil_Production.Material_STS = 3 // Fine Lamiera
	        THEN
	            IF "ProductionMng_PERS".Pers.Data."C_ID_Aspo_In_Linea_@CoilEnd" = 0
	            THEN
	                "Production".Coil_Data.ASPO_IN_LINEA.Stato_Rotolo := "SR_FINE_LAMIERA";
	            END_IF;
	            
	            "ProductionMng_PERS".Pers.Data."C_ID_Aspo_In_Linea_@CoilEnd" := "Production".Coil_Data.ASPO_IN_LINEA.C_ID;
	            
	        ELSIF #Coil_Production.Material_STS = 4 // Taglio abort
	        THEN
	            IF "ProductionMng_PERS".Pers.Data."C_ID_Aspo_In_Linea_@AbortCut" = 0
	            THEN
	                "Production".Coil_Data.ASPO_IN_LINEA.Stato_Rotolo := "SR_INTERRUZIONE"; // Interruzione;
	            END_IF;
	            "ProductionMng_PERS".Pers.Data."C_ID_Aspo_In_Linea_@AbortCut" := "Production".Coil_Data.ASPO_IN_LINEA.C_ID;
	        ELSE
	            "Production".Coil_Data.ASPO_IN_LINEA.Stato_Rotolo := "SR_DATO_PRESENTE"; // 1 = In Uso
	            "ProductionMng_PERS".Pers.Data."C_ID_Aspo_In_Linea_@CoilEnd" := 0;
	            "ProductionMng_PERS".Pers.Data."C_ID_Aspo_In_Linea_@AbortCut" := 0;
	        END_IF;
	        
	        
	        "Production".Coil_Data.ASPO_IN_LINEA.MES_IDP := "Coil_Assign_MES_ID"();
	        
	        
	        "Production".Coil_Data.ASPO_IN_LINEA.Diametro_Rotolo := #Coil_Production.Coil_Diameter;
	        
	        
	        
	        
	        (*
	        IF #MES_ID_Temp <> '?' THEN
	            "Production".Coil_Data.ASPO_IN_LINEA.MES_IDP := #MES_ID_Temp;
	        ELSE
	            "Production".Coil_Data.ASPO_IN_LINEA.MES_IDP := '';
	        END_IF;
	        *)
	        
	        
	        REGION Gestione controllo spessore lamiera
	            
	            "Production".Coil_Data.ASPO_IN_LINEA.Spessore_Deroga_TKD := 0;
	            IF #Coil_Production.Deroga_Spessore_Errato
	            THEN
	                "Production".Coil_Data.ASPO_IN_LINEA.Spessore_Deroga_TKD := 1;
	            END_IF;
	            
	            "Production".Coil_Data.ASPO_IN_LINEA.Spessore_Errore := 0;
	            IF #Coil_Production.Spessore_Lamiera_Errato
	            THEN
	                "Production".Coil_Data.ASPO_IN_LINEA.Spessore_Errore := 1;
	            END_IF;
	            
	            "Production".Coil_Data.ASPO_IN_LINEA.Spessore_materiale_TAV := #Coil_Production.Spessore_Lamiera_Sensore_Valore;
	            
	            "Production".Coil_Data.ASPO_IN_LINEA.Spessore_materiale_TEV := #Coil_Production.Spessore_Lamiera_Errore_Valore;
	            
	        END_REGION
	        
	        
	        
	        REGION Gestione provino
	            
	            #COut.COIL_Stop_Provino := "Production".Coil_Data.ASPO_IN_LINEA.Provini_Eseguiti < "Production".Coil_Data.ASPO_IN_LINEA.Provini_Richiesti;
	            
	            
	            IF #Coil_Production.Provino_Eseguito
	            THEN
	                "Production".Coil_Data.ASPO_IN_LINEA.Provini_Eseguiti := "Production".Coil_Data.ASPO_IN_LINEA.Provini_Richiesti;
	            END_IF;
	            
	            #COut.COIL_ACK_Provino_Eseguito := #Coil_Production.Provino_Eseguito;
	            
	        END_REGION
	        
	        
	    ELSE
	        #COut.COIL_Stop_Provino := FALSE;
	        
	    END_IF;
	    
	END_REGION
	
	
	
	
	REGION Aspo in carico
	    
	    // Cancella sempre il MES_IDP
	    // 
	    "Production".Coil_Data.ASPO_IN_CARICO.MES_IDP := '';
	    
	    // Cancella tutti i dati aspo in carico (prima era in lavoro)
	    // 
	    #Delete :=
	    "Production".Coil_Data.ASPO_IN_CARICO.C_ID > 0
	    AND
	    "Production".Coil_Data.ASPO_IN_CARICO.Stato_Rotolo = "SR_FINE_LAMIERA" // Fine lamiera
	    AND
	    #MSG413_Update_Coil_Aspo_In_Carico.Msg413_Send.Status = 0 // non sta inviando messaggi
	    ;
	    
	    
	    
	    #ton_Delete_AspoInCarico(IN := #Delete,
	                             PT := T#5s);
	    
	    IF #ton_Delete_AspoInCarico.Q THEN
	        
	        "Production".Coil_Data.ASPO_IN_CARICO := "Production".Coil_Data.Empty;
	    END_IF;
	    
	    
	    
	END_REGION
	
	
	
	
	REGION Stop manca dati aspo in carico IMPEDISCE CAMBIO COIL 
	    
	    #COut.COIL_Stop_Manca_Dati_AspoInCarico := FALSE;
	    IF (
	        NOT #Coil_Production.Material_Presence_Straight_Entry
	        OR
	        NOT #Coil_Production.Material_Presence_Straight_Exit
	        OR
	        TRUE
	        )
	        AND
	        (
	        "Production".Coil_Data.ASPO_IN_CARICO.MES_CN = ''
	        OR
	        "Production".Coil_Data.ASPO_IN_CARICO.MES_HN = ''
	        )
	        AND
	        "ProductionMng_PERS".Pers.Data.MODE.MES_ON
	    THEN
	        #COut.COIL_Stop_Manca_Dati_AspoInCarico := TRUE;
	    END_IF;
	    
	    
	END_REGION
	
	REGION Stop manca dati aspo in linea IMPEDISCE INSERIMENTO LAMIERA
	    
	    #COut.COIL_Stop_Manca_Dati_AspoInLinea := FALSE;
	    IF (
	        #Coil_Production.Material_STS = 1 // Introduzione
	        OR
	        #Coil_Production.Material_STS = 2 // Full
	        )
	        AND
	        (
	        "Production".Coil_Data.ASPO_IN_LINEA.MES_CN = ''
	        OR
	        "Production".Coil_Data.ASPO_IN_LINEA.MES_HN = ''
	        )
	        AND
	        "ProductionMng_PERS".Pers.Data.MODE.MES_ON
	    THEN
	        #COut.COIL_Stop_Manca_Dati_AspoInLinea := TRUE;
	    END_IF;
	    
	    
	END_REGION
	
	
	
	REGION JB DATA WRITE / DELETE
	    
	    //########################################################################################################################
	    // SET DATI
	    //######################################################################################################################## 
	    //
	    IF #Coil_Production.Material_STS = 2 // Material status full load
	        AND
	        #Coil_Production.Material_Presence_Straight_Exit
	        AND
	        #Coil_Production.Material_Presence_Straight_Exit
	        AND
	        #Coil_Production.Material_Presence_JB_Exit
	        AND
	        #Part_Production.MaterialPresenceEntry
	        //AND
	        //"Production".Coil_Data.JB.C_ID <> "Production".Coil_Data.ASPO_IN_LINEA.C_ID
	        AND
	        "Production".Coil_Data.ASPO_IN_LINEA.C_ID > 0
	    THEN
	        "Production".Coil_Data.JB := "Production".Coil_Data.ASPO_IN_LINEA;
	    END_IF;
	    
	    //########################################################################################################################
	    // DELET DATI
	    //######################################################################################################################## 
	    // 
	    IF #Coil_Production.Material_STS = 0 // Material status Empty
	        AND
	        NOT #Coil_Production.Material_Presence_Straight_Entry
	        AND
	        NOT #Coil_Production.Material_Presence_Straight_Exit
	        AND
	        NOT #Coil_Production.Material_Presence_JB_Exit
	        AND
	        "Production".Coil_Data.JB.C_ID > 0
	    THEN
	        "Production".Coil_Data.JB := "Production".Coil_Data.Empty;
	    END_IF;
	    
	END_REGION
	
	
	
	
	REGION FINAL CUT DATA WRITE / DELETE
	    
	    
	    //########################################################################################################################
	    // SET DATI
	    //######################################################################################################################## 
	    //
	    IF #Part_Production.Material_Presence
	    THEN
	        
	        IF "Production".Coil_Data.JB.C_ID > 0
	            AND
	            (NOT #Coil_Production.Weld_Presence // Aspetto a cambiare rotolo quando memoria saldatura va a zero
	            OR
	            #Coil_Production.Weld_Missing_To_FinalCut < 0.0
	            )
	        THEN
	            // Cambio Codice Colata...Bisogna atrezzare lo stampo
	            // 
	            IF "Production".Coil_Data.JB.MES_HN <> "Production".Coil_Data.TAGLIO_FINALE.MES_HN
	                AND
	                "Production".O_ACT.Use_Trace_Mark_Tool
	            THEN
	                "ProductionMng_PERS".Pers.Data.SET_UP.STS_Man_SetUp.Ack_Eseguito_Y["PROD_SETUP_Y_MARCHIATURA_TRACCIABILITA"] := FALSE;
	            END_IF;
	            
	            
	            
	            // Push nuovo rotolo da buffer rotoli completati
	            // 
	            IF "Production".Coil_Data.TAGLIO_FINALE.C_ID <> "Production".Coil_Data.JB.C_ID
	            THEN
	                
	                // Shift in basso della lista..coil usciti
	                // 
	                #NumToShift := INT_TO_UDINT("PROD_COILS_OUT_ITEMS" - 1);
	                
	                
	                #RETAVAL := MOVE_BLK_VARIANT(
	                                             SRC := "Production".Coil_Data.OUT,
	                                             COUNT := #NumToShift,
	                                             SRC_INDEX := 0,
	                                             DEST_INDEX := 1,
	                                             DEST => "Production".Coil_Data.OUT);
	                
	                "Production".Coil_Data.OUT[1] := "Production".Coil_Data.TAGLIO_FINALE;
	                
	                
	                
	            END_IF;
	            
	            // Nuovo Rotolo al taglio finale
	            // 
	            "Production".Coil_Data.TAGLIO_FINALE := "Production".Coil_Data.JB;
	            
	            
	        END_IF;
	        
	        
	    END_IF;
	    
	    
	    
	    
	    //########################################################################################################################
	    // DELETE DATI
	    //########################################################################################################################
	    
	    IF NOT #Part_Production.MaterialPresenceEntry
	        AND
	        NOT #Part_Production.MaterialPresenceEntry
	        AND
	        NOT #Part_Production.MaterialPresenceExit
	        AND
	        "Production".Coil_Data.TAGLIO_FINALE.C_ID > 0
	    THEN
	        
	        // Push messaggio 431 scarto finale
	        // 
	        #ScartoFineRotolo_FAKE_Ptrk := "PTrk".Empty;
	        #ScartoFineRotolo_FAKE_Ptrk.Quality := "PART_BAD_END_OF_COIL";
	        #ScartoFineRotolo_FAKE_Ptrk.C_ID := "Production".Coil_Data.TAGLIO_FINALE.C_ID;
	        #ScartoFineRotolo_FAKE_Ptrk.PLen_Meas := LREAL_TO_REAL(#Part_Production.Material_Qty_FromLastCut) + #Par_CoilInterruption_ExtraMaterialNeedFor;
	        #ScartoFineRotolo_Esegui := TRUE;
	        
	        
	        // Push ultimi dati rotolo su buffer coils terminati
	        // 
	        
	        
	        // Shift in basso della lista..coil usciti
	        // 
	        #NumToShift := INT_TO_UDINT("PROD_COILS_OUT_ITEMS" - 1);
	        
	        
	        #RETAVAL := MOVE_BLK_VARIANT(
	   SRC := "Production".Coil_Data.OUT,
	   COUNT := #NumToShift,
	   SRC_INDEX := 0,
	   DEST_INDEX := 1,
	   DEST => "Production".Coil_Data.OUT);
	        
	        "Production".Coil_Data.OUT[1] := "Production".Coil_Data.TAGLIO_FINALE;
	        
	        
	        // Cancellazione dati rotolo
	        // 
	        "Production".Coil_Data.TAGLIO_FINALE := "Production".Coil_Data.Empty;
	        
	    END_IF;
	    
	    
	    
	    //########################################################################################################################
	    //
	    
	    // Cancella dati non significativi per taglio finale
	    // 
	    "Production".Coil_Data.TAGLIO_FINALE.Decoiler_N := 0;
	    "Production".Coil_Data.TAGLIO_FINALE.Stato_Rotolo := "SR_VUOTO";
	    "Production".Coil_Data.TAGLIO_FINALE.Provini_Richiesti := 0;
	    "Production".Coil_Data.TAGLIO_FINALE.Provini_Eseguiti := 0;
	    "Production".Coil_Data.TAGLIO_FINALE.Diametro_Rotolo := 0;
	    "Production".Coil_Data.TAGLIO_FINALE.Spessore_Errore := 0;
	    "Production".Coil_Data.TAGLIO_FINALE.Spessore_materiale_TEV := 0;
	    "Production".Coil_Data.TAGLIO_FINALE.Spessore_Deroga_TKD := 0;
	    
	    // STOP matrice stampo tracciabiità non attrezzata
	    // 
	    #COut.Part_RF_Stop_Matrice_Colata_Coil :=
	    "ProductionMng_PERS".Pers.Data.MODE.MES_ON
	    AND
	    #Part_Production.Material_Presence
	    AND
	    "Production".Coil_Data.TAGLIO_FINALE.C_ID > 0
	    AND
	    "Production".O_ACT.O_ID > 0
	    AND
	    "Production".O_ACT.Use_Trace_Mark_Tool
	    AND
	    (
	    NOT "ProductionMng_PERS".Pers.Data.SET_UP.STS_Man_SetUp.Ack_Eseguito_Y["PROD_SETUP_Y_MARCHIATURA_TRACCIABILITA"]
	    OR
	    // (begin) Inizio segnalazione già da quando il nuovo rotolo è saldato
	    (
	    "Production".Coil_Data.JB.C_ID > 0
	    AND
	    "Production".Coil_Data.TAGLIO_FINALE.C_ID > 0
	    AND
	    "Production".Coil_Data.JB.MES_HN <> "Production".Coil_Data.TAGLIO_FINALE.MES_HN
	    )
	    // (End) Inizio segnalazione già da quando il nuovo rotolo è saldato
	    );
	    
	    
	    
	END_REGION
	
	
	
	
	
	REGION PUSH Msg 411 / 413 / 423 / 431
	    
	    #MSG411_Update_Coil_Aspo_In_Linea(Msg411_Err := #Err_Msg411,
	                                      Prev_Data  := #Aspo_In_Linea_Prev_Values);
	    
	    #MSG413_Update_Coil_Aspo_In_Carico(Msg413_Err := #Err_Msg413,
	                                       Prev_Data  := #Aspo_In_Carico_Prev_Values);
	    
	    
	    
	    #MSG423_Update_Coil_Taglio_Finale(Msg423_Err := #Err_Msg423,
	                                      Prev_Data  := #Taglio_Finale_Prev_Values);
	    
	    
	    #MSG431_Update_Scarto_Fine_Lamiera(PTrk_FAKE  := #ScartoFineRotolo_FAKE_Ptrk,
	                                       Execute    := #ScartoFineRotolo_Esegui,
	                                       Msg431_Err := #Err_Msg431_FineLamieraRF);
	    
	    
	END_REGION
	
	
	
	
	
	
	
	
	
	
END_FUNCTION_BLOCK

