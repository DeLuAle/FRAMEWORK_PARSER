FUNCTION "Prod_Item_CalcData_Msg100" : DInt
{ S7_Optimized_Access := 'TRUE' ; Published := 'FALSE' }
VERSION : 0.1
   VAR_IN_OUT 
      Item_Prod : "Prod_Item";
      Prg : "PPrf_Data";
   END_VAR

   VAR_TEMP 
      RETVAL : Int;
      Tol_Min : Real;
      Tol_Max : Real;
   END_VAR


BEGIN
	
	#Item_Prod.Beam_Code := #Prg.GeneralData.Beam_Code;
	
	
	
	
	
	//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
	// CALCOLO LUNGHEZZE MIN / MAX
	//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ 
	
	#Tol_Min := "PLEN_TOL_MIN_DEFAULT";
	#Tol_Max := "PLEN_TOL_MAX_DEFAULT";
	
	IF (#Prg.GeneralData.Beam_Len_Tol_Max > #Prg.GeneralData.Beam_Len_Tol_Min)
	    AND
	    (#Prg.GeneralData.Beam_Len_Tol_Min <> 0.0 OR #Prg.GeneralData.Beam_Len_Tol_Max <> 0.0)
	THEN
	    #Tol_Min := #Prg.GeneralData.Beam_Len_Tol_Min;
	    #Tol_Max := #Prg.GeneralData.Beam_Len_Tol_Max;
	END_IF;
	
	#Item_Prod.Beams_Len_Min := #Item_Prod.Beams_Len + #Tol_Min;
	#Item_Prod.Beams_Len_Max := #Item_Prod.Beams_Len + #Tol_Max;
	
	#Item_Prod.Parts_Tol := ABS(#Tol_Max - #Tol_Min) / 2.0; // Salva tolleranza usata per controllo della lunghezza parti tagiate
	
	#Item_Prod.Parts_Len := #Item_Prod.Beams_Len - #Item_Prod.Parts_Tol; // Lunghezza parti ridotta per stare al centro della tolleranza
	
	
	//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
	// CONTATTORI PARTI / PEZZI
	//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
	
	#Item_Prod.Beams_ToGo := #Item_Prod.Beams_REQ;
	
	
	#Item_Prod.Parts_REQ := #Item_Prod.Beams_REQ;
	
	IF #Item_Prod.Beam_Code = 1 THEN
	    #Item_Prod.Parts_REQ := #Item_Prod.Parts_REQ * 2;
	END_IF;
	
	#Item_Prod.Parts_ToGo := #Item_Prod.Parts_REQ;
	
	
	//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
	// QUANTITA' MATERIALE RICHIESTO
	//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
	
	
	#Item_Prod.Material_Len_REQ := #Item_Prod.Parts_Len * DINT_TO_REAL(#Item_Prod.Parts_REQ);
	#Item_Prod.Material_Len_ToGo := #Item_Prod.Material_Len_REQ;
	
	
	
	//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
	// LAYOUT DEL PACCO
	//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
	
	// Se valore non scritto ovvero se da MES non c'è una misura specifica del pacco
	// 
	IF #Item_Prod.PackLayout_BeamsPerLayer <= 0 AND #Item_Prod.PackLayout_Layers_Qty <= 0 THEN
	    
	    #Item_Prod.PackLayout_BeamsPerLayer := #Prg.GeneralData.PackLayout_BeamsPerLayer;
	    #Item_Prod.PackLayout_Layers_Qty := #Prg.GeneralData.PackLayout_Layers_Qty;
	END_IF;
	
	
	#Prod_Item_CalcData_Msg100 := "PROD_FUN_RESULT_OK";
	
	
	
	
	
END_FUNCTION

