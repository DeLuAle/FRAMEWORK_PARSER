FUNCTION "Prod_Item_Check_O_Can_be_Deleted" : DInt
{ S7_Optimized_Access := 'TRUE' ; Published := 'FALSE' }
VERSION : 0.1
   VAR_INPUT 
      O_ID : DInt;
      MES_IDP : String;
      Enable_O_TODO : Bool;   // TRUE = Considera cancellabile ordini che sono su O_TODO
      Enable_O_ENDING : Bool;   // TRUE = Considera cancellabile ordini che sono su O_ENDING
   END_VAR

   VAR_OUTPUT 
      O_Position : DInt;
      O_Index : Int;
   END_VAR

   VAR_TEMP 
      Item : "Prod_Item";
      indx : Int;
      RETVAL_CHECK_O_POS : DInt;
      RETVAL_CHECK_PRESENCE : Int;
   END_VAR


BEGIN
	// Controlla se l'ordine può essere cancellato in base al suo stato.
	// 
	
	
	// Inizializza valore di ritorno
	// 
	#Prod_Item_Check_O_Can_be_Deleted := "PROD_FUN_RESULT_OK";
	
	#O_Position := "PROD_ERR_ORDER_NOT_FOUND";
	#O_Index := 1;
	
	
	// Ricerca posizione ordine
	// 
	#RETVAL_CHECK_O_POS := "Prod_item_GetData"(O_ID:=#O_ID, MES_IDP:=#MES_IDP, Item=>#Item, indx=>#indx);
	
	
	// Assegna in uscita la posizione dell'Item ricercato
	// 
	IF #RETVAL_CHECK_O_POS >= "PROD_ORDER_IS_AT_O_ACT" THEN
	    
	    #O_Position := #RETVAL_CHECK_O_POS;
	    #O_Index := #indx;
	    
	END_IF;
	
	
	
	// Errore perchè ordine non trovato
	// 
	IF #RETVAL_CHECK_O_POS < "PROD_FUN_RESULT_OK" THEN
	    #Prod_Item_Check_O_Can_be_Deleted := "ERR_O_CAN_NOT_BE_DELETED_NOT_PRESENT";
	    RETURN;
	END_IF;
	
	// Errore perchè ordine è in lavoro
	// 
	IF #RETVAL_CHECK_O_POS = "PROD_ORDER_IS_AT_O_ACT" THEN
	    #Prod_Item_Check_O_Can_be_Deleted := "ERR_O_CAN_NOT_BE_DELETED_IS_O_ACT";
	    RETURN;
	END_IF;
	
	(*
	// Errore perchè ordine è in via di completamento ma non sono stati tagliati tutti i SG e l'ordine è da MES..
	// .. quindi deve ritornare tra i TODO dove in quella posizione può essere cancellato
	// .. Se invece è un ordine HMI allora si può cancellare se non ci sono più posizioni tracking che lo usano
	// 
	IF #RETVAL_CHECK_O_POS = "PROD_ORDER_IS_AT_O_ENDING"
	THEN
	    IF #Item.Type = "PROD_ITEM_TYPE_MES" AND #Item.Parts_ToGo > 0 THEN
	        
	        #Prod_Item_Check_O_Can_be_Deleted :=
	        "ERR_O_CAN_NOT_BE_DELETED_IS_O_ENDING";
	        RETURN;
	    END_IF;
	END_IF;
	*)
	
	
	// Errore perchè ordine già uscito
	// 
	IF #RETVAL_CHECK_O_POS = "PROD_ORDER_IS_AT_O_OUT" THEN
	    #Prod_Item_Check_O_Can_be_Deleted := "ERR_O_CAN_NOT_BE_DELETED_IS_O_OUT";
	    RETURN;
	END_IF;
	
	
	//.. Da qui in poi passano "PROD_ORDER_IS_AT_O_TODO" / "PROD_ORDER_IS_AT_O_ENDING"
	//
	
	
	
	// Ricerca presenza pezzi nel P_TRK che usano questo Ordine
	// 
	#RETVAL_CHECK_PRESENCE := "PTrk_CheckPresence_O_ID_FAST"(O_ID := #Item.O_ID, Complete_Scan := TRUE);
	
	// Errore perchè ci sono pezzi sul tracking che usano questo ordine
	// 
	IF #RETVAL_CHECK_PRESENCE > 0 THEN
	    #Prod_Item_Check_O_Can_be_Deleted := "ERR_O_CAN_NOT_BE_DELETED_PTRK_PRESENCE";
	    RETURN;
	END_IF;
	
	// Ricerca presenza pezzi nel B_TRK che usano questo Ordine
	// 
	#RETVAL_CHECK_PRESENCE := "BTrk_CheckPresence_O_ID_FAST"(O_ID := #Item.O_ID, Complete_Scan := TRUE);
	
	// Errore perchè ci sono parti nel tracking che usano questo ordine
	// 
	IF #RETVAL_CHECK_PRESENCE > 0 THEN
	    #Prod_Item_Check_O_Can_be_Deleted := "ERR_O_CAN_NOT_BE_DELETED_BTRK_PRESENCE";
	    RETURN;
	END_IF;
	
	
	
	
	
	
	
	
	
	
	
	
	
	
END_FUNCTION

