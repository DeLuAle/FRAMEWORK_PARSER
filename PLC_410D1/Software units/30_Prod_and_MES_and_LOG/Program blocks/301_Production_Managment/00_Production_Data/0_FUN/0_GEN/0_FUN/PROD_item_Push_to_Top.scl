FUNCTION "PROD_item_Push_to_Top" : DInt
{ S7_Optimized_Access := 'TRUE' ; Published := 'FALSE' }
VERSION : 0.1
   VAR_INPUT 
      item : "Prod_Item";
      Evaluate_FULL : Bool;
   END_VAR

   VAR_IN_OUT 
      Buffer : Array[*] of "Prod_Item";
   END_VAR

   VAR_TEMP 
      myCounter : DInt;
      index_MIN : Int;
      index_MAX : Int;
      CORREZIONE_INDICE_0 : Int;   // Indice base-0 per MOVE_BLK_VARIANT
      i : Int;
      RETVAL : DInt;
      RETVAL_int : Int;
      NumToShift : UDInt;
   END_VAR

   VAR CONSTANT 
      MAX_CLEAR_GAP_ITERATIONS : Int := 5;
   END_VAR


BEGIN
	// Dimensione del buffer da inviare
	// 
	#index_MIN := DINT_TO_INT(LOWER_BOUND(ARR := #Buffer, DIM := 1));
	#index_MAX := DINT_TO_INT(UPPER_BOUND(ARR := #Buffer, DIM := 1));
	
	
	
	// Correzione indice zero per funzione MOVE_BLK_VARIANT
	// 
	// Counting at the parameters SRC_INDEX AND DEST_INDEX always begins with the low limit "0", regardless OF the later declaration OF the ARRAY."
	// 
	IF #index_MIN > 0 THEN
	    #CORREZIONE_INDICE_0 := - #index_MIN;
	END_IF;
	
	
	// Pulisci i gap
	// 
	
	FOR #i := #index_MIN TO #index_MAX DO
	    
	   
	    
	    #RETVAL := "PROD_item_Clear_GAP"(#Buffer);
	    
	    IF #RETVAL <> "PROD_FUN_RESULT_CLEAR_GAP_DONE" THEN
	        EXIT;
	    END_IF;
	    
	    IF #i >= #MAX_CLEAR_GAP_ITERATIONS THEN
	        EXIT;
	    END_IF;
	    
	    
	END_FOR;
	
	
	// Uscita per buffer pieno e non permettere di fare push su buffer pieno
	// 
	// 
	IF #RETVAL < 0 AND #Evaluate_FULL THEN
	    
	    #PROD_item_Push_to_Top := #RETVAL;
	    
	    RETURN;
	    
	END_IF;
	
	
	// Shift in alto del buffer..Posizione più vecchia viene eliminato...Prima posizione è duplicata
	// 
	#NumToShift := INT_TO_UDINT(#index_MAX - #index_MIN);
	
	IF #NumToShift > 0 THEN
	    #RETVAL_int := MOVE_BLK_VARIANT(
	                                SRC := #Buffer,
	                                COUNT := #NumToShift,
	                                SRC_INDEX := 1 + #CORREZIONE_INDICE_0,
	                                DEST_INDEX := 2 + #CORREZIONE_INDICE_0,
	                                DEST => #Buffer);
	    
	    // Uscita per errore shift
	    // 
	    IF #RETVAL_int <> 0 THEN
	        #PROD_item_Push_to_Top := "PROD_ERR_BUFFER_ORDERs_SHIFT";
	        RETURN;
	    END_IF;
	END_IF;
	
	// Push su prima posizione
	// 
	#Buffer[#index_MIN] := #item;
	
	
	
	// RETVAL OK 
	//
	#PROD_item_Push_to_Top := "PROD_FUN_RESULT_OK";
	
	
	
	
	
	
	
END_FUNCTION

