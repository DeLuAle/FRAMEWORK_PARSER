FUNCTION "MES_Msg423_Push" : DInt
{ S7_Optimized_Access := 'TRUE' ; Published := 'FALSE' }
VERSION : 0.1
   VAR_INPUT 
      MES_IDI : DInt;   // ID del messaggio MES→PLC che ha richiesto l’interrogazione
   END_VAR

   VAR_IN_OUT 
      Err : "MES_Err_&_Msg";
   END_VAR

   VAR_TEMP 
      RETVAL : DInt;   // Valore di ritorno della funzione di ricerca dati ItemProd usando ID_Prod (PLC) 
      strPayload : String;   // Stringa Payload per formare il messaggio
      Msg : "MES_Msg";
      CN : String;
      HN : String;
      IDP : String;
   END_VAR

   VAR CONSTANT 
      NUMERO_MESSAGGIO : UDInt := 423;
   END_VAR


BEGIN
	REGION INFO
	    (* 
	    
	    PLC → MES – Topic = 423: Stato rotolo al taglio finale
	    
	    Il messaggio viene inviato:
	    •   non appena il rotolo cambia di stato, oppure
	    •   in risposta a una richiesta di interrogazione del MES (Topic = 422).
	    
	    Payload
	    
	    Chiave  Formato Descrizione
	    [IDI]   N0  D del messaggio MES → PLC che ha richiesto l’interrogazione. Se = 0, il messaggio è stato inviato spontaneamente dal PLC.
	    [CN]    N0  Numero identificativo del rotolo
	    [HN]    N0  Numero di colata
	    [IDP]   N0  ID dell’ordine che sta utilizzando il rotolo.
	
	    
	    
	    *)
	    
	    
	END_REGION
	
	
	
	REGION INIT
	    
	    // RETURN value = 0 Fine esecuzione senza errori
	    // 
	    
	    #MES_Msg423_Push := "PROD_FUN_RESULT_OK";
	    #Err.Presence := FALSE;
	    
	END_REGION
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	REGION PayLoad creation
	    
	    
	    //@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
	    //
	    // CREAZIONE PAYLOAD
	    // 
	    //@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@    
	    
	    // Creazione payload
	    #strPayload := '';
	    
	    // [IDI]   ID del messaggio MES → PLC che ha richiesto l’interrogazione. Se = 0, il messaggio è stato inviato spontaneamente dal PLC.
	    // 
	    IF #MES_IDI > 0 THEN
	        
	        #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_IDI");
	        #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := #MES_IDI, NDec := 0));
	        #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    ELSE
	        #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_IDI");
	        #strPayload := CONCAT(IN1 := #strPayload, IN2 := '0');
	        #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    END_IF;
	    
	    
	    
	    
	    
	    
	    // [CN] Numero identificativo del rotolo
	    //
	    #CN := "Production".Coil_Data.TAGLIO_FINALE.MES_CN;
	    IF #CN = '' THEN
	        #CN := 'vuoto';
	    END_IF;
	    
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_CN");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 :=#CN);
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    // [HN] Numero di colata
	    // 
	    #HN := "Production".Coil_Data.TAGLIO_FINALE.MES_HN;
	    IF #HN = '' THEN
	        #HN := 'vuoto';
	    END_IF;
	    
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_HN");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := #HN);
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    
	    // [IDP] ID dell’ordine che sta utilizzando il rotolo.
	    // 
	    #IDP := "Production".O_ACT.MES_IDP;
	    IF #IDP = '' THEN
	        #IDP := 'vuoto';
	    END_IF;
	    
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_IDP");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := #IDP);
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	   
	    
	    
	  
	    
	    
	    
	    
	    
	END_REGION ;
	
	
	
	REGION PUSH messaggio
	    
	    // Incrementa ultimo ID
	    // 
	    "MES_Send_Buffer_Msg_Topic4XX".lastId := "Inc_DINT_Circ"("MES_Send_Buffer_Msg_Topic4XX".lastId);
	    
	    
	    // Prepararazione messaggio
	    // 
	    #Msg.Id := "MES_Send_Buffer_Msg_Topic4XX".lastId;
	    #Msg.timestamp := "Conv_DT_to_STRING"("Now"());
	    #Msg.topic := #NUMERO_MESSAGGIO;
	    #Msg.payLoad := #strPayload;
	    
	    
	    // Push messaggio
	    // 
	    #RETVAL := "MES_Buffer_Push"(Msg := #Msg,
	                                 Evaluate_FULL := "ProductionMng_PERS".Pers.Data.MODE.MES_ON,
	                                 Buffer_msg := "MES_Send_Buffer_Msg_Topic4XX".msg);
	    
	    
	    
	    // Push messaggio LOG
	    // 
	    "IPC_Push_Log_MES"(Msg := #Msg,
	                       Ack := #RETVAL);
	    
	    
	    IF #RETVAL < "PROD_FUN_RESULT_OK" THEN
	        
	        #MES_Msg423_Push := #RETVAL;
	        
	        #Err := "MES_Error_Set"(msg_RETVAL := #RETVAL, msg := #Msg);
	        RETURN;
	    END_IF;
	    
	    
	    // Return value = OK
	    // 
	    #MES_Msg423_Push := "PROD_FUN_RESULT_OK";
	    RETURN;
	END_REGION
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
END_FUNCTION

