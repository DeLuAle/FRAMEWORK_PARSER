FUNCTION "MES_Msg211_Push" : DInt
{ S7_Optimized_Access := 'TRUE' ; Published := 'TRUE' }
VERSION : 0.1
// COSTRUISCE IL PAYLOAD PER IL MESSAGGIO TOPIC = 101 (INIZIO SET-UP)
   VAR_INPUT 
      MES_IDI : DInt;   // ID del messaggio MES→PLC che ha richiesto l’interrogazione
   END_VAR

   VAR_IN_OUT 
      Err : "MES_Err_&_Msg";
   END_VAR

   VAR_TEMP 
      RETVAL : DInt;   // Valore di ritorno della funzione di ricerca dati ItemProd usando ID_Prod (PLC) 
      Msg : "MES_Msg";
      strPayload : String;   // Stringa Payload per formare il messaggio
      item_Prod : "Prod_Item";   // Item (Batch) di produzione da dove sono stati prelevati i dati inviati
      O_ID : DInt;
      O_ID_Index : Int;
      BaiaScarico_Vuota : Bool;
      IDP : String;   // Identificativo univoco dell’ordine di produzione lato PLC
      SPU_Num : Int;   // Stato pacco su baia di uscita ( codice 0,1,2,3,4)
      QTY_Num : Int;   // Quantità di pezzi richiesti nel pacco
      QG_Num : Int;   // Numero di pezzi depositati nel pacco
      NPR_Num : Int;   // Schema di pallettizzazione: numero di pezzi per strato
      NPL_Num : Int;   // Schema di pallettizzazione: numero di strati previsti
      QPR_Num : Int;   //  Numero di pezzi nella riga (strato) attualmente non completata
      QPL_Num : Int;   // Numero di strati completamente completati
   END_VAR

   VAR CONSTANT 
      NUMERO_MESSAGGIO : UDInt := 211;
   END_VAR


BEGIN
	
	REGION NOTE
	    
	    (*
	    
	    PLC → MES – Topic = 211: Stato del pacco su baia di uscita
	    Il messaggio viene inviato:
	    •   non appena il pacco su baia di uscita cambia stato, oppure
	    •   in risposta a una richiesta di interrogazione del MES (Topic = 210).
	    Payload
	    Chiave  Formato Descrizione
	    [IDI]   N0  ID del messaggio MES → PLC che ha richiesto l’interrogazione. Se = 0, il messaggio è stato inviato spontaneamente dal PLC.
	    [IDP]   C   Identificativo univoco che indentifica l’ordine di produzione lato PLC
	    [SPU]   N0  Stato pacco su baia di uscita (vedi codici sotto)
	    [QTY]   N0  Quantità di pezzi richiesta per il pacco
	    [QG]    N0  Numero di pezzi effettivamente depositati nel pacco
	    [NPR]   N0  Schema di pallettizzazione: numero di pezzi per strato
	    [NPL]   N0  Schema di pallettizzazione: numero di strati
	    [QPR]   N0  Numero di pezzi nella riga (strato) attualmente non completata
	    [QPL]   N0  Numero di strati completati
	    
	    Codici possibili per [SPU] – Stato pacco su baia di uscita
	    •   0 – Vuoto
	    •   1 – Concluso ma non in posizione di reggiatura (navetta in movimento)
	    •   2 – Concluso e in posizione di reggiatura
	    •   3 – Reggiatura conclusa e pacco rimosso
	    •   4 – In attesa di cancellazione dei dati dopo la rimozione del pacco
	        
	    *)
	    
	END_REGION ;
	
	
	
	
	REGION InitData
	    
	    #strPayload := '';
	    #Msg.Id := "MES_Send_Buffer_Msg_Topic2XX".lastId;
	    #Msg.timestamp := "Conv_DT_to_STRING"("Now"());
	    #Msg.topic := #NUMERO_MESSAGGIO;
	    #Msg.payLoad := #strPayload;
	    
	    #IDP := 'vuoto';
	    #SPU_Num := 0;
	    #QTY_Num := 0;
	    #QG_Num := 0;
	    #NPR_Num := 0;
	    #NPL_Num := 0;
	    #QPR_Num := 0;
	    #QPL_Num := 0;
	    
	    #item_Prod := "Production".O_Empty;
	    
	    #O_ID := 0;
	    
	    
	END_REGION
	
	
	
	REGION Get ProdItem
	    
	    
	    //@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
	    //
	    // Trova item di produzione corrispondente al ID_PROD (PLC)
	    // 
	    //@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
	    
	    #O_ID := "BTrk".A0304.O_ID;
	    
	    IF #O_ID > 0 THEN
	        
	        // Trova ordine
	        // 
	        #RETVAL := "Prod_item_GetData"(O_ID := #O_ID, MES_IDP := '', Item => #item_Prod, indx => #O_ID_Index);
	        
	        
	        // Return value = #FAIL se non è stato trovato item di produzione 
	        // 
	        IF #RETVAL < "PROD_FUN_RESULT_OK" THEN
	            #MES_Msg211_Push := #RETVAL;
	            #Err := "MES_Error_Set"(msg_RETVAL := #RETVAL, msg := #Msg);
	            RETURN;
	        END_IF;
	        
	        
	        // ID produzione MES a PLC
	        // 
	        #IDP := #item_Prod.MES_IDP;
	        
	        
	        // Verifica se la baia di preparazione è vuota
	        // 
	        #BaiaScarico_Vuota := TRUE;
	        
	        IF "BTrk".A0304.O_ID > 0 THEN
	            
	            #BaiaScarico_Vuota := FALSE;
	            
	        END_IF;
	        
	        
	        
	        // Usa info tracking stacker 
	        //
	        IF NOT #BaiaScarico_Vuota THEN
	            
	            // Stato pacco
	            // 
	            (*
	         Codici possibili per [SPU] – Stato pacco su baia di uscita
	        •   0 – Vuoto
	        •   1 – Concluso ma non in posizione di reggiatura (navetta in movimento)
	        •   2 – Concluso e in posizione di reggiatura
	        •   3 – Reggiatura conclusa e pacco rimosso
	        •   4 – In attesa di cancellazione dei dati dopo la rimozione del pacco
	        *)
	            
	            IF "BTrk".A0304.PaccoRimosso
	            THEN
	                #SPU_Num := 4;
	                
	            ELSIF "BTrk".A0304.ReggiaturaConclusa
	            THEN
	                #SPU_Num := 3;
	                
	            ELSIF "BTrk".A0304.NavettaInReggiatura AND NOT "BTrk".A0304.Empty
	            THEN
	                #SPU_Num := 2;
	                
	            ELSIF NOT "BTrk".A0304.Empty
	            THEN
	                #SPU_Num := 1;
	            ELSE
	                #SPU_Num := 0;
	            END_IF;
	            
	            
	            // Schema di pallettizzazione: numero di pezzi per strato
	            //
	            #NPR_Num := #item_Prod.PackLayout_BeamsPerLayer;
	            
	            // Schema di pallettizzazione: numero di strati previsti
	            //
	            #NPL_Num := #item_Prod.PackLayout_Layers_Qty;
	            
	            // Calcolo Quantità di pezzi richiesti nel pacco
	            // 
	            #QTY_Num := #item_Prod.PackLayout_BeamsPerLayer * #item_Prod.PackLayout_Layers_Qty;
	            
	            // Numero di pezzi depositati nel pacco
	            // 
	            #QG_Num := "BTrk".A0304.Beams_Qty;
	            
	            
	            IF #NPR_Num > 0 THEN
	                // Numero di strati completamente completati
	                //
	                #QPL_Num := #QG_Num / #NPR_Num;
	                
	                //  Numero di pezzi nella riga (strato) attualmente non completata
	                //
	                #QPR_Num := #QG_Num MOD #NPR_Num;
	            END_IF;
	            
	        END_IF;
	    END_IF;
	END_REGION ;
	
	
	REGION PayLoad creation
	    
	    //@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
	    //
	    // CREAZIONE PAYLOAD
	    // 
	    //@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@  
	    
	    #strPayload := '';
	    
	    // [IDI]   ID del messaggio MES → PLC che ha richiesto l’interrogazione. Se = 0, il messaggio è stato inviato spontaneamente dal PLC.
	    // 
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_IDI");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := #MES_IDI, NDec := 0));
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    // [IDP] Identificativo univoco che indentifica l’ordine di produzione nel PLC.
	    // 
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_IDP");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := #IDP);
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    
	    
	    // [SPU] Stato pacco su baia di uscita
	    // 
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_SPU");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := #SPU_Num, NDec := 0));
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    // [QTY] Quantità di pezzi richiesti nel pacco
	    // 
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_QTY");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := #QTY_Num, NDec := 0));
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    // [QG] Numero di pezzi depositati nel pacco
	    // 
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_QG");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := #QG_Num, NDec := 0));
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    // [NPR] Schema di pallettizzazione: numero di pezzi per strato
	    // 
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_NPR");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := #NPR_Num, NDec := 0));
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    // [NPL] Schema di pallettizzazione: numero di strati previsti
	    // 
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_NPL");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := #NPL_Num, NDec := 0));
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    
	    // [QPR] Numero di pezzi nella riga (strato) attualmente non completata
	    //
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_QPR");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := #QPR_Num, NDec := 0));
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    // [QPL] Numero di strati completamente completati
	    // 
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_QPL");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := #QPL_Num, NDec := 0));
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    
	END_REGION ;
	
	
	
	
	REGION PUSH messaggio
	    
	    // Incrementa ultimo ID
	    // 
	    "MES_Send_Buffer_Msg_Topic2XX".lastId := "Inc_DINT_Circ"("MES_Send_Buffer_Msg_Topic2XX".lastId);
	    
	    
	    // Prepararazione messaggio
	    // 
	    #Msg.Id := "MES_Send_Buffer_Msg_Topic2XX".lastId;
	    #Msg.timestamp := "Conv_DT_to_STRING"("Now"());
	    #Msg.topic := #NUMERO_MESSAGGIO;
	    #Msg.payLoad := #strPayload;
	    
	    
	    // Push messaggio
	    // 
	    #RETVAL := "MES_Buffer_Push"(Msg := #Msg,
	                                 Evaluate_FULL := "ProductionMng_PERS".Pers.Data.MODE.MES_ON,
	                                 Buffer_msg := "MES_Send_Buffer_Msg_Topic2XX".msg);
	    
	    // Push messaggio LOG
	    // 
	    "IPC_Push_Log_MES"(Msg := #Msg,
	                       Ack := #RETVAL);
	    
	    
	    IF #RETVAL < "PROD_FUN_RESULT_OK" THEN
	        #MES_Msg211_Push := #RETVAL;
	        #Err := "MES_Error_Set"(msg_RETVAL := #RETVAL, msg := #Msg);
	        RETURN;
	    END_IF;
	    
	    
	    // Return value = OK
	    // 
	    #MES_Msg211_Push := "PROD_FUN_RESULT_OK";
	    RETURN;
	END_REGION
	
	
	
	
	
	
	
	
	
	
	
END_FUNCTION

