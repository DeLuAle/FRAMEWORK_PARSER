FUNCTION "MES_Msg144_Push" : DInt
{ S7_Optimized_Access := 'TRUE' ; Published := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      MES_IDI : DInt;   // ID del messaggio MES→PLC che ha richiesto l’interrogazione
   END_VAR

   VAR_IN_OUT 
      Err : "MES_Err_&_Msg";
   END_VAR

   VAR_TEMP 
      RETVAL : DInt;   // Valore di ritorno delle funzioni
      RETVAL_IPC : DInt;
      i : Int;
      Msg : "MES_Msg";
      strPayload : String;   // Stringa Payload per formare il messaggio
      IDP_Value : String;
      KEY_Name : String;
   END_VAR

   VAR CONSTANT 
      NUMERO_MESSAGGIO : UDInt := 144;
   END_VAR


BEGIN
	REGION NOTE
	    (*
	    
	    PLC → MES – Topic = 143: Lista ordini di produzione in coda
	    
	    Il messaggio contiene la lista degli ordini presenti nella coda di produzione, che quindi possono essere can-cellati.
	    •   Lo stato di ciascun ordine (SOR) può assumere i valori: 0, 1 o 7.
	    •   La lista è composta da N = 15 posizioni fisse, identificate da un indice da 01 a 15, ognuna delle quali rappresenta l’ID di un ordine.
	    •   Se una posizione non è valorizzata, al suo posto viene assegnata automaticamente la stringa "vuoto".
	    
	    ⚠️ Nota per il MES
	    •   La coda contiene 15 posizioni totali.
	    •   Il MES può caricare al massimo 10 ordini.
	    •   Le restanti 5 posizioni sono riservate agli ordini che ritornano in coda (re-inserimento automatico).
	    •   In questo modo, possono esserci al massimo 5 ordini di ritorno contemporaneamente.
	    
	    Payload
	    
	    Chiave      Formato Descrizione
	    [IDI]       N0      ID del messaggio MES→PLC che ha richiesto l’interrogazione. Se = 0, il messaggio è stato inviato spontaneamente dal PLC.
	    [IDP_06]    C       Identificativo univoco che indentifica l’ordine di produzione nel PLC.    
	    [IDP_07]    C   
	    [IDP_08]    C   
	    [IDP_09]    C         
	    [IDP_10]    C      
	         
	    *)
	    
	END_REGION ;
	
	REGION INIT
	    
	    #RETVAL := "PROD_FUN_RESULT_OK";
	    #MES_Msg144_Push := #RETVAL;
	    #Err.Presence := FALSE;
	    
	    
	    
	END_REGION
	
	
	
	
	
	
	
	
	
	REGION PayLoad creation
	    
	    
	    //@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
	    //
	    // CREAZIONE PAYLOAD
	    // 
	    //@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@    
	    
	    
	    #strPayload := '';
	    
	    IF #MES_IDI > 0 THEN
	        
	        #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_IDI");
	        #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := #MES_IDI, NDec := 0));
	        #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    ELSE
	        #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_IDI");
	        #strPayload := CONCAT(IN1 := #strPayload, IN2 := '0');
	        #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    END_IF;
	    
	    
	    IF "PROD_TODO_ITEMS" >= 10 THEN
	        
	        FOR #i := 6 TO 10 DO
	            
	            
	            #IDP_Value := "Production".O_TODO[#i].MES_IDP;
	            
	            IF "Production".O_TODO[#i].O_ID <= 0 THEN
	                #IDP_Value := 'vuoto';
	            END_IF;
	            
	            IF #i <= 9 THEN
	                #KEY_Name := CONCAT(IN1 := '[IDP_0', IN2 := "Conv_NUM_to_STRING"(NumericValue := #i, NDec := 0), IN3 := ']=');
	            ELSE
	                #KEY_Name := CONCAT(IN1 := '[IDP_', IN2 := "Conv_NUM_to_STRING"(NumericValue := #i, NDec := 0), IN3 := ']=');
	            END_IF;
	            
	            #strPayload := CONCAT(IN1 := #strPayload, IN2 := #KEY_Name);
	            #strPayload := CONCAT(IN1 := #strPayload, IN2 := #IDP_Value);
	            #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	            
	        END_FOR;
	    END_IF;
	    
	    
	END_REGION ;
	
	
	
	REGION PUSH messaggio
	    
	    // Incrementa ultimo ID
	    // 
	    "MES_Send_Buffer_Msg_Topic1XX".lastId := "Inc_DINT_Circ"("MES_Send_Buffer_Msg_Topic1XX".lastId);
	    
	    
	    // Prepararazione messaggio
	    // 
	    #Msg.Id := "MES_Send_Buffer_Msg_Topic1XX".lastId;
	    #Msg.timestamp := "Conv_DT_to_STRING"("Now"());
	    #Msg.topic := #NUMERO_MESSAGGIO;
	    #Msg.payLoad := #strPayload;
	    
	    
	    // Push messaggio
	    // 
	    #RETVAL := "MES_Buffer_Push"(Msg := #Msg, Evaluate_FULL := "ProductionMng_PERS".Pers.Data.MODE.MES_ON,
	                                 Buffer_msg := "MES_Send_Buffer_Msg_Topic1XX".msg);
	    
	    
	    // Push messaggio LOG
	    // 
	   "IPC_Push_Log_MES"(Msg := #Msg, Ack := #RETVAL);
	    
	    
	    IF #RETVAL < "PROD_FUN_RESULT_OK" THEN
	        #MES_Msg144_Push := #RETVAL;
	        #Err := "MES_Error_Set"(msg_RETVAL := #RETVAL, msg := #Msg);
	        RETURN;
	    END_IF;
	    
	    
	    // Return value = OK
	    // 
	    #MES_Msg144_Push := "PROD_FUN_RESULT_OK";
	    RETURN;
	END_REGION
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
END_FUNCTION

