FUNCTION "Trk_ListToCheck_Clear_Gap" : Int
{ S7_Optimized_Access := 'TRUE' ; Published := 'FALSE' }
VERSION : 0.1
   VAR_TEMP 
      i : Int;
      j : Int;
      indx_Empty : Int;
      indx_Busy : Int;
      All_Empty : Bool;
      All_Full : Bool;
      Gap_Found : Bool;
      NumToShift : UDInt;
      RETVAL : Int;
   END_VAR

   VAR CONSTANT 
      CORREZIONE_INDICE_0 : Int := -1;   // Indice base-0 per MOVE_BLK_VARIANT
      BUFFER_EMPTY : Int := 1;
      BUFFER_FULL_NOSPACE : Int := -503;
      BUFFER_ERROR_O_ENDING_SHIFT : Int := -502;
      "BUFFER_FUN_CLEAR-GAP_DONE" : Int := 0;
      "BUFFER_FUN_NO-GAP" : Int := 2;
   END_VAR


BEGIN
	#Trk_ListToCheck_Clear_Gap := -999;
	// Ricerca GAP = una posizione vuota e la seguente occupata
	//
	#All_Full := TRUE;
	#All_Empty := TRUE;
	#Gap_Found := FALSE;
	
	
	#indx_Empty := -1;
	#indx_Busy := -1;
	
	
	FOR #i := 1 TO "BTRK_A03_EXIT_TOCHECK_ITEMS" DO
	    
	    // Prima si trova la posizione vuota
	    IF "BTrk".A03_EXIT_TOCHECK.B[#i].B_ID <= 0 THEN
	        
	        #All_Full := FALSE;
	        
	        IF #indx_Empty <= 0 THEN
	            #indx_Empty := #i;
	            CONTINUE;
	        END_IF;
	    ELSE
	        
	        #All_Empty := FALSE;
	        
	        // Poi si trova la prima posizione occupata
	        IF #indx_Empty > 0 THEN
	            
	            IF #indx_Busy <= 0 THEN
	                #indx_Busy := #i;
	                #Gap_Found := TRUE;
	                EXIT;
	            END_IF;
	        END_IF;
	        
	    END_IF;
	END_FOR;
	
	// DEBUG
	#All_Full := #All_Full;
	#All_Empty := #All_Empty;
	#Gap_Found := #Gap_Found;
	
	#indx_Empty := #indx_Empty;
	#indx_Busy := #indx_Busy;
	    
	//
	
	// Tutto il buffer è vuoto
	// 
	IF #All_Empty THEN
	    #Trk_ListToCheck_Clear_Gap := #BUFFER_EMPTY;
	    RETURN;
	END_IF;
	
	// Tutto il buffer è pieno
	// 
	IF #All_Full THEN
	    #Trk_ListToCheck_Clear_Gap := #BUFFER_FULL_NOSPACE;
	    RETURN;
	END_IF;
	
	// // Buffer compatto: tutti gli occupati all'inizio, tutti i vuoti alla fine
	// 
	IF NOT #Gap_Found THEN
	    #Trk_ListToCheck_Clear_Gap := #"BUFFER_FUN_NO-GAP";
	    RETURN;
	END_IF;
	
	// Shift in alto del buffer..
	// 
	#NumToShift := INT_TO_UDINT("BTRK_A03_EXIT_TOCHECK_ITEMS" - #indx_Empty);
	
	
	#RETVAL := MOVE_BLK_VARIANT(
	                            SRC := "BTrk".A03_EXIT_TOCHECK.B,
	                            COUNT := #NumToShift,
	                            SRC_INDEX := #indx_Empty + 1 + #CORREZIONE_INDICE_0,
	                            DEST_INDEX := #indx_Empty + #CORREZIONE_INDICE_0,
	                            DEST => "BTrk".A03_EXIT_TOCHECK.B);
	
	
	
	// Uscita per errore shift
	// 
	IF #RETVAL <> 0 THEN
	    #Trk_ListToCheck_Clear_Gap := #BUFFER_ERROR_O_ENDING_SHIFT;
	    RETURN;
	END_IF;
	
	
	// cancellazione ultima posizione duplicata dopo lo shift
	// 
	"BTrk".A03_EXIT_TOCHECK.B["BTRK_A03_EXIT_TOCHECK_ITEMS"] := "BTrk".Empty;
	
	
	// Uscita per Gap rimosso    
	//
	#Trk_ListToCheck_Clear_Gap := #"BUFFER_FUN_CLEAR-GAP_DONE";
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
END_FUNCTION

