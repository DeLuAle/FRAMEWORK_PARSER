FUNCTION_BLOCK "A04_0304TrkManager_FB"
{ S7_Optimized_Access := 'TRUE' ; Published := 'FALSE' }
VERSION : 0.1
   VAR_INPUT 
      AreaInterface : "AreaInterface";
      SuperUserLogged : Bool;
   END_VAR

   VAR 
      HMI : Struct
         Move : Array[1..20] of "TrkMng_Move";
         NumDelete { S7_SetPoint := 'True'} : Int;
      END_STRUCT;
      Sts : Struct
         Move : Struct
            SelectionOn : Bool;
            SelectionNo : Int;
         END_STRUCT;
      END_STRUCT;
      TON_Delete_Buttons {InstructionName := 'IEC_TIMER'; LibVersion := '1.0'; S7_SetPoint := 'False'} : IEC_TIMER;
   END_VAR

   VAR_TEMP 
      i : Int;
      RetVal : Int;
      EmptyNFound : Int;
      ShiftN : UDInt;
   END_VAR

   VAR CONSTANT 
      MANIPULATOR : Int := 1;
      DEPOSIT : Int := 2;
      PREPARATION : Int := 3;
      TRANSFER : Int := 4;
      STACKER : Int := 5;
      SHUTTLE : Int := 6;
   END_VAR


BEGIN
	
	IF NOT #AreaInterface.Man THEN
	    #Sts.Move.SelectionNo := 0;
	    END_IF;
	    
	    #Sts.Move.SelectionOn := #Sts.Move.SelectionNo <> 0;
	    
	    FOR #i := 1 TO 20 DO
	        
	        //Tracking Select button
	        IF #HMI.Move[#i].SelButton THEN
	            IF #Sts.Move.SelectionNo = 0 OR #Sts.Move.SelectionNo <> #i THEN
	                #Sts.Move.SelectionNo := #i;
	            ELSE
	                #Sts.Move.SelectionNo := 0;
	            END_IF;
	            #HMI.Move[#i].SelButton := FALSE;
	            
	        END_IF;
	        
	        //Buttons Visibility
	
	        #HMI.Move[#i].DeleteButton_Visibile := #AreaInterface.Man AND #SuperUserLogged AND #Sts.Move.SelectionOn AND #Sts.Move.SelectionNo = #i;
	        #HMI.Move[#i].SelectButton_Selected := #Sts.Move.SelectionNo = #i;
	        
	    END_FOR;
	    
	 // Timer 5s alla pressione dei pulsanti delete    
	    #TON_Delete_Buttons.TON(IN := #HMI.Move[#MANIPULATOR].DeleteButton OR #HMI.Move[#DEPOSIT].DeleteButton OR #HMI.Move[#PREPARATION].DeleteButton OR #HMI.Move[#TRANSFER].DeleteButton OR #HMI.Move[#STACKER].DeleteButton OR #HMI.Move[#SHUTTLE].DeleteButton,
	                            PT := T#5S);
	    
	//Tracking Delete Button
	//
	    IF #HMI.Move[#MANIPULATOR].DeleteButton AND #TON_Delete_Buttons.Q  THEN
	        
	        IF "BTrk".A0302_EXIT.B["BTRK_A0302_MANIP_DEPOSITO_A0303_BASSO"].Quality = "GOOD_R"
	        THEN
	            "BTrk".A0302_EXIT.B["BTRK_A0302_MANIP_DEPOSITO_A0303_BASSO"].Quality := "GOOD_R_DEL";
	        ELSIF "BTrk".A0302_EXIT.B["BTRK_A0302_MANIP_DEPOSITO_A0303_BASSO"].Quality = "GOOD"
	        THEN
	            "BTrk".A0302_EXIT.B["BTRK_A0302_MANIP_DEPOSITO_A0303_BASSO"].Quality := "GOOD_DEL";
	        ELSE
	            "BTrk".A0302_EXIT.B["BTRK_A0302_MANIP_DEPOSITO_A0303_BASSO"] := "BTrk".Empty;
	        END_IF;
	        
	        #HMI.Move[#MANIPULATOR].DeleteButton := FALSE;
	        #Sts.Move.SelectionNo := 0;
	        
	        
	    ELSIF #HMI.Move[#DEPOSIT].DeleteButton AND #TON_Delete_Buttons.Q  THEN
	      
	        
	        IF "BTrk".A0303.Preparation.B.Quality= "GOOD_R"
	        THEN
	            "BTrk".A0303.Preparation.B.Quality := "GOOD_R_DEL";
	        ELSIF "BTrk".A0303.Preparation.B.Quality = "GOOD"
	        THEN
	            "BTrk".A0303.Preparation.B.Quality := "GOOD_DEL";
	        ELSE
	            "BTrk".A0303.Preparation.B := "BTrk".Empty;
	        END_IF;
	        
	        #HMI.Move[#DEPOSIT].DeleteButton := FALSE;
	        #Sts.Move.SelectionNo := 0;
	        
	        
	    ELSIF #HMI.Move[#PREPARATION].DeleteButton AND #TON_Delete_Buttons.Q THEN
	        IF #HMI.NumDelete = 0 THEN
	            "BTrk".A0303.Preparation.Layer := "BTrk".EmptyLayer;
	        ELSIF #HMI.NumDelete > 0 AND  #HMI.NumDelete <="BTRK_LAYER_ITEMS" THEN
	            
	            
	            
	            IF "BTrk".A0303.Preparation.Layer[#HMI.NumDelete].Quality = "GOOD_R"
	            THEN
	                "BTrk".A0303.Preparation.Layer[#HMI.NumDelete].Quality := "GOOD_R_DEL";
	            ELSIF "BTrk".A0303.Preparation.Layer[#HMI.NumDelete].Quality = "GOOD"
	            THEN
	                "BTrk".A0303.Preparation.Layer[#HMI.NumDelete].Quality := "GOOD_DEL";
	            ELSE
	                "BTrk".A0303.Preparation.Layer[#HMI.NumDelete] :="BTrk".Empty;
	            END_IF;
	            
	        END_IF;
	        #HMI.Move[#PREPARATION].DeleteButton := FALSE;
	        #Sts.Move.SelectionNo := 0;
	        #HMI.NumDelete := 0;
	        
	        
	        
	    ELSIF #HMI.Move[#TRANSFER].DeleteButton AND #TON_Delete_Buttons.Q  THEN
	        "BTrk".A0303.Transfert.Layer := "BTrk".EmptyLayer;
	        #HMI.Move[#TRANSFER].DeleteButton := FALSE;
	        #Sts.Move.SelectionNo := 0;
	        
	    ELSIF #HMI.Move[#STACKER].DeleteButton AND #TON_Delete_Buttons.Q  THEN
	        "BTrk".A0303.Stacker.Layers := "BTrk".EmptyLayers;
	        #HMI.Move[#STACKER].DeleteButton := FALSE;
	        #Sts.Move.SelectionNo := 0;
	    ELSIF #HMI.Move[#SHUTTLE].DeleteButton AND #TON_Delete_Buttons.Q  THEN
	        "BTrk".A0304.Layers := "BTrk".EmptyLayers;
	        #HMI.Move[#SHUTTLE].DeleteButton := FALSE;
	        #Sts.Move.SelectionNo := 0;
	    END_IF;
	    
	//Tracking Paste Button
	
	    IF #HMI.Move[#DEPOSIT].PasteButton THEN
	        "BTrk".A0303.Preparation.B := "BTrk".A0302_EXIT.B["BTRK_A0302_MANIP_DEPOSITO_A0303_BASSO"];
	        "BTrk".A0302_EXIT.B["BTRK_A0302_MANIP_DEPOSITO_A0303_BASSO"] := "BTrk".Empty;
	        #HMI.Move[#DEPOSIT].PasteButton := FALSE;
	        #Sts.Move.SelectionNo := 0;
	
	    ELSIF #HMI.Move[#PREPARATION].PasteButton THEN
	        IF #Sts.Move.SelectionNo = #DEPOSIT THEN
	            IF "BTrk".A0303.Preparation.Beam_Qty > 0 THEN
	                // Cerco se ci sono degli spazi vuoti dovuti da un delete del trk manuale
	                // 
	                FOR #i := 1 TO "BTrk".A0303.Preparation.Beam_Qty DO
	                    IF "BTrk".A0303.Preparation.Layer[#i].B_ID = 0 THEN
	                        #EmptyNFound := #i;
	                        EXIT;
	                    END_IF;
	                    #EmptyNFound := 0;
	                    
	                END_FOR;
	            END_IF;
	            IF #EmptyNFound > 0 THEN
	                #ShiftN := INT_TO_UDINT(#EmptyNFound);
	            ELSE
	                #ShiftN := INT_TO_UDINT("BTrk".A0303.Preparation.BeamsPerLayer_Target);
	            END_IF;
	            
	
	            #RetVal := MOVE_BLK_VARIANT(
	                                          SRC := "BTrk".A0303.Preparation.Layer,
	                                          COUNT := #ShiftN - 1,// numero di elementi da spostare
	                                          SRC_INDEX := 0,
	                                          DEST_INDEX := 1,
	                                          DEST => "BTrk".A0303.Preparation.Layer);
	            
	            "BTrk".A0303.Preparation.Layer[1] := "BTrk".A0303.Preparation.B;
	            "BTrk".A0303.Preparation.B := "BTrk".Empty;
	        ELSIF #Sts.Move.SelectionNo = #TRANSFER THEN
	            "BTrk".A0303.Preparation.Layer := "BTrk".A0303.Transfert.Layer;
	            "BTrk".A0303.Transfert.Layer:= "BTrk".EmptyLayer;
	        END_IF;
	        #HMI.Move[#PREPARATION].PasteButton := FALSE;
	        #Sts.Move.SelectionNo := 0;
	    ELSIF #HMI.Move[#TRANSFER].PasteButton THEN
	        "BTrk".A0303.Transfert.Layer := "BTrk".A0303.Preparation.Layer;
	        "BTrk".A0303.Preparation.Layer := "BTrk".EmptyLayer;
	        #HMI.Move[#TRANSFER].PasteButton := FALSE;
	        #Sts.Move.SelectionNo := 0;
	        
	       
	        
	    ELSIF #HMI.Move[#STACKER].PasteButton THEN
	        IF #Sts.Move.SelectionNo = #TRANSFER THEN
	            FOR #i := "BTrk".A0303.Stacker.Layers_Target - 1 TO 1 BY -1 DO
	                #RetVal := MOVE_BLK_VARIANT(
	                                            SRC := "BTrk".A0303.Stacker.Layers[#i],
	                                            COUNT := 1,
	                                            SRC_INDEX := 0,
	                                            DEST_INDEX := 0,
	                                            DEST => "BTrk".A0303.Stacker.Layers[#i + 1]);
	            END_FOR;
	            "BTrk".A0303.Stacker.Layers[1].Layer := "BTrk".A0303.Transfert.Layer;
	            "BTrk".A0303.Transfert.Layer := "BTrk".EmptyLayer;
	        ELSIF #Sts.Move.SelectionNo = #SHUTTLE THEN
	            "BTrk".A0303.Stacker.Layers := "BTrk".A0304.Layers;
	            "BTrk".A0304.Layers := "BTrk".EmptyLayers;
	        END_IF;
	        #HMI.Move[#STACKER].PasteButton := FALSE;
	        #Sts.Move.SelectionNo := 0;
	    ELSIF #HMI.Move[#SHUTTLE].PasteButton THEN
	        "BTrk".A0304.Layers := "BTrk".A0303.Stacker.Layers;
	        "BTrk".A0303.Stacker.Layers := "BTrk".EmptyLayers;
	        #HMI.Move[#SHUTTLE].PasteButton := FALSE;
	        #Sts.Move.SelectionNo := 0;
	    END_IF;
	    
	        
	     //Button Visibility
	     //
	
	    #HMI.Move[#DEPOSIT].PasteButton_Visibile := #AreaInterface.Man AND #SuperUserLogged AND #Sts.Move.SelectionOn AND NOT (#Sts.Move.SelectionNo = #DEPOSIT) AND (#Sts.Move.SelectionNo = #MANIPULATOR);
	    #HMI.Move[#PREPARATION].PasteButton_Visibile := #AreaInterface.Man AND #SuperUserLogged AND #Sts.Move.SelectionOn AND NOT (#Sts.Move.SelectionNo = #PREPARATION) AND ((#Sts.Move.SelectionNo = #DEPOSIT) OR (#Sts.Move.SelectionNo = #TRANSFER));
	    #HMI.Move[#TRANSFER].PasteButton_Visibile := #AreaInterface.Man AND #SuperUserLogged AND #Sts.Move.SelectionOn AND NOT (#Sts.Move.SelectionNo = #TRANSFER) AND ((#Sts.Move.SelectionNo = #PREPARATION) OR (#Sts.Move.SelectionNo = #STACKER)) AND "BTrk".A0303.Transfert.Empty;
	    #HMI.Move[#STACKER].PasteButton_Visibile := #AreaInterface.Man AND #SuperUserLogged AND #Sts.Move.SelectionOn AND NOT (#Sts.Move.SelectionNo = #STACKER) AND ((#Sts.Move.SelectionNo = #TRANSFER) OR (#Sts.Move.SelectionNo = #SHUTTLE)) ;
	    #HMI.Move[#SHUTTLE].PasteButton_Visibile := #AreaInterface.Man AND #SuperUserLogged AND #Sts.Move.SelectionOn AND NOT (#Sts.Move.SelectionNo = #SHUTTLE) AND (#Sts.Move.SelectionNo = #STACKER);
	    
	    #HMI.Move[#MANIPULATOR].SelectButton_Visibile := #AreaInterface.Man AND #SuperUserLogged AND NOT #HMI.Move[#MANIPULATOR].PasteButton_Visibile AND "BTrk".A0302_EXIT.B["BTRK_A0302_MANIP_DEPOSITO_A0303_BASSO"].B_ID > 0;
	    #HMI.Move[#DEPOSIT].SelectButton_Visibile := #AreaInterface.Man AND #SuperUserLogged AND NOT #HMI.Move[#DEPOSIT].PasteButton_Visibile AND "BTrk".A0303.Preparation.B.B_ID>0 ;
	    #HMI.Move[#PREPARATION].SelectButton_Visibile := #AreaInterface.Man AND #SuperUserLogged AND NOT #HMI.Move[#PREPARATION].PasteButton_Visibile AND "BTrk".A0303.Preparation.Beam_Qty>0;
	    #HMI.Move[#TRANSFER].SelectButton_Visibile := #AreaInterface.Man AND #SuperUserLogged AND NOT #HMI.Move[#TRANSFER].PasteButton_Visibile AND "BTrk".A0303.Transfert.Beams_Qty > 0;
	    #HMI.Move[#STACKER].SelectButton_Visibile := #AreaInterface.Man AND #SuperUserLogged AND NOT #HMI.Move[#STACKER].PasteButton_Visibile AND "BTrk".A0303.Stacker.Beams_Qty > 0;
	    #HMI.Move[#SHUTTLE].SelectButton_Visibile := #AreaInterface.Man AND #SuperUserLogged AND NOT #HMI.Move[#SHUTTLE].PasteButton_Visibile AND "BTrk".A0304.Layers_Qty > 0;
	    
	    
	    
	    
	    //Info Button
	    //
	    IF #HMI.Move[#MANIPULATOR].InfoButton THEN
	        "TRK_Info".HMI_Info := "BTrk".A0302_EXIT.B["BTRK_A0302_MANIP_DEPOSITO_A0303_BASSO"];
	        #HMI.Move[#MANIPULATOR].InfoButton := 0;
	    ELSIF #HMI.Move[#DEPOSIT].InfoButton THEN
	        "TRK_Info".HMI_Info := "BTrk".A0303.Preparation.B;
	        #HMI.Move[#DEPOSIT].InfoButton := 0;
	    ELSIF #HMI.Move[#PREPARATION].InfoButton THEN
	        IF #HMI.NumDelete > 0 AND #HMI.NumDelete < 13 THEN
	            "TRK_Info".HMI_Info := "BTrk".A0303.Preparation.Layer[#HMI.NumDelete];
	            #HMI.Move[#PREPARATION].InfoButton := 0;
	        END_IF;
	    END_IF;
	
	    
END_FUNCTION_BLOCK

