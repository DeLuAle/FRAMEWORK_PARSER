FUNCTION "MES_Msg201_Push" : DInt
{ S7_Optimized_Access := 'TRUE' ; Published := 'TRUE' }
VERSION : 0.1
// COSTRUISCE IL PAYLOAD PER IL MESSAGGIO TOPIC = 101 (INIZIO SET-UP)
   VAR_INPUT 
      MES_IDI : DInt;   // ID del messaggio MES→PLC che ha richiesto l’interrogazione
   END_VAR

   VAR_IN_OUT 
      Err : "MES_Err_&_Msg";
   END_VAR

   VAR_TEMP 
      RETVAL : DInt;
      Msg : "MES_Msg";
      strPayload : String;   // Stringa Payload per formare il messaggio
      item_Prod : "Prod_Item";   // Item (Batch) di produzione da dove sono stati prelevati i dati inviati
      O_ID : DInt;
      O_ID_Index : Int;
      BaiaPreparazione_Vuota : Bool;
      Usa_Info_Stacker : Bool;
      IDP : String;   // Identificativo univoco dell’ordine di produzione lato PLC
      SPP_Num : Int;   // Stato pacco (0 – Vuoto, 1 – Iniziato, 2 – Concluso)
      QTY_Num : Int;   // Quantità di pezzi richiesti nel pacco
      QG_Num : Int;   // Numero di pezzi depositati nel pacco
      NPR_Num : Int;   // Schema di pallettizzazione: numero di pezzi per strato
      NPL_Num : Int;   // Schema di pallettizzazione: numero di strati previsti
      QPR_Num : Int;   //  Numero di pezzi nella riga (strato) attualmente non completata
      QPL_Num : Int;   // Numero di strati completamente completati
   END_VAR

   VAR CONSTANT 
      NUMERO_MESSAGGIO : UDInt := 201;
   END_VAR


BEGIN
	
	REGION NOTE
	    (*
	    
	    PLC → MES – Topic = 201: Stato del pacco su baia di preparazione
	    
	    Il messaggio viene inviato:
	    •   non appena lo stato del pacco in preparazione cambia, oppure
	    •   in risposta a una richiesta di interrogazione del MES (Topic = 200).
	    
	    
	    Payload:
	    Chiave  Formato Descrizione
	    [IDI]   N0  ID del messaggio MES → PLC che ha richiesto l’interrogazione. Se = 0, il messaggio è stato inviato spontaneamente dal PLC.
	    [IDP]   C   Identificativo univoco dell’ordine di produzione lato PLC
	    [SPP]   N0  Stato pacco (vedi codici sotto)
	    [QTY]   N0  Quantità di pezzi richiesti nel pacco
	    [QG]    N0  Numero di pezzi depositati nel pacco
	    [NPR]   N0  Schema di pallettizzazione: numero di pezzi per strato
	    [NPL]   N0  Schema di pallettizzazione: numero di strati previsti
	    [QPR]   N0  Numero di pezzi nella riga (strato) attualmente non completata
	    [QPL]   N0  Numero di strati completamente completati
	    
	    Codici possibili per [SPP] – Stato del pacco
	    •   0 – Vuoto
	    •   1 – Iniziato
	    •   2 – Concluso
	    
	    
	    *)
	    
	END_REGION ;
	
	
	#RETVAL := "PROD_FUN_RESULT_OK";
	#MES_Msg201_Push := #RETVAL;
	#Err.Presence := FALSE;
	
	
	
	
	
	REGION INIT
	    
	    #strPayload := '';
	    #Msg.Id := "MES_Send_Buffer_Msg_Topic2XX".lastId;
	    #Msg.timestamp := "Conv_DT_to_STRING"("Now"());
	    #Msg.topic := #NUMERO_MESSAGGIO;
	    #Msg.payLoad := #strPayload;
	    
	    #IDP := 'vuoto';
	    #SPP_Num := 0;
	    #QTY_Num := 0;
	    #QG_Num := 0;
	    #NPR_Num := 0;
	    #NPL_Num := 0;
	    #QPR_Num := 0;
	    #QPL_Num := 0;
	    
	    #Usa_Info_Stacker := FALSE;
	    
	    #O_ID := 0;
	    
	END_REGION
	
	
	
	REGION Get ProdItem
	    
	    
	    //@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
	    //
	    // Trova item di produzione corrispondente al ID_PROD (PLC)
	    // 
	    //@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
	    
	    
	    
	    // Verifica se la baia di preparazione è vuota
	    // 
	    
	    IF "BTrk".A0303.Stacker.O_ID > 0
	    THEN
	        
	        #O_ID := "BTrk".A0303.Stacker.O_ID;
	        
	        #Usa_Info_Stacker := TRUE;
	        
	        IF "BTrk".A0303.Stacker.Beams_Qty > 0 THEN
	            // Stato pacco
	            //
	            #SPP_Num := 1; // Pacco iniziato  
	            
	            // Numero di pezzi depositati nel pacco
	            // 
	            #QG_Num := "BTrk".A0303.Stacker.Beams_Qty;
	            
	            
	        END_IF;
	        
	        
	    (*    
	    ELSIF "BTrk".A0303.Transfert.O_ID > 0
	    THEN
	        
	        #O_ID := "BTrk".A0303.Transfert.O_ID;
	        
	        IF "BTrk".A0303.Preparation.Beam_Qty > 0
	        THEN
	            
	            // Stato pacco
	            //
	            #SPP_Num := 1; // Pacco iniziato  
	            
	            // Numero di pezzi depositati nel pacco
	            // 
	            #QG_Num := "BTrk".A0303.Preparation.Beam_Qty;
	            
	        END_IF;
	        
	        
	        
	    ELSIF "BTrk".A0303.Preparation.O_ID > 0
	    THEN
	        
	        #O_ID := "BTrk".A0303.Preparation.O_ID;
	        
	        IF "BTrk".A0303.Preparation.Beam_Qty > 0
	        THEN
	            // Stato pacco
	            //
	            #SPP_Num := 1; // Pacco iniziato  
	            
	            // Numero di pezzi depositati nel pacco
	            // 
	            #QG_Num := "BTrk".A0303.Preparation.Beam_Qty;
	            
	        END_IF;
	     *)   
	    END_IF;
	    
	    
	    IF #O_ID > 0 THEN
	        
	        // Trova ordine
	        // 
	        #RETVAL := "Prod_item_GetData"(O_ID := #O_ID, MES_IDP := '', Item => #item_Prod, indx => #O_ID_Index);
	        
	        
	        // Return value = #FAIL se non è stato trovato item di produzione 
	        // 
	        IF #RETVAL < "PROD_FUN_RESULT_OK" THEN
	            #MES_Msg201_Push := #RETVAL;
	            #Err := "MES_Error_Set"(msg_RETVAL := #RETVAL, msg := #Msg);
	            RETURN;
	        END_IF;
	        
	            
	        // ID produzione MES a PLC
	        // 
	        #IDP := #item_Prod.MES_IDP;
	        
	        // Schema di pallettizzazione: numero di pezzi per strato
	        //
	        #NPR_Num := #item_Prod.PackLayout_BeamsPerLayer;
	        
	        // Schema di pallettizzazione: numero di strati previsti
	        //
	        #NPL_Num := #item_Prod.PackLayout_Layers_Qty;
	        
	        // Calcolo Quantità di pezzi richiesti nel pacco
	        // 
	        #QTY_Num := #item_Prod.PackLayout_BeamsPerLayer * #item_Prod.PackLayout_Layers_Qty;
	        
	        
	        // Usa info stacker
	        // 
	        IF #Usa_Info_Stacker THEN
	            
	            // Pacco concluso
	            // 
	            IF "BTrk".A0303.Stacker.Completato
	                OR
	                ("BTrk".A0303.Stacker.Beams_Qty >= #QTY_Num AND #QTY_Num > 0)
	            THEN
	                #SPP_Num := 2;
	            END_IF;
	            
	            
	            IF #NPR_Num > 0 THEN
	                
	                // Numero di strati completamente completati
	                #QPL_Num := #QG_Num / #NPR_Num;
	                
	                //  Numero di pezzi nella riga (strato) attualmente non completata
	                #QPR_Num := #QG_Num MOD #NPR_Num;
	            END_IF;
	            
	        END_IF;
	        
	        
	        
	    END_IF;
	    
	END_REGION ;
	
	
	
	
	
	REGION PayLoad creation
	    
	    //@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
	    //
	    // CREAZIONE PAYLOAD
	    // 
	    //@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@  
	    
	    #strPayload := '';
	    
	    // [IDI]   ID del messaggio MES → PLC che ha richiesto l’interrogazione. Se = 0, il messaggio è stato inviato spontaneamente dal PLC.
	    // 
	    IF #MES_IDI > 0 THEN
	        
	        #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_IDI");
	        #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := #MES_IDI, NDec := 0));
	        #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    ELSE
	        #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_IDI");
	        #strPayload := CONCAT(IN1 := #strPayload, IN2 := '0');
	        #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    END_IF;
	    
	    
	    // [IDP] Identificativo univoco che indentifica l’ordine di produzione nel PLC.
	    // 
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_IDP");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := #IDP);
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    
	    
	    // [SPP] Stato pacco (vedi codici sotto)
	    // 
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_SPP");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := #SPP_Num, NDec := 0));
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    // [QTY] Quantità di pezzi richiesti nel pacco
	    // 
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_QTY");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := #QTY_Num, NDec := 0));
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    // [QG] Numero di pezzi depositati nel pacco
	    // 
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_QG");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := #QG_Num, NDec := 0));
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    // [NPR] Schema di pallettizzazione: numero di pezzi per strato
	    // 
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_NPR");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := #NPR_Num, NDec := 0));
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    // [NPL] Schema di pallettizzazione: numero di strati previsti
	    // 
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_NPL");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := #NPL_Num, NDec := 0));
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    
	    // [QPR] Numero di pezzi nella riga (strato) attualmente non completata
	    //
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_QPR");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := #QPR_Num, NDec := 0));
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    // [QPL] Numero di strati completamente completati
	    // 
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_QPL");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_NUM_to_STRING"(NumericValue := #QPL_Num, NDec := 0));
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    
	END_REGION ;
	
	
	
	REGION PUSH messaggio
	    
	    // Incrementa ultimo ID
	    // 
	    "MES_Send_Buffer_Msg_Topic2XX".lastId := "Inc_DINT_Circ"("MES_Send_Buffer_Msg_Topic2XX".lastId);
	    
	    
	    // Prepararazione messaggio
	    // 
	    #Msg.Id := "MES_Send_Buffer_Msg_Topic2XX".lastId;
	    #Msg.timestamp := "Conv_DT_to_STRING"("Now"());
	    #Msg.topic := #NUMERO_MESSAGGIO;
	    #Msg.payLoad := #strPayload;
	    
	    
	    // Push messaggio
	    // 
	    #RETVAL := "MES_Buffer_Push"(Msg := #Msg,
	                                 Evaluate_FULL := "ProductionMng_PERS".Pers.Data.MODE.MES_ON,
	                                 Buffer_msg := "MES_Send_Buffer_Msg_Topic2XX".msg);
	    
	    // Push messaggio LOG
	    // 
	    "IPC_Push_Log_MES"(Msg := #Msg,
	                       Ack := #RETVAL);
	    
	    
	    IF #RETVAL < "PROD_FUN_RESULT_OK" THEN
	        #MES_Msg201_Push := #RETVAL;
	        #Err := "MES_Error_Set"(msg_RETVAL := #RETVAL, msg := #Msg);
	        RETURN;
	    END_IF;
	    
	    
	    // Return value = OK
	    // 
	    #MES_Msg201_Push := "PROD_FUN_RESULT_OK";
	    RETURN;
	   
	END_REGION
	
	
	
	
	
	
	
END_FUNCTION

