FUNCTION "MES_Msg101_Push" : DInt
{ S7_Optimized_Access := 'TRUE' ; Published := 'TRUE' }
VERSION : 0.1
// COSTRUISCE IL PAYLOAD PER IL MESSAGGIO TOPIC = 101 (INIZIO SET-UP)
   VAR_IN_OUT 
      Err : "MES_Err_&_Msg";
   END_VAR

   VAR_TEMP 
      RETVAL : DInt;   // Valore di ritorno della funzione di ricerca dati ItemProd usando ID_Prod (PLC) 
      RETVAL_IPC : DInt;
      strPayload : String;   // Stringa Payload per formare il messaggio
      Msg : "MES_Msg";
      ID_MES : String;   // ID di produzione MES
   END_VAR

   VAR CONSTANT 
      NUMERO_MESSAGGIO : UDInt := 101;
   END_VAR


BEGIN
	
	REGION NOTE
	    (*
	    PLC → MES – Topic = 101: Inizio set-up
	    il messaggio viene inviato quando un ordine viene messo in lavoro nel PLC, ma le macchine non sono ancora pronte per l’introduzione del materiale.
	    ________________________________________
	    Note sul funzionamento PLC
	    L’ordine può essere messo in lavoro nel PLC in due modalità:
	    1.  Automatica: secondo l’ordine di ricezione degli ordini da parte del PLC.
	    2.  Manuale: previa immissione di una password, è possibile selezionare manualmente un ordine qualsiasi, ignorando la sequenza definita dal MES. Questa modalità è utile in particolare durante le fasi di test o manutenzione.
	    
	    Una volta selezionato, l’ordine carica la ricetta associata al profilo, che contiene i target degli assi di formatura e le regolazioni meccaniche richieste.
	    
	    Se uno o più dispositivi non risultano ancora in posizione, il PLC richiede all’operatore di avviare il ciclo di set-up.
	    
	    Il ciclo di set-up può essere avviato tramite un pulsante sul pulpito di comando, a condizione che siano soddisfatti i requisiti di sicurezza (ad esempio: barriere chiuse, ecc.).
	    
	    Da definire: se il messaggio al MES debba essere inviato immediatamente dopo il caricamento della ricetta nel PLC, oppure solo all’avvio effettivo del ciclo di set-up.
	    
	    *)
	    
	END_REGION ;
	
	
	
	#MES_Msg101_Push := "PROD_FUN_RESULT_OK";
	#Err.Presence := FALSE;
	
	
	
	
	
	
	REGION PayLoad creation
	    
	    #ID_MES := "Production".O_ACT.MES_IDP;
	    
	    //@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
	    //
	    // CREAZIONE PAYLOAD
	    // 
	    //@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@  
	    
	    
	    
	    #strPayload := '';
	    
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_IDP");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := #ID_MES);
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	END_REGION ;
	
	
	
	REGION PUSH MSG101
	    
	    // Incrementa ultimo ID
	    // 
	    "MES_Send_Buffer_Msg_Topic1XX".lastId := "Inc_DINT_Circ"("MES_Send_Buffer_Msg_Topic1XX".lastId);
	    
	    
	    // Prepararazione messaggio
	    // 
	    #Msg.Id := "MES_Send_Buffer_Msg_Topic1XX".lastId;
	    #Msg.timestamp := "Conv_DT_to_STRING"("Now"());
	    #Msg.topic := #NUMERO_MESSAGGIO;
	    #Msg.payLoad := #strPayload;
	    
	    
	    // Push messaggio a MES
	    // 
	    #RETVAL := "MES_Buffer_Push"(Msg := #Msg,
	                                 Evaluate_FULL := "ProductionMng_PERS".Pers.Data.MODE.MES_ON,
	                                 Buffer_msg := "MES_Send_Buffer_Msg_Topic1XX".msg);
	    
	    
	    // Push messaggio LOG
	    // 
	    "IPC_Push_Log_MES"(Msg := #Msg,
	                       Ack := #RETVAL);
	    
	    
	    IF #RETVAL < "PROD_FUN_RESULT_OK" THEN
	        #MES_Msg101_Push := #RETVAL;
	        #Err := "MES_Error_Set"(msg_RETVAL:=#RETVAL, msg:=#Msg);
	        RETURN;
	    END_IF;
	    
	    
	    
	    
	    // Return value = OK
	    // 
	    #MES_Msg101_Push := "PROD_FUN_RESULT_OK";
	    RETURN;
	END_REGION
	
	
	
	
	
	
	
	
	
	
	
END_FUNCTION

