FUNCTION "MES_Msg102_Push" : DInt
{ S7_Optimized_Access := 'TRUE' ; Published := 'TRUE' }
VERSION : 0.1
// COSTRUISCE E INSERISCE IN CODA IL MESSAGGIO TOPIC = 102 (FINE SET-UP)
   VAR_IN_OUT 
      Err : "MES_Err_&_Msg";
   END_VAR

   VAR_TEMP 
      RETVAL : DInt;   // Valore di ritorno della funzione di ricerca dati ItemProd usando ID_Prod (PLC) 
      Msg : "MES_Msg";
      strPayload : String;   // Stringa Payload per formare il messaggio
      ID_MES : String;   // ID di produzione MES
   END_VAR

   VAR CONSTANT 
      NUMERO_MESSAGGIO : UDInt := 102;
   END_VAR


BEGIN
	
	REGION NOTE
	    (*
	    
	    PLC → MES – Topic = 102: Fine set-up
	   
	    Il messaggio viene inviato al termine del ciclo di set-up, per indicare che la macchina è pronta per l’introduzione del materiale.
	    Da definire: se il messaggio debba essere inviato immediatamente alla conclusione del ciclo di set-up, oppure solo quando inizia effettivamente l’introduzione della lamiera nella profilatrice.
	
	    *)
	    
	END_REGION ;
	
	
	
	
	
	
	
	
	
	#MES_Msg102_Push := "PROD_FUN_RESULT_OK";
	#Err.Presence := FALSE;
	
	
	
	
	
	
	
	
	REGION PayLoad creation
	    
	    
	    //@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
	    //
	    // CREAZIONE PAYLOAD
	    // 
	    //@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@  
	    
	    #ID_MES := "Production".O_ACT.MES_IDP;
	    
	    #strPayload := '';
	    
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_IDP");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := #ID_MES);
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	END_REGION ;
	
	
	
	
	
	
	
	
	
	REGION PUSH MSG102
	    
	    // Incrementa ultimo ID
	    // 
	    "MES_Send_Buffer_Msg_Topic1XX".lastId := "Inc_DINT_Circ"("MES_Send_Buffer_Msg_Topic1XX".lastId);
	    
	    
	    // Prepararazione messaggio
	    // 
	    #Msg.Id := "MES_Send_Buffer_Msg_Topic1XX".lastId;
	    #Msg.timestamp := "Conv_DT_to_STRING"("Now"());
	    #Msg.topic := #NUMERO_MESSAGGIO;
	    #Msg.payLoad := #strPayload;
	    
	    
	    // Push messaggio
	    // 
	    #RETVAL := "MES_Buffer_Push"(Msg := #Msg, Evaluate_FULL := "ProductionMng_PERS".Pers.Data.MODE.MES_ON,
	                                 Buffer_msg := "MES_Send_Buffer_Msg_Topic1XX".msg);
	    
	    
	    // Push messaggio LOG
	    // 
	    "IPC_Push_Log_MES"(Msg:=#Msg, Ack:=#RETVAL);
	    
	    
	    IF #RETVAL < "PROD_FUN_RESULT_OK" THEN
	        
	        #MES_Msg102_Push := #RETVAL;
	        #Err := "MES_Error_Set"(msg_RETVAL:=#RETVAL, msg:=#Msg);
	        
	        RETURN;
	    END_IF;
	    
	    
	    // Return value = OK
	    // 
	    #MES_Msg102_Push := "PROD_FUN_RESULT_OK";
	    
	END_REGION
	
	
	
	
	
END_FUNCTION

