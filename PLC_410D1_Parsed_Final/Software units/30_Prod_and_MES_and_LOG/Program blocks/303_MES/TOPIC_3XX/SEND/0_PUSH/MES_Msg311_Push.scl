FUNCTION "MES_Msg311_Push" : DInt
{ S7_Optimized_Access := 'TRUE' ; Published := 'TRUE' }
VERSION : 0.1
// COSTRUISCE IL PAYLOAD PER IL MESSAGGIO TOPIC = 101 (INIZIO SET-UP)
   VAR_INPUT 
      MES_IDP : String;
   END_VAR

   VAR_IN_OUT 
      Err : "MES_Err_&_Msg";
   END_VAR

   VAR_TEMP 
      RETVAL : DInt;   // Valore di ritorno della funzione di ricerca dati ItemProd usando ID_Prod (PLC) 
      Msg : "MES_Msg";
      strPayload : String;   // Stringa Payload per formare il messaggio
   END_VAR

   VAR CONSTANT 
      NUMERO_MESSAGGIO : UDInt := 311;
   END_VAR


BEGIN
	
	REGION NOTE
	    (*
	    
	    PLC → MES – Topic = 311: Fine sospensione della produzione 
	    Il messaggio è inviato per informare il MES che la sospensione dell’ordine è stata rimossa perché 
	    il ciclo automatico è stato riavviato
	
	    Payload 
	    
	    Chiave  Formato Descrizione
	    [IDP]   C   Identificativo univoco dell’ordine di produzione.
	    [DT]    C   Timestamp del momento in cui l’ordine è uscito dalla sospensione. Formato: "YYYY-MM-DD HH:MM:SS.mmm"
	    
	   
	
	    *)
	    
	END_REGION ;
	
	
	#MES_Msg311_Push := "PROD_FUN_RESULT_OK";
	
	
	
	
	REGION PayLoad creation
	    
	    
	    //@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
	    //
	    // CREAZIONE PAYLOAD
	    // 
	    //@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@  
	    
	    // [IDP]   C   Identificativo ordine.
	    //
	    #strPayload := '';
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_IDP");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := #MES_IDP); 
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    // [DT]    C   Timestamp del momento in cui l’ordine è entrata in sospensione. Formato: "YYYY-MM-DD HH:MM:SS.mmm"
	    // 
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_DT");
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "Conv_DT_to_STRING"("Now"())); 
	    #strPayload := CONCAT(IN1 := #strPayload, IN2 := "MES_MSG_KEY_SEPARATOR");
	    
	    
	        
	END_REGION ;
	
	
	
	
	
	
	
	REGION PUSH messaggio
	    
	    // Incrementa ultimo ID
	    // 
	    "MES_Send_Buffer_Msg_Topic3XX".lastId := "Inc_DINT_Circ"("MES_Send_Buffer_Msg_Topic3XX".lastId);
	    
	    
	    // Prepararazione messaggio
	    // 
	    #Msg.Id := "MES_Send_Buffer_Msg_Topic3XX".lastId;
	    #Msg.timestamp := "Conv_DT_to_STRING"("Now"());
	    #Msg.topic := #NUMERO_MESSAGGIO;
	    #Msg.payLoad := #strPayload;
	    
	    
	    // Push messaggio
	    // 
	    #RETVAL := "MES_Buffer_Push"(Msg := #Msg, Evaluate_FULL := "ProductionMng_PERS".Pers.Data.MODE.MES_ON,
	                                 Buffer_msg := "MES_Send_Buffer_Msg_Topic3XX".msg);
	    
	    // Push messaggio LOG
	    // 
	   "IPC_Push_Log_MES"(Msg := #Msg, Ack := #RETVAL);
	    
	    
	    IF #RETVAL < "PROD_FUN_RESULT_OK" THEN
	        #MES_Msg311_Push := #RETVAL;
	        #Err := "MES_Error_Set"(msg_RETVAL := #RETVAL, msg := #Msg);
	        RETURN;
	    END_IF;
	    
	    
	    // Return value = OK
	    // 
	    #MES_Msg311_Push := "PROD_FUN_RESULT_OK";
	    RETURN;
	END_REGION
	
	
	
	
	
	
END_FUNCTION

