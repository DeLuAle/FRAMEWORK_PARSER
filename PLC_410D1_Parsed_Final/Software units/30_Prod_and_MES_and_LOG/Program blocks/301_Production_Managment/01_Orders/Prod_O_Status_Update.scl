FUNCTION_BLOCK "Prod_O_Status_Update"
{ S7_Optimized_Access := 'TRUE' ; Published := 'FALSE' }
VERSION : 0.1
   VAR_INPUT 
      PT_RETRY { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Time := T#5s;
      PART_PROD { ExternalWritable := 'False'} : "Prod_Part_Area";
      BEAM_PROD { ExternalWritable := 'False'} : "Prod_Beam_Area";
   END_VAR

   VAR_IN_OUT 
      Err_MSG131_O_ACT : "MES_Err_&_Msg";
      Err_MSG131_O_TODO : "MES_Err_&_Msg";
      Err_MSG131_O_ENDING : "MES_Err_&_Msg";
      Err_MSG131_O_OUT : "MES_Err_&_Msg";
   END_VAR

   VAR 
      ACT : Struct
         Status : Int;
         Indx : Int;
         Retry : Int;
         MSG131_RETVAL : DInt;
         MSG131_Prod_Item { S7_SetPoint := 'False'} : "Prod_Item";
         MSG131_strPayLoad : String;
         TON_Retry {InstructionName := 'TON_TIME'; LibVersion := '1.0'; S7_SetPoint := 'False'} : TON_TIME;
      END_STRUCT;
      TODO : Struct
         Status : Int;
         Indx : Int;
         Retry : Int;
         MSG131_RETVAL : DInt;
         MSG131_Prod_Item { S7_SetPoint := 'False'} : "Prod_Item";
         MSG131_strPayLoad : String;
         TON_Retry {InstructionName := 'TON_TIME'; LibVersion := '1.0'; S7_SetPoint := 'False'} : TON_TIME;
      END_STRUCT;
      ENDING : Struct
         Status : Int;
         Indx : Int;
         Retry : Int;
         MSG131_RETVAL : DInt;
         MSG131_Prod_Item { S7_SetPoint := 'False'} : "Prod_Item";
         MSG131_strPayLoad : String;
         TON_Retry {InstructionName := 'TON_TIME'; LibVersion := '1.0'; S7_SetPoint := 'False'} : TON_TIME;
      END_STRUCT;
      OUT : Struct
         Status : Int;
         Indx : Int;
         Retry : Int;
         MSG131_RETVAL : DInt;
         MSG131_Prod_Item { S7_SetPoint := 'False'} : "Prod_Item";
         MSG131_strPayLoad : String;
         TON_Retry {InstructionName := 'TON_TIME'; LibVersion := '1.0'; S7_SetPoint := 'False'} : TON_TIME;
      END_STRUCT;
   END_VAR

   VAR CONSTANT 
      CHECK : Int := 0;
      EXECUTE : Int := 1;
   END_VAR


BEGIN
	(*
	
	
	 //@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
	//
	// Aggiornamento "Status" su Prod_Item assegnao a chiave [SOR] nei MSG 131
	// 
	//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
	
	Valori possibili per [SOR] – Stato ordine:
	•   0: Non iniziato.
	•   1: Iniziato – Il materiale è uscito dalla cesoia nella stazione di giunzione, ma non è ancora arrivato alla zona di taglio dei semi-gusci.
	•   2: In lavoro – Il materiale che esce dallo stampo di taglio (dopo la profilatura) viene utilizzato per produrre i semi-gusci richiesti.
	•   3: Conclusione parziale – Tutti i semi-gusci sono stati prodotti, ma ci sono ancora pezzi in fase di composizione. (Processo terminato/ Fine coda)
	•   4: Concluso completo – Tutti i semi-gusci sono stati prodotti, tutti i pezzi sono usciti dalla macchina e la quantità richiesta è stata raggiunta. (Processo terminato/ Fine coda)
	•   5: Concluso incompleto – Come sopra, ma la quantità richiesta non è stata raggiunta. (Processo terminato/ Fine coda)
	•   6: Pausa parziale – L’ordine era in lavorazione, ma il taglio delle parti è stato interrotto prima del completamento; sono ancora presenti semi-gusci o pezzi in macchina. (Ritorno in coda)
	•   7: Pausa totale – L’ordine è in pausa e non ci sono semi-gusci o pezzi residui in macchina. L’ordine ritorna in coda per essere ripreso successivamente. (Ritorno in coda)
	
	*)
	
	(*
	# TODO
	"Sys".ByPass := "Sys".ByPass; // Vedere per non inviare messaggio 131 a MES se ordini HMI ma inviare MSG a LOG
	*)
	
	
	REGION ACT
	    
	    (*
	    
	    Valori possibili per [SOR] – Stato ordine:
	    
	    •   0: Non iniziato.
	    •   1: Iniziato – Il materiale è uscito dalla cesoia nella stazione di giunzione, ma non è ancora arrivato alla zona di taglio dei semi-gusci.
	    •   2: In lavoro – Il materiale che esce dallo stampo di taglio (dopo la profilatura) viene utilizzato per produrre i semi-gusci richiesti.
	    *)
	    
	    
	    
	        
	    
	    
	    
	    
	    
	    
	    #ACT.TON_Retry(IN := #ACT.Retry > 0,
	                   PT := #PT_RETRY);
	    
	    IF #ACT.TON_Retry.Q THEN
	        #ACT.Retry := 0;
	    END_IF;
	    
	    
	    CASE #ACT.Status OF
	            
	        #CHECK:
	            
	            
	            "Production".O_ACT.Status := "SOR_NON_INIZIATO";
	            
	            IF "ProductionMng_PERS".Pers.Data.SET_UP.ProductionReady
	                AND
	                #PART_PROD.Material_Presence
	            THEN
	                "Production".O_ACT.Status := "SOR_INZIATO";
	                
	                IF "Production".O_ACT.O_ID > 0
	                    AND
	                    "Production".O_ACT.Parts_REQ > 0
	                THEN
	                    "Production".O_ACT.Status := "SOR_IN_LAVORO";
	                END_IF;
	            END_IF;
	            
	            
	            
	            IF "Production".O_ACT.Status <> "Production".O_ACT.Status_Saved
	                AND
	                #ACT.Retry = 0
	                
	            THEN
	                #ACT.Status += 1;
	            END_IF;
	            
	            
	        #EXECUTE:
	            
	            #ACT.MSG131_RETVAL := "MES_Msg131_Push"(O_ID := "Production".O_ACT.O_ID,
	                                                    MES_IDI := -1,
	                                                    PART_PROD_Halted := #PART_PROD.Halted,
	                                                    BEAM_PROD_Halted := #BEAM_PROD.Halted,
	                                                    Err => #Err_MSG131_O_ACT);
	            
	            
	            IF #ACT.MSG131_RETVAL = "PROD_FUN_RESULT_OK" THEN
	                
	                #ACT.Retry := 0;
	                "Production".O_ACT.Status_Saved := "Production".O_ACT.Status;
	                
	            ELSE
	                #ACT.Retry := 1;
	                
	                #ACT.TON_Retry(IN := FALSE,
	                               PT := #PT_RETRY);
	                
	            END_IF;
	            
	            #ACT.Status += 1;
	            
	        ELSE
	            #ACT.Status := #CHECK;
	            
	    END_CASE;
	    
	END_REGION ;
	
	
	
	    
	    
	REGION TODO    
	    
	    (*
	    
	    Valori possibili per [SOR] – Stato ordine:
	    •   0: Non iniziato.
	    •   7: Pausa totale – L’ordine è in pausa e non ci sono semi-gusci o pezzi residui in macchina.
	    
	    *)
	    
	    CASE #TODO.Status OF
	            
	        #CHECK:
	            
	           
	            #TODO.Indx := LIMIT(MN := 1, IN := #TODO.Indx, MX := "PROD_TODO_ITEMS");
	            
	            
	            IF "Production".O_TODO[#TODO.Indx].O_ID > 0
	                AND
	                "Production".O_TODO[#TODO.Indx].Parts_REQ > 0
	            THEN
	                "Production".O_TODO[#TODO.Indx].Status := "SOR_NON_INIZIATO";
	                
	                IF "Production".O_TODO[#TODO.Indx].Parts_ToGo < "Production".O_TODO[#TODO.Indx].Parts_REQ THEN
	                    "Production".O_TODO[#TODO.Indx].Status := "SOR_PAUSA_TOTALE";
	                END_IF;
	            ELSE
	                
	                "Production".O_TODO[#TODO.Indx].Status := "SOR_NON_INIZIATO";
	                "Production".O_TODO[#TODO.Indx].Status_Saved := "SOR_NON_INIZIATO";
	            END_IF;
	            
	            
	            IF "Production".O_TODO[#TODO.Indx].Status <> "Production".O_TODO[#TODO.Indx].Status_Saved
	                AND
	                #TODO.Indx <> #TODO.Retry
	            THEN
	                #TODO.Status += 1;
	                
	            ELSE
	               
	                // Incrementa indice
	                #TODO.Indx := (#TODO.Indx MOD "PROD_TODO_ITEMS") + 1;
	                
	            END_IF;
	            
	            
	        #EXECUTE:
	            
	            #TODO.MSG131_RETVAL :=
	            
	            
	            "MES_Msg131_Push"(O_ID := "Production".O_TODO[#TODO.Indx].O_ID,
	                              MES_IDI := 0,
	                              PART_PROD_Halted := #PART_PROD.Halted,
	                              BEAM_PROD_Halted := #BEAM_PROD.Halted,
	                              Err => #Err_MSG131_O_TODO);
	            
	            
	            IF #TODO.MSG131_RETVAL = "PROD_FUN_RESULT_OK" THEN
	                
	                #TODO.Retry := 0;
	                "Production".O_TODO[#TODO.Indx].Status_Saved := "Production".O_TODO[#TODO.Indx].Status;
	                
	            ELSE
	                #TODO.Retry := #TODO.Indx;
	                
	                #TODO.TON_Retry(IN := FALSE,
	                                PT := #PT_RETRY);
	                
	            END_IF;
	            
	            #TODO.Status += 1;
	            
	        ELSE
	            
	            #TODO.Status := #CHECK;
	            
	    END_CASE;
	    
	END_REGION ;
	
	
	
	
	
	    
	    
	REGION ENDING
	    
	        
	        (*
	        
	        Valori possibili per [SOR] – Stato ordine:
	                
	        •   2: In lavoro – Il materiale che esce dallo stampo di taglio (dopo la profilatura) viene utilizzato per produrre i semi-gusci richiesti.
	        •   3: Conclusione parziale – Tutti i semi-gusci sono stati prodotti, ma ci sono ancora pezzi in fase di composizione.
	        •   6: Pausa parziale – L’ordine era in lavorazione, ma il taglio delle parti è stato interrotto prima del completamento; ma ci sono ancora pezzi in fase di composizione.
	        *)
	        
	    CASE #ENDING.Status OF
	            
	            
	            #CHECK:
	                
	                
	                #ENDING.Indx := LIMIT(MN := 1, IN := #ENDING.Indx, MX := "PROD_ENDING_ITEMS");
	                
	                
	                // Aggiornamento stato dell'ordine
	                // 
	                
	                IF "Production".O_ENDING[#ENDING.Indx].O_ID > 0
	                    AND
	                    "Production".O_ENDING[#ENDING.Indx].Parts_REQ > 0
	                THEN
	                    
	                    "Production".O_ENDING[#ENDING.Indx].Status := "SOR_NON_INIZIATO";
	                    
	                    IF "Production".O_ACT.O_ID = "Production".O_ENDING[#ENDING.Indx].O_ID THEN
	                        
	                        "Production".O_ENDING[#ENDING.Indx].Status := "SOR_IN_LAVORO";
	                        
	                    ELSE
	                        
	                        // Ritornerà tra i TODO quando nessun PTRK / BTRK usa questo ordine
	                        //
	                        "Production".O_ENDING[#ENDING.Indx].Status := "SOR_PAUSA_PARZIALE"; 
	                        
	                        IF "Production".O_ENDING[#ENDING.Indx].Parts_ToGo <= 0 THEN
	                            
	                            // Andra tra gli OUT quando nessun PTRK / BTRK usa questo ordine
	                            
	                            "Production".O_ENDING[#ENDING.Indx].Status := "SOR_CONCLUSO_PARZIALE";
	                        END_IF;
	                        
	                    END_IF;
	                ELSE
	                    "Production".O_ENDING[#ENDING.Indx].Status := "SOR_NON_INIZIATO";
	                    "Production".O_ENDING[#ENDING.Indx].Status_Saved :="SOR_NON_INIZIATO";
	                    
	                END_IF;
	                
	                // Avvio send messaggio se lo stato non è stato aggiornato al MES
	                //
	                IF "Production".O_ENDING[#ENDING.Indx].Status <> "Production".O_ENDING[#ENDING.Indx].Status_Saved
	                    AND
	                    #ENDING.Indx <> #ENDING.Retry
	                THEN
	                    
	                    #ENDING.Status += 1;
	                    
	                ELSE
	                    
	                    // Incrementa indice
	                    #ENDING.Indx := (#ENDING.Indx MOD "PROD_ENDING_ITEMS") + 1;
	                END_IF;
	                
	                
	                
	            #EXECUTE:
	                
	                // Aggiorna stato dell'ordine a MES
	                // 
	                #ENDING.MSG131_RETVAL:= "MES_Msg131_Push"(O_ID := "Production".O_ENDING[#ENDING.Indx].O_ID,
	                                                          MES_IDI:=0,
	                                                          PART_PROD_Halted:=#PART_PROD.Halted,
	                                                          BEAM_PROD_Halted:=#BEAM_PROD.Halted,
	                                                          Err=>#Err_MSG131_O_ENDING);
	                
	                
	                IF #ENDING.MSG131_RETVAL = "PROD_FUN_RESULT_OK" THEN
	                    
	                    #ENDING.Retry := 0;
	                    "Production".O_ENDING[#ENDING.Indx].Status_Saved := "Production".O_ENDING[#ENDING.Indx].Status;
	                    
	                ELSE
	                    #ENDING.Retry := #ENDING.Indx;
	                    
	                    #ENDING.TON_Retry(IN := FALSE,
	                                    PT := #PT_RETRY);
	                    
	                END_IF;
	                
	               #ENDING.Status += 1;
	               
	                
	                
	            ELSE
	                
	                #ENDING.Status := #CHECK;
	                
	        END_CASE;
	        
	END_REGION ;
	    
	    
	    
	
	
	REGION OUT
	    
	    
	    (*
	    
	    Valori possibili per [SOR] – Stato ordine:
	    •   4: Concluso completo – Tutti i semi-gusci sono stati prodotti, tutti i pezzi sono usciti dalla macchina e la quantità richiesta è stata raggiunta.
	    •   5: Concluso incompleto – Come sopra, ma la quantità richiesta non è stata raggiunta.
	    *)
	    
	    CASE #OUT.Status OF
	            
	        #CHECK:
	            
	            
	            #OUT.Indx := LIMIT(MN := 1, IN := #OUT.Indx, MX := "PROD_OUT_ITEMS");
	            
	            // Aggiornamento stato dell'ordine
	            // 
	            
	            
	            IF "Production".O_OUT[#OUT.Indx].O_ID > 0
	                AND
	                "Production".O_OUT[#OUT.Indx].Parts_REQ > 0
	            THEN
	                
	                "Production".O_OUT[#OUT.Indx].Status := "SOR_CONCLUSO_COMPLETO";
	                
	            
	                
	                // Se ordine era solo di semigusci...
	                // ..Controllo conteggio parts
	                // 
	                IF "Production".O_OUT[#OUT.Indx].Beam_Code = 0
	                    AND "Production".O_OUT[#OUT.Indx].Parts_ToGo > 0
	                THEN
	                    
	                    "Production".O_OUT[#OUT.Indx].Status := "SOR_CONCLUSO_INCOMPLETO";
	                    
	                END_IF;
	                
	                
	                // Se ordine era di pezzi accoppiati
	                // ..Controllo conteggio Beam
	                // 
	                IF "Production".O_OUT[#OUT.Indx].Beam_Code = 1
	                    AND "Production".O_OUT[#OUT.Indx].Beams_ToGo > 0
	                THEN
	                    
	                    "Production".O_OUT[#OUT.Indx].Status := "SOR_CONCLUSO_INCOMPLETO";
	                    
	                END_IF;
	                
	                
	            ELSE
	                "Production".O_OUT[#OUT.Indx].Status := "SOR_NON_INIZIATO";
	                "Production".O_OUT[#OUT.Indx].Status_Saved := "SOR_NON_INIZIATO";
	            END_IF;
	            
	            
	            // Avvio send messaggio se lo stato non è stato aggiornato al MES
	            //
	            IF "Production".O_OUT[#OUT.Indx].Status <> "Production".O_OUT[#OUT.Indx].Status_Saved
	                AND
	                #OUT.Indx <> #OUT.Retry
	            THEN
	                #OUT.Status += 1;
	                
	            ELSE
	                
	                // Incrementa indice
	                #OUT.Indx := (#OUT.Indx MOD "PROD_OUT_ITEMS") + 1;
	            END_IF;
	            
	        #EXECUTE:
	            
	            #OUT.MSG131_RETVAL:="MES_Msg131_Push"(O_ID:="Production".O_OUT[#OUT.Indx].O_ID,
	                                                  MES_IDI:=-1,
	                                                  PART_PROD_Halted:=#PART_PROD.Halted,
	                                                  BEAM_PROD_Halted:=#BEAM_PROD.Halted,
	                                                  Err=>#Err_MSG131_O_OUT);
	            
	            IF #OUT.MSG131_RETVAL = "PROD_FUN_RESULT_OK" THEN
	                
	                #OUT.Retry := 0;
	                
	                "Production".O_OUT[#OUT.Indx].Status_Saved := "Production".O_OUT[#OUT.Indx].Status;
	                
	            ELSE
	                
	                #OUT.Retry := #OUT.Indx;
	                
	                #OUT.TON_Retry(IN := FALSE,
	                                  PT := #PT_RETRY);
	                
	            END_IF;
	            
	            #OUT.Status += 1;
	            
	        ELSE
	            
	            #OUT.Status := #CHECK;
	            
	    END_CASE;
	    
	END_REGION ;
	
	
	
	
	
	
	
END_FUNCTION_BLOCK

