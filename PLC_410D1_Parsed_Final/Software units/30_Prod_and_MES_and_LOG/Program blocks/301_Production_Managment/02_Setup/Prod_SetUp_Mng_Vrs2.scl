FUNCTION_BLOCK "Prod_SetUp_Mng_Vrs2"
{ S7_Optimized_Access := 'TRUE' ; Published := 'FALSE' }
VERSION : 0.1
   VAR_INPUT 
      Rst : Bool;
      PB_LineSetUp_Start : Bool;
      PB_LineSetUp_Stop : Bool;
   END_VAR
   VAR_INPUT RETAIN
      PART_PROD { ExternalWritable := 'False'} : "Prod_Part_Area";
      BEAM_PROD { ExternalWritable := 'False'} : "Prod_Beam_Area";
   END_VAR
   VAR_INPUT 
      MODE { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : "Prod_Mode";
   END_VAR

   VAR_OUTPUT 
      CTRL_LinePreparation : Bool;
   END_VAR

   VAR_IN_OUT 
      SETUP : "Prod_SetUp_Data";
      Warning : Struct
         PUSH_MSG101 : "MES_Err_&_Msg";
         Caricamento_PPrf : Struct
            Err_Load_PPrf : Bool;
            Err_Load_Timeout : Bool;
            Err_NumeroProgrammaNonValido : Bool;
            Err_NonCorrisponde_W_A : Bool;
            Err_NonCorrisponde_H_B : Bool;
            Err_NonCorrisponde_Spessore : Bool;
         END_STRUCT;
         Attesa_Avvio_SetUp_Automatico : Bool;
         SetUp_Automatico_Avviato : Bool;
         Attesa_Completamento_SetUp_Manuale : Bool;
         Attesa_Inizio_Inserimento_lamiera : Bool;
         Attesa_Fine_inserimento_lamiera : Bool;
         Attesa_Taglio_di_Intestazione : Bool;
         PUSH_MSG102 : "MES_Err_&_Msg";
      END_STRUCT;
   END_VAR

   VAR 
      Ctrl : Struct
         LinePreparationStart { S7_SetPoint := 'True'} : Bool;
      END_STRUCT;
      i : Int;
      RETVAL : DInt;
      PPrf_Get : "GetPrg";
      Ton_TimeOutLoadPPrf {InstructionName := 'TON_TIME'; LibVersion := '1.0'} : TON_TIME;
      Ton_Inposizione {InstructionName := 'TON_TIME'; LibVersion := '1.0'} : TON_TIME;
      R_TRIG_PbStart {InstructionName := 'R_TRIG'; LibVersion := '1.0'} : R_TRIG;
   END_VAR


BEGIN
	
	#R_TRIG_PbStart(CLK := #PB_LineSetUp_Start);
	
	
	
	
	
	REGION Reset
	    IF #Rst THEN
	        
	        // Reset errori
	        //
	        
	        #Warning.Caricamento_PPrf.Err_Load_PPrf :=
	        #Warning.Caricamento_PPrf.Err_Load_Timeout :=
	        #Warning.Caricamento_PPrf.Err_NumeroProgrammaNonValido :=
	        #Warning.Caricamento_PPrf.Err_NonCorrisponde_W_A :=
	        #Warning.Caricamento_PPrf.Err_NonCorrisponde_H_B :=
	        #Warning.Caricamento_PPrf.Err_NonCorrisponde_Spessore :=
	        
	        #Warning.Attesa_Avvio_SetUp_Automatico :=
	        #Warning.SetUp_Automatico_Avviato :=
	        #Warning.Attesa_Completamento_SetUp_Manuale :=
	        #Warning.Attesa_Inizio_Inserimento_lamiera :=
	        #Warning.Attesa_Fine_inserimento_lamiera :=
	        #Warning.Attesa_Taglio_di_Intestazione := FALSE;
	        
	    END_IF;
	END_REGION ;
	
	
	
	REGION Ricarica PPRF Se Ordine in lavoro usa programma profilo diverso dalla preparazione linea
	    
	    IF #SETUP.Status > "PROD_SETUP_WAIT_MAN_PREPARATION"
	        AND
	        "Production".O_ACT.O_ID > 0
	        AND
	        "Production".O_ACT.PPrf_No <> #SETUP.Prepapration_Aut_Done_PPrf_Number
	    THEN
	        #SETUP.Status := "PROD_SETUP_WAIT_ORDER_TO_PRODUCE";
	    END_IF;
	    
	END_REGION
	
	
	
	REGION Force Init per cambio produzione
	    
	    IF "Production".O_ACT.O_ID > 0
	        AND
	        "Production".O_ACT.PPrf_No > 0
	        AND
	        "Production".O_ACT.PPrf_No <> "ProductionMng_PERS".Pers.Data.SET_UP.Prepapration_Aut_Done_PPrf_Number
	        AND
	        NOT #PART_PROD.Material_Presence
	        AND
	        #PART_PROD.MaterialFeedingStandstill
	        AND
	        #SETUP.ProductionReady
	    THEN
	        #SETUP.Status := "PROD_SETUP_SPARE_0";
	        
	    END_IF;
	    
	    
	    IF "Production".O_ACT.O_ID <= 0
	        AND
	        NOT #PART_PROD.Material_Presence
	        AND
	        #PART_PROD.MaterialFeedingStandstill
	        AND
	        #SETUP.ProductionReady
	    THEN
	        #SETUP.Status := "PROD_SETUP_SPARE_0";
	        
	    END_IF;
	    
	    
	    
	    
	    
	END_REGION
	
	
	IF #SETUP.Status <> "PROD_SETUP_EXE_AUT_PREPARATION"
	THEN
	    #CTRL_LinePreparation := FALSE;
	    #Warning.SetUp_Automatico_Avviato := FALSE;
	END_IF;
	
	
	
	REGION POP_PRV_ORDER: Libero
	    
	    IF #SETUP.Status = "PROD_SETUP_SPARE_0" THEN
	        
	        #SETUP.ProductionReady := FALSE;
	        
	        
	        #SETUP.Status += 1;
	        
	        
	    END_IF;
	    
	        
	END_REGION
	
	
	
	
	REGION WAIT ORDER: Attesa ordine in lavoro
	    
	    
	    IF #SETUP.Status = "PROD_SETUP_WAIT_ORDER_TO_PRODUCE" THEN
	        
	        
	        // Cambio step
	        // 
	        IF "Production".O_ACT.O_ID > 0 THEN
	            #SETUP.Status += 1;
	            
	            RETURN;
	        END_IF;
	        
	        RETURN;
	        
	   
	    END_IF;
	END_REGION ;        
	
	
	REGION INIT_LOAD_PPRF : Inizializza risultati caricamento programma profilo
	    
	    
	    IF #SETUP.Status = "PROD_SETUP_INIT_LOAD_PPRF" THEN
	        
	        #PPrf_Get(
	                  Execute  := FALSE,
	                  PrgNo    := "Production".O_ACT.PPrf_No,
	                  GET_ITF  := "PPrf_MAIN".GET_ITF,
	                  GET_Data := "PPrf_MAIN".RWD.GET,
	                  PRG_Data := "PPrf_MAIN".ON_WORK
	        );
	        
	        #Ton_TimeOutLoadPPrf(
	                             IN := FALSE,
	                             PT := T#2s
	        );
	        
	        
	        IF NOT #PPrf_Get.Busy
	            AND
	            NOT #PPrf_Get.Error
	            AND
	            NOT #PPrf_Get.Done
	            AND
	            NOT #Ton_TimeOutLoadPPrf.Q
	            AND
	            NOT #Warning.Caricamento_PPrf.Err_Load_PPrf
	            AND
	            NOT #Warning.Caricamento_PPrf.Err_Load_Timeout
	            AND
	            NOT #Warning.Caricamento_PPrf.Err_NumeroProgrammaNonValido
	            AND
	            NOT #Warning.Caricamento_PPrf.Err_NonCorrisponde_W_A
	            AND
	            NOT #Warning.Caricamento_PPrf.Err_NonCorrisponde_H_B
	            AND
	            NOT #Warning.Caricamento_PPrf.Err_NonCorrisponde_Spessore
	            
	        THEN
	            #SETUP.Status += 1;
	            RETURN;
	        END_IF;
	        
	        RETURN;
	    END_IF;
	    
	END_REGION
	
	
	
	
	
	REGION LOAD_PPRF: Caricamento programma profilo
	    
	    
	    IF #SETUP.Status = "PROD_SETUP_LOAD_PPRF"
	    THEN
	        
	        // Cambio / Ricarica programma profilo in lavoro + WNG FAIL
	        
	        IF "Production".O_ACT.O_ID >= 1
	            AND
	            "Production".O_ACT.PPrf_No < 1
	        THEN
	            #Warning.Caricamento_PPrf.Err_NumeroProgrammaNonValido := TRUE;
	            RETURN;
	        END_IF;
	        
	        
	        
	        #PPrf_Get(
	                  Execute  := TRUE,
	                  PrgNo    := "Production".O_ACT.PPrf_No,
	                  GET_ITF  := "PPrf_MAIN".GET_ITF,
	                  GET_Data := "PPrf_MAIN".RWD.GET,
	                  PRG_Data := "PPrf_MAIN".ON_WORK
	        );
	        
	        
	        #Ton_TimeOutLoadPPrf(
	                             IN := NOT #PPrf_Get.Done ,
	                             PT := T#2s
	        );
	        
	        
	        
	        
	        IF #PPrf_Get.Error THEN
	            #Warning.Caricamento_PPrf.Err_Load_PPrf := TRUE;
	            #SETUP.Status := "PROD_SETUP_INIT_LOAD_PPRF";
	            RETURN;
	        END_IF;
	        
	        
	        IF #Ton_TimeOutLoadPPrf.Q AND "Sys".ByPass THEN
	            #Warning.Caricamento_PPrf.Err_Load_Timeout := TRUE;
	            #SETUP.Status := "PROD_SETUP_INIT_LOAD_PPRF";
	            RETURN;
	        END_IF;
	            
	            
	        // Controllo coerenza dati programma con quelli richiesti dal MES
	        // 
	        
	        IF #PPrf_Get.Done THEN
	            
	            IF ABS("PPrf_MAIN".ON_WORK.GeneralData.Beam_W_A_Code - "Production".O_ACT.Beams_W_A) > 0.01 THEN
	                #Warning.Caricamento_PPrf.Err_NonCorrisponde_W_A := TRUE;
	                #SETUP.Status := "PROD_SETUP_INIT_LOAD_PPRF";
	                RETURN;
	            END_IF;
	            
	            IF ABS("Production".O_ACT.Beams_W_A - "Production".O_ACT.Beams_W_A) > 0.01 THEN
	                #Warning.Caricamento_PPrf.Err_NonCorrisponde_H_B := TRUE;
	                #SETUP.Status := "PROD_SETUP_INIT_LOAD_PPRF";
	                RETURN;
	            END_IF;
	            
	            IF ABS("PPrf_MAIN".ON_WORK.GeneralData.Material_Thickness - "PPrf_MAIN".ON_WORK.GeneralData.Material_Thickness) > 0.01 THEN
	                #Warning.Caricamento_PPrf.Err_NonCorrisponde_Spessore := TRUE;
	                #SETUP.Status := "PROD_SETUP_INIT_LOAD_PPRF";
	                RETURN;
	            END_IF;
	            
	            #SETUP.Status += 1;
	            
	        END_IF;
	        
	        
	        
	        
	        RETURN;
	     
	        
	    END_IF;
	END_REGION ;            
	
	
	
	
	
	
	REGION LINE_AUT_PREPARATION : Esecuzione setup autotomatico
	    
	    IF #SETUP.Status = "PROD_SETUP_EXE_AUT_PREPARATION" THEN
	        
	        #Warning.Attesa_Avvio_SetUp_Automatico := #SETUP.Prepapration_Aut_Done_PPrf_Number <> "PPrf_MAIN".ON_WORK.Header.PrgNo;
	        
	        "Sys".ByPass := "Sys".ByPass; // Controllare no pezzi per strada diversi !!!
	        
	        IF NOT #PART_PROD.Man THEN
	            #CTRL_LinePreparation := FALSE;
	            RETURN;
	        END_IF;
	        
	        IF NOT #PART_PROD.Doors_Closed THEN
	            #CTRL_LinePreparation := FALSE;
	            RETURN;
	        END_IF;
	        
	        IF NOT #BEAM_PROD.Man  THEN
	            #CTRL_LinePreparation := FALSE;
	            RETURN;
	        END_IF;
	        
	        IF NOT #BEAM_PROD.Doors_Closed THEN
	            #CTRL_LinePreparation := FALSE;
	            RETURN;
	        END_IF;
	        
	        
	        IF #PB_LineSetUp_Stop THEN
	            #CTRL_LinePreparation := FALSE;
	            RETURN;
	        END_IF;
	        
	        
	        
	        // Invio messaggio 101 a MES + LOG per inizio setup
	        // 
	        IF #R_TRIG_PbStart.Q
	            AND
	            "Production".O_ACT.Type = "PROD_ITEM_TYPE_MES"
	            AND
	            "PPrf_MAIN".ON_WORK.Header.PrgNo <> #SETUP.MSG101_inviato_MES_PPrf_Number
	        THEN
	            
	            #SETUP.MSG101_inviato_MES_PPrf_Number := 0;
	            #SETUP.MSG102_inviato_MES_PPrf_Number := 0;
	            
	            //#Warning.PUSH_MSG101.Presence := FALSE;
	            
	            //IF NOT #Warning.PUSH_MSG101.Presence THEN
	                
	                #RETVAL := "MES_Msg101_Push"(#Warning.PUSH_MSG101);
	                
	                IF #RETVAL >= "PROD_FUN_RESULT_OK" THEN
	                    
	                    "IPC_Push_Log_Gen"("LOG_TOPIC_InizioSetup");
	                    
	                    #SETUP.MSG101_inviato_MES_PPrf_Number := "PPrf_MAIN".ON_WORK.Header.PrgNo;
	                    #SETUP.MSG101_inviato_LOG_PPrf_Number := "PPrf_MAIN".ON_WORK.Header.PrgNo;
	                    
	                END_IF;
	            //END_IF;
	           
	        END_IF;
	        
	        // Invio messaggio 101 solo a LOG per inzio setup
	        // 
	        IF #R_TRIG_PbStart.Q
	            AND
	            "Production".O_ACT.Type <> "PROD_ITEM_TYPE_MES"
	            AND
	            #SETUP.MSG101_inviato_LOG_PPrf_Number <> "PPrf_MAIN".ON_WORK.Header.PrgNo
	            
	        THEN
	            "IPC_Push_Log_Gen"("LOG_TOPIC_InizioSetup");
	            #SETUP.MSG101_inviato_LOG_PPrf_Number := "PPrf_MAIN".ON_WORK.Header.PrgNo;
	        END_IF;
	        
	        
	        // Reset memorie preparazione precedente
	        // 
	        IF #R_TRIG_PbStart.Q
	            AND
	            #SETUP.MSG101_inviato_LOG_PPrf_Number = "PPrf_MAIN".ON_WORK.Header.PrgNo
	            AND
	            (
	            #SETUP.Prepapration_Aut_Done_PPrf_Number <> "PPrf_MAIN".ON_WORK.Header.PrgNo
	            OR
	            #SETUP.Prepapration_Man_Done_PPrf_Number <> "PPrf_MAIN".ON_WORK.Header.PrgNo
	            )
	        THEN
	            // Reset codice PPrf della preparazione completata
	            #SETUP.Prepapration_Aut_Done_PPrf_Number := 0;
	            #SETUP.Prepapration_Man_Done_PPrf_Number := 0;
	            
	            #SETUP.STS_Aut_SetUp.Assi_Profilatrice_InPosizione := FALSE;
	            #SETUP.STS_Aut_SetUp.Assi_Bordatrice_InPosizione := FALSE;
	            
	            // Cancella memorie delle operazioni manuali eseguite
	            FOR #i := 1 TO "SETUP_MAN_OPERATIONS_QTY" DO
	                #SETUP.STS_Man_SetUp.Ack_Eseguito_X[#i] := FALSE;
	            END_FOR;
	            
	            FOR #i := 1 TO "SETUP_MAN_OP_WITH_CODE_QTY" DO
	                #SETUP.STS_Man_SetUp.Ack_Eseguito_Y[#i] := FALSE;
	            END_FOR;
	            
	            #SETUP.STS_Man_SetUp.Done := FALSE;
	            
	            
	            #CTRL_LinePreparation := TRUE;
	            
	        END_IF;
	        
	        #Warning.SetUp_Automatico_Avviato := #CTRL_LinePreparation;
	        
	        #SETUP.STS_Aut_SetUp.Assi_Profilatrice_InPosizione := #PART_PROD.SetUpDone;
	        
	        #SETUP.STS_Aut_SetUp.Assi_Bordatrice_InPosizione := #BEAM_PROD.SetUpDone;
	        
	        #Ton_Inposizione(IN := TRUE
	                         AND #SETUP.MSG101_inviato_LOG_PPrf_Number = "PPrf_MAIN".ON_WORK.Header.PrgNo
	                         AND #SETUP.STS_Aut_SetUp.Assi_Profilatrice_InPosizione
	                         AND #SETUP.STS_Aut_SetUp.Assi_Bordatrice_InPosizione,
	                         PT := T#1s);
	        
	        IF #Ton_Inposizione.Q THEN
	            
	            #SETUP.Prepapration_Aut_Done_PPrf_Number := "PPrf_MAIN".ON_WORK.Header.PrgNo;
	            
	            #CTRL_LinePreparation := FALSE;
	            
	            // Cambio step
	            // 
	            #SETUP.Status += 1;
	            
	            RETURN;
	            
	        ELSIF TRUE // Linea già prepara !
	           
	            //AND "PPrf_MAIN".ON_WORK.Header.PrgNo = #SETUP.MSG101_inviato_MES_PPrf_Number
	            AND "PPrf_MAIN".ON_WORK.Header.PrgNo = #SETUP.MSG101_inviato_LOG_PPrf_Number
	            
	            AND "PPrf_MAIN".ON_WORK.Header.PrgNo = #SETUP.MSG102_inviato_MES_PPrf_Number
	            AND "PPrf_MAIN".ON_WORK.Header.PrgNo = #SETUP.MSG102_inviato_LOG_PPrf_Number
	            
	            
	            AND #SETUP.Prepapration_Aut_Done_PPrf_Number = "PPrf_MAIN".ON_WORK.Header.PrgNo
	            AND #SETUP.Prepapration_Man_Done_PPrf_Number = "PPrf_MAIN".ON_WORK.Header.PrgNo
	        THEN
	            // Cambio step
	            // 
	            #SETUP.Status += 1;
	            
	            RETURN;
	        END_IF;
	        
	        RETURN;
	    ELSE
	        #Warning.Attesa_Avvio_SetUp_Automatico := FALSE;
	    END_IF;
	    
	END_REGION
	
	
	
	
	REGION WAIT_MAN_PREPARATION
	    
	    IF #SETUP.Status = "PROD_SETUP_WAIT_MAN_PREPARATION" THEN
	        
	        
	        IF #SETUP.Prepapration_Man_Done_PPrf_Number <> "PPrf_MAIN".ON_WORK.Header.PrgNo
	        THEN
	            
	            #RETVAL := "Prod_Check_Man_SetUp"(STS_Man_SetUp := #SETUP.STS_Man_SetUp);
	            
	            
	            IF #RETVAL < "PROD_FUN_RESULT_OK"
	            THEN
	                #Warning.Attesa_Completamento_SetUp_Manuale := TRUE;
	                RETURN;
	            END_IF;
	            
	            #SETUP.Prepapration_Man_Done_PPrf_Number := "PPrf_MAIN".ON_WORK.Header.PrgNo;
	            
	        ELSE
	            
	            // Cambio step
	            // 
	            #SETUP.Status += 1;
	            
	            RETURN;
	            
	            
	        END_IF;
	        
	    ELSE
	        #Warning.Attesa_Completamento_SetUp_Manuale := fALSE;
	    END_IF;
	    
	END_REGION
	
	
	
	
	REGION WAIT_THREADING_BEGIN : Attesa inserimento lamiera
	    
	    IF #SETUP.Status = "PROD_SETUP_WAIT_THREADING_BEGIN" THEN
	        
	        // Attesa inizio inserimento lamiera in profilatrice / cambio step
	        
	        #Warning.Attesa_Inizio_Inserimento_lamiera := TRUE;
	        
	        IF #PART_PROD.MaterialPresenceEntry THEN
	            
	            #SETUP.Status += 1;
	            
	        END_IF;
	        
	        RETURN;
	        
	    ELSE
	        #Warning.Attesa_Inizio_Inserimento_lamiera := FALSE;
	    END_IF;
	END_REGION ;
	
	
	
	REGION WAIT_THREADING_END : Attesa Fine introduzione
	    
	    IF #SETUP.Status = "PROD_SETUP_WAIT_THREADING_END" THEN
	        
	        // Wng  Attesa Inserimento lamiera fino a encoder fine profilatrice
	        // 
	        #Warning.Attesa_Fine_inserimento_lamiera := TRUE;
	        
	        
	        // Eseguito → Cambio Step
	        // 
	        IF #PART_PROD.MaterialPresenceEntry
	            AND
	            #PART_PROD.MaterialPresenceExit
	            AND
	            #PART_PROD.MaterialPresenceEntry
	        THEN
	            
	            #SETUP.Status += 1;
	        END_IF;
	            
	        RETURN;
	 
	    ELSE
	        #Warning.Attesa_Fine_inserimento_lamiera := FALSE;
	        
	    END_IF;
	END_REGION ;
	
	
	REGION WAIT_LEAP_CUT_DONE: Attesa esecuzione taglio di intestazione
	    
	    IF #SETUP.Status = "PROD_SETUP_WAIT_LEAD_CUT" THEN
	        
	        #Warning.Attesa_Taglio_di_Intestazione := TRUE;
	        
	        IF #PART_PROD.LeadCutMem
	            AND
	            "Production".O_ACT.O_ID >= 1
	        THEN
	            #SETUP.Status += 1;
	            
	        END_IF;
	        
	        RETURN;
	    ELSE
	        #Warning.Attesa_Taglio_di_Intestazione := FALSE;
	    END_IF;
	END_REGION ;
	
	
	
	REGION PUSH_MES_MSG102: Fine Setup
	   
	    
	    IF #SETUP.Status = "PROD_SETUP_PUSH_MES_MSG102" THEN
	        
	        
	        // #1 Invio messaggio 102 a MES + LOG per inizio setup
	        // 
	        IF  TRUE
	            AND "Production".O_ACT.Type = "PROD_ITEM_TYPE_MES"
	            AND "PPrf_MAIN".ON_WORK.Header.PrgNo <> #SETUP.MSG102_inviato_MES_PPrf_Number
	        THEN
	            
	            IF  NOT #Warning.PUSH_MSG102.Presence
	            THEN
	                
	                #RETVAL := "MES_Msg102_Push"(#Warning.PUSH_MSG101);
	                
	                IF #RETVAL >= "PROD_FUN_RESULT_OK"
	                THEN
	                    #SETUP.MSG102_inviato_MES_PPrf_Number := "PPrf_MAIN".ON_WORK.Header.PrgNo;
	                END_IF;
	                
	            END_IF;
	            
	            
	        // #2 Invio messaggio 102 solo a LOG per inzio setup
	        // 
	        ELSIF TRUE
	            AND
	            #SETUP.MSG102_inviato_LOG_PPrf_Number <> "PPrf_MAIN".ON_WORK.Header.PrgNo
	        THEN
	            "IPC_Push_Log_Gen"("LOG_TOPIC_FineSetup");
	            //#SETUP.MSG102_Inviato_LOG := TRUE;
	           #SETUP.MSG102_inviato_LOG_PPrf_Number := "PPrf_MAIN".ON_WORK.Header.PrgNo;
	           
	           
	        // #3 Fine invio messaggi
	        // 
	       ELSE
	           
	            #SETUP.Status += 1;
	            RETURN;
	        END_IF;
	        
	        
	        RETURN;
	    END_IF;
	END_REGION ;
	
	
	
	
	REGION PROD_SETUP_PRODUCTION_READY : Set production ready
	    
	    IF #SETUP.Status = "PROD_SETUP_PRODUCTION_READY"
	    THEN
	        
	        // La linea è pronta per partire in automatico a eseguire Semi-Gusci
	        // 
	        #SETUP.ProductionReady := TRUE;
	        
	        
	        RETURN;
	        
	        
	    END_IF;
	END_REGION ;
	
	
	
	
END_FUNCTION_BLOCK

