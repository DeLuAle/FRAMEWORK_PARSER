FUNCTION_BLOCK "PosFbk_AnalogIn"
{ S7_Optimized_Access := 'TRUE' ; Published := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      HW_IO : HW_IO;   // Hardware ID
      Channel : Int;   // 0 <= Channel number <=15
      Simulation : Bool;
      SimulatedInput : Int;
   END_VAR

   VAR_IN_OUT 
      Interface : "PosFbk_ITF";   // Interface with OnOff Axis
      "Retain" : "PosFbk_RTN";
      Alarm : "PosFbk_AnalogIn_Alr";
   END_VAR

   VAR 
      AnalogIN { S7_SetPoint := 'True'} : Array[0..15] of Int;
      posRaw { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : DInt;
      CommOK { S7_SetPoint := 'True'} : Bool;
   END_VAR

   VAR_TEMP 
      deltaPos : DInt;
      startPreset : Bool;
   END_VAR

   VAR CONSTANT 
      pulsePerStroke : LReal := 27648.0;
   END_VAR


BEGIN
	
	REGION Info
	    //=============================================================================
	    //PM FORMING
	    //-----------------------------------------------------------------------------
	    // Library: StandardBlocks
	    // Tested with: S7-1518TF
	    // Engineering: TIA Portal V18
	    // Restrictions: -
	    // Requirements: S7-1500
	    // Functionality: Position Feedback by Analog Device
	    //
	    // v0.0.1  31/08/2022  New version
	    // v0.0.3  25/11/2022  Remove struct from Retain for HMI
	    // v1.0.0  28/12/2022  New version V18
	    // v1.0.1  29/12/2022  Add simulation
	    // v1.0.2  10/01/2023  Fix not synched on startup
	    //=============================================================================
	    
	END_REGION
	
	REGION Data Input
	(* Read Data From Device *)
	    #CommOK := DPRD_DAT(LADDR := #HW_IO, RECORD => #AnalogIN) = 0 OR #Simulation;
	END_REGION
	
	IF NOT #Simulation THEN
	    #posRaw := #AnalogIN[#Channel];
	ELSE
	    #posRaw := #SimulatedInput;
	END_IF;
	
	REGION Alarms Handling
	    // Reset
	    IF #Interface.Ctrl.Rst THEN
	        #Alarm.CommFault := FALSE;
	        #Alarm.SensorFault := FALSE;
	    END_IF;
	    
	    // Communication Fault 
	    #Alarm.CommFault := NOT #CommOK;
	    // Sensor Fault (Broken Wire)
	    IF #AnalogIN[#Channel] = -32768 THEN
	        #Alarm.SensorFault := TRUE;
	    END_IF;
	    // Parameter Error
	    #Alarm.Resolution := #Retain.Config.Resolution = 0.0;
	    
	    // Device Error
	    #Interface.Sts.Error := #Alarm.CommFault OR #Alarm.SensorFault OR #Alarm.Resolution;
	    
	END_REGION
	
	REGION Positions Handling
	        
	    // Reduild Encoder Position 
	    #deltaPos := (#posRaw - #Retain.Preset.Pulses);
	    #Interface.Sts.Position := #Retain.Preset.Units + LREAL_TO_REAL((DINT_TO_LREAL(#deltaPos) * #Retain.Config.Resolution ));
	    
	    // Device interface
	    #Interface.Sts.ErrorNotSynched:=FALSE;
	    #Interface.Sts.EncIsAbsolute := TRUE;
	    
	END_REGION
	
	REGION Preset
	     // Preset Command
	    #startPreset := #Interface.Ctrl.PresetCmd AND #CommOK;;
	
	    IF #startPreset AND NOT #Interface.Sts.PresetAck THEN
	        #Retain.Preset.Pulses := #posRaw;
	        #Retain.Preset.Units := #Interface.Ctrl.PresetPosition;
	        #Interface.Sts.PresetAck:=TRUE;
	    END_IF;
	
	    IF NOT #Interface.Ctrl.PresetCmd THEN 
	        #Interface.Sts.PresetAck := FALSE;
	    END_IF;
	END_REGION
	
	
	
	
END_FUNCTION_BLOCK

