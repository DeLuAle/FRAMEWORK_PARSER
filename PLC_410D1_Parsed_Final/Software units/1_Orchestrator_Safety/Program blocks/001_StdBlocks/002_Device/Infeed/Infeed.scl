// Block: Infeed
// Title: Info

FUNCTION_BLOCK "Infeed"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
// Info
   VAR_INPUT
      EStopDly : Bool;
      Rst : Bool;
      Enable : Bool;
      Tel370_HWID : HW_IO;
      AccessPoint : HW_IO;
      AxisID : USInt;
      TokenChain : Int;
      Simulation : Bool;
      Delay_PowerOff : Time;
   END_VAR

   VAR_OUTPUT
      Ready : Bool;
      Running : Bool;
      AlarmPresence : Bool;
      WarningPresence : Bool;
   END_VAR

   VAR_IN_OUT
      Alarm : InfeedAlr;
      Warning : InfeedWng;
      Diag : TO_Diagnostic;
   END_VAR

   VAR
      ReadAlarm : RW_SINAMICS_Par;
      ReadWarning : RW_SINAMICS_Par;
      SinaInfeed : SinaInfeed;
      ComunicationON : Bool;
      EnableInfeed : Bool;
      tonComunicationON : IEC_TIMER;
      tonPowered : IEC_TIMER;
      tonTimeoutActivation : IEC_TIMER;
      tonPowerOff : IEC_TIMER;
   END_VAR


BEGIN
   REGION "Info"
      //=============================================================================
      //PM FORMING
      //-----------------------------------------------------------------------------
      // Library: StandardBlocks
      // Tested with: S7-1518TF
      // Engineering: TIA Portal V18
      // Restrictions: -
      // Requirements: S7-1500
      // Functionality: Sinamics Infeed device
      //
      // v1.0.0  28/12/2022 
      // v1.1.0  14/02/2022 Simulation added
      //=============================================================================
      //

   END_REGION

   REGION "Comunication ON"
      #tonComunicationON(
         IN := (SinaInfeed.DiagID = 0),
         PT := T#1S
      );

      #tonComunicationON.Q;
      ComunicationON := #tonComunicationON.Q;
   END_REGION

   REGION "Infeed ON"
      // OFF1

      #tonPowerOff(
         IN := (NOT (Enable) AND EnableInfeed),
         PT := Delay_PowerOff
      );

      IF (((Enable AND EStopDly) AND SinaInfeed.Ready) AND NOT (Simulation)) THEN
         EnableInfeed := TRUE;
      END_IF;
      #tonPowerOff.Q;
      IF (#tonPowerOff.Q OR NOT (EStopDly) OR NOT (SinaInfeed.Ready) OR Simulation) THEN
         EnableInfeed := FALSE;
      END_IF;
   END_REGION

   REGION "Sina Infeed"
      #SinaInfeed(
         en := TRUE,
         EnablePrecharging := EnableInfeed,
         EnableInfeed := EnableInfeed,
         AckFault := Rst,
         HWIDSTW := Tel370_HWID,
         HWIDZSW := Tel370_HWID,
         Fault => Alarm.InfeedFault,
         Warning => Warning.InfeedWarning
      );

      #SinaInfeed.Q;
   END_REGION

   REGION "Communication Fault"
      IF (NOT (tonComunicationON.Q) AND NOT (Simulation)) THEN
         Alarm.ComunicationFault := TRUE;
      ELSIF Rst THEN
         Alarm.ComunicationFault := FALSE;
      END_IF;
   END_REGION

   REGION "Timeout activation"
      #tonTimeoutActivation(
         IN := ((Enable AND NOT (SinaInfeed.Run)) AND NOT (Simulation)),
         PT := T#20S
      );

      #tonTimeoutActivation.Q;
      IF #tonTimeoutActivation.Q THEN
         Alarm.TimeoutActivation := TRUE;
      ELSIF Rst THEN
         Alarm.TimeoutActivation := FALSE;
      END_IF;
   END_REGION

   REGION "General Error"
      IF (SinaInfeed.Error AND NOT (Simulation)) THEN
         Alarm.GeneralError := TRUE;
      ELSIF Rst THEN
         Alarm.GeneralError := FALSE;
      END_IF;
   END_REGION

   REGION "Ready to power"
      Ready := (SinaInfeed.Ready OR Simulation);
   END_REGION

   REGION "Power Run"
      #tonPowered(
         IN := SinaInfeed.Run,
         PT := T#500MS
      );

      #tonPowered.Q;
      Running := (#tonPowered.Q OR (Enable AND Simulation));
   END_REGION

   REGION "Alarm Presence"
      AlarmPresence := NOT ((((NOT (Alarm.InfeedFault) AND NOT (Alarm.ComunicationFault)) AND NOT (Alarm.TimeoutActivation)) AND NOT (Alarm.GeneralError)));
   END_REGION

   REGION "Warning Presence"
      WarningPresence := NOT ((NOT (Warning.InfeedWarning) AND NOT (Simulation)));
   END_REGION

   REGION "Diganostic"

      Diag.DeviceType := 2;
      Diag.TO_SimulationActive := Simulation;
          
      IF AccessPoint > 0 AND TokenChain > 0 AND NOT Simulation THEN
          
          ReadAlarm(Enable := SinaInfeed.Fault,
                     ReadWrite := 0,
                     ParNo := 2131,
                     Index := 0,
                     AxisNo := AxisID,
                     HW_ID := AccessPoint,
                     TokenChain := TokenChain);
          
          ReadWarning(Enable := SinaInfeed.Warning AND NOT SinaInfeed.Fault,
                       ReadWrite := 0,
                       ParNo := 2132,
                       Index := 0,
                       AxisNo := AxisID,
                       HW_ID := AccessPoint,
                       TokenChain := TokenChain);
          
          
          IF SinaInfeed.Fault THEN
              IF ReadAlarm.Done THEN
                  Diag.Drive_Message := (ReadAlarm.ValueReadReal);
              END_IF;
          ELSIF SinaInfeed.Warning THEN
              IF ReadWarning.Done THEN
                  Diag.Drive_Message := (ReadWarning.ValueReadReal);
              END_IF;
          ELSE
              Diag.Drive_Message := 0;
          END_IF;
          
          Diag.Drive_ErrorPresence := SinaInfeed.Fault;
      ELSE
          Diag.Drive_Message := 0;
          Diag.Drive_ErrorPresence := FALSE;
      END_IF;

      IF AlarmPresence THEN
          Diag.MessageType := 2;
      ELSIF WarningPresence THEN
          Diag.MessageType := 1;
      ELSE
          Diag.MessageType := 0;
      END_IF;



   END_REGION


END_FUNCTION_BLOCK
