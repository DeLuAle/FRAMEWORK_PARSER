// Block: Lubr_Pump
// Title: Info

FUNCTION_BLOCK "Lubr_Pump"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
// Info
   VAR_INPUT
      DataIn : Struct
         SensorLevel : Bool;
      END_STRUCT;
      ValveCycleTimeOut : Time;
      Simulation : Bool;
   END_VAR

   VAR_OUTPUT
      ValveCmd : Bool;
   END_VAR

   VAR_IN_OUT
      Par : LubrPump_Par;
   END_VAR

   VAR
      Ctrl : Struct
         RunReq : Bool;
      END_STRUCT;
      Sts : Struct
         OldValve : Int;
      END_STRUCT;
      Alarm : LubrPump_Alr;
      HMI : LubrMotorPump_HMI;
      LubrInterface : LubrValve_ITF;
      tonLowLevel : IEC_TIMER;
      tonValveON : IEC_TIMER;
      tonValveOFF : IEC_TIMER;
      ValveChianTimeOut : IEC_TIMER;
   END_VAR


BEGIN
   REGION "Info"
      //=============================================================================
      //PM FORMING
      //-----------------------------------------------------------------------------
      // Library: StandardBlocks
      // Tested with: S7-1518TF
      // Engineering: TIA Portal V18
      // Restrictions: -
      // Requirements: S7-1500
      // Functionality: Lubrication Pump for Lubr_Valve Machine
      //
      // v1.0.0  28/12/2022 
      // v1.0.2  23/01/2023 Set default ValveCycleTimeOut at 5M
      // v1.1.0  14/02/2023 Simulation added
      // v1.1.3  22/11/2023 Bug Fixes
      //=============================================================================


   END_REGION

   REGION "Reset Alarms"
      IF LubrInterface.Ctrl.RstAlarms THEN
          Alarm.LowLevelLubrication := FALSE;
      END_IF;

   END_REGION

   REGION "Alarms & Warning"
      //Low level lubrication alarm
      tonLowLevel.(IN := NOT DataIn.SensorLevel,
                       PT := Par.LowLevelCheckTime);

      IF tonLowLevel.Q AND ValveCmd AND NOT Simulation THEN
          Alarm.LowLevelLubrication := TRUE;
      END_IF;


   END_REGION

   REGION "PumpCtrl"
      IF LubrInterface.ValveChain.ActualValve > 0 THEN
          Ctrl.RunReq := LubrInterface.Ctrl.WorkReq;
      ELSE
          Ctrl.RunReq := FALSE;
      END_IF;

      IF tonValveON.Q OR LubrInterface.Sts.Error OR NOT Ctrl.RunReq THEN
          ValveCmd := FALSE;
      ELSIF Ctrl.RunReq AND tonValveOFF.Q THEN
          ValveCmd := TRUE;
      END_IF;

      tonValveON.(IN := ValveCmd,
                      PT := Par.ValveONTime);

      tonValveOFF.(IN := NOT ValveCmd,
                       PT := Par.ValveOFFTime);

      LubrInterface.Sts.Running := ValveCmd;
      LubrInterface.Sts.Error := Alarm.LowLevelLubrication;
      LubrInterface.Sts.PumpType := 1;

   END_REGION

   REGION "ValveChain"
      // End of round
      IF LubrInterface.ValveChain.ActualValve > LubrInterface.ValveChain.MaxValve THEN
          LubrInterface.ValveChain.ActualValve := 1;
      END_IF;

      // Timeout
      ValveChianTimeOut.(IN := LubrInterface.ValveChain.ActualValve = Sts.OldValve,
                             PT := ValveCycleTimeOut);

      IF ValveChianTimeOut.Q THEN
          LubrInterface.ValveChain.ActualValve += 1;
      END_IF;
      Sts.OldValve := LubrInterface.ValveChain.ActualValve;

      // Not enabled 
      IF LubrInterface.ValveChain.MaxValve = 0 THEN
          LubrInterface.ValveChain.ActualValve := 0;
      END_IF;

      // Init Chain
      LubrInterface.ValveChain.MaxValve := 0;
       

   END_REGION

   REGION "HMI"
      HMI.Alarm := LubrInterface.Sts.Error;
      HMI.Running := LubrInterface.Sts.Running;

   END_REGION


END_FUNCTION_BLOCK
