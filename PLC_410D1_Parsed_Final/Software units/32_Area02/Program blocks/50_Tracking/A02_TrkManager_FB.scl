FUNCTION_BLOCK "A02_TrkManager_FB"
{ S7_Optimized_Access := 'TRUE' ; Published := 'FALSE' }
VERSION : 0.1
   VAR_INPUT 
      AreaInterface : "AreaInterface";
      SuperUserLogged : Bool;
   END_VAR

   VAR 
      HMI : Struct
         A0202 : Struct
            Move { S7_SetPoint := 'False'} : Array[1.."PTRK_A0202_ITEMS"] of "TrkMng_Move";
            NumDelete { S7_SetPoint := 'True'} : Int;
         END_STRUCT;
         A020_EXIT : Struct
            ButtonRecondVisibility : Array[1.."PTRK_A0202_EXIT_TOCHECK_ITEMS"] of Bool;
         END_STRUCT;
      END_STRUCT;
      Sts : Struct
         MoveA0202 : Struct
            SelectionOn : Bool;   // Selezione attivata= 1 => posso spostare
            SelectionNo : Int;   // Numero della posizione selezionata da spostare
         END_STRUCT;
      END_STRUCT;
      A0202_TON_Delete_Buttons {InstructionName := 'IEC_TIMER'; LibVersion := '1.0'; S7_SetPoint := 'False'} : IEC_TIMER;
   END_VAR

   VAR_TEMP 
      i : Int;
      RetVal : Int;
      DeleteButton : Bool;
   END_VAR


BEGIN
	// *********** SPOSTAMENTO MANUALE DEL TRACKING *************
	
	REGION A0202 - Condizioni per enable / disable movimento Tracking
	    
	    IF NOT #AreaInterface.Man THEN
	        #Sts.MoveA0202.SelectionNo := 0;
	        
	    END_IF;
	    
	    #Sts.MoveA0202.SelectionOn := #Sts.MoveA0202.SelectionNo <> 0;
	    
	    #DeleteButton := FALSE;
	    FOR #i := 1 TO "PTRK_A0202_ITEMS" DO
	        
	        //Tracking Select button
	        IF #HMI.A0202.Move[#i].SelButton THEN
	            IF #Sts.MoveA0202.SelectionNo = 0 OR #Sts.MoveA0202.SelectionNo <> #i THEN
	                #Sts.MoveA0202.SelectionNo := #i;
	            ELSE
	                #Sts.MoveA0202.SelectionNo := 0;
	            END_IF;
	            #HMI.A0202.Move[#i].SelButton := FALSE;
	            
	        END_IF;
	        
	        IF #Sts.MoveA0202.SelectionNo = #i AND "PTrk".A0202.P[#i].P_ID = 0 THEN
	            #Sts.MoveA0202.SelectionNo := 0;
	        END_IF;
	        
	        
	        //Buttons Visibility
	        #HMI.A0202.Move[#i].SelectButton_Visibile := #AreaInterface.Man AND #SuperUserLogged AND NOT #HMI.A0202.Move[#i].PasteButton_Visibile AND "PTrk".A0202.P[#i].P_ID <> 0;
	        #HMI.A0202.Move[#i].DeleteButton_Visibile := #AreaInterface.Man AND #SuperUserLogged AND #Sts.MoveA0202.SelectionOn AND #Sts.MoveA0202.SelectionNo = #i;
	        #HMI.A0202.Move[#i].SelectButton_Selected := #Sts.MoveA0202.SelectionNo = #i;
	        
	        IF #i = "PTRK_A0202_CUTTED" THEN
	            #HMI.A0202.Move[#i].PasteButton_Visibile := #Sts.MoveA0202.SelectionNo = #i + 1 AND "PTrk".A0202.P[#i].P_ID = 0;
	        ELSIF #i = "PTRK_A0202_P6" THEN
	            #HMI.A0202.Move[#i].PasteButton_Visibile := (#Sts.MoveA0202.SelectionNo = "PTRK_A0202_CUTTED" OR #Sts.MoveA0202.SelectionNo = #i + 1) AND "PTrk".A0202.P[#i].P_ID = 0;
	        ELSIF #i = "PTRK_A0202_P0" THEN
	            #HMI.A0202.Move[#i].PasteButton_Visibile := (#Sts.MoveA0202.SelectionNo = "PTRK_A0202_DEVIATOR_To_A0301" OR #Sts.MoveA0202.SelectionNo = "PTRK_A0202_DEVITATOR_To_EXIT" OR #Sts.MoveA0202.SelectionNo = #i - 1) AND "PTrk".A0202.P[#i].P_ID = 0;
	            
	        ELSIF #i = "PTRK_A0202_DEVIATOR_To_A0301" OR #i = "PTRK_A0202_DEVITATOR_To_EXIT" THEN
	            #HMI.A0202.Move[#i].PasteButton_Visibile := #Sts.MoveA0202.SelectionNo = "PTRK_A0202_P0" AND "PTrk".A0202.P[#i].P_ID = 0;
	        ELSE
	            #HMI.A0202.Move[#i].PasteButton_Visibile := (#Sts.MoveA0202.SelectionNo = #i + 1 OR #Sts.MoveA0202.SelectionNo = #i - 1) AND "PTrk".A0202.P[#i].P_ID = 0;
	        END_IF;
	        
	        IF #HMI.A0202.Move[#i].DeleteButton THEN
	            #DeleteButton := TRUE;
	        END_IF;
	
	    END_FOR;
	    
	END_REGION
	
	
	
	REGION A0202_RULLIERA
	    
	   
	    // 
	    // Timer 5s alla pressione dei pulsanti delete    
	    #A0202_TON_Delete_Buttons.TON(IN := #DeleteButton,
	                                  PT := T#5S);
	    //
	    REGION Tracking DELETE Button
	        
	        FOR #i := 1 TO "PTRK_A0202_ITEMS" DO
	            
	            
	            // ******* DELETE Part ********
	            IF #HMI.A0202.Move[#i].DeleteButton AND #A0202_TON_Delete_Buttons.Q THEN
	                
	                
	                IF "PTrk".A0202.P[#i].Quality = "GOOD_R"
	                THEN
	                    "PTrk".A0202.P[#i].Quality := "GOOD_R_DEL";
	                ELSIF "PTrk".A0202.P[#i].Quality = "GOOD"
	                THEN
	                    "PTrk".A0202.P[#i].Quality := "GOOD_DEL";
	                ELSE
	                    "PTrk".A0202.P[#i] := "PTrk".Empty;
	                END_IF;
	                
	                #HMI.A0202.Move[#i].DeleteButton := FALSE;
	                #Sts.MoveA0202.SelectionNo := 0;
	            END_IF;
	        END_FOR;
	    
	        
	        
	    END_REGION
	    
	    
	    REGION Tracking PASTE Button
	        
	        IF #Sts.MoveA0202.SelectionNo > 0 THEN
	            
	            FOR #i := 1 TO "PTRK_A0202_ITEMS" DO
	                
	                IF #HMI.A0202.Move[#i].PasteButton THEN
	                    "PTrk".A0202.P[#i] := "PTrk".A0202.P[#Sts.MoveA0202.SelectionNo];
	                    "PTrk".A0202.P[#Sts.MoveA0202.SelectionNo] := "PTrk".Empty;
	                    
	                    #HMI.A0202.Move[#i].PasteButton := FALSE;
	                    #Sts.MoveA0202.SelectionNo := 0;
	                    
	                END_IF;
	                
	            END_FOR;
	        END_IF;
	            
	            
	    END_REGION
	
	    
	END_REGION
	
	REGION A202 EXIT TO CHECK
	    
	    
	    FOR #i := 1 TO "PTRK_A0202_EXIT_TOCHECK_ITEMS" DO
	        
	        #HMI.A020_EXIT.ButtonRecondVisibility[#i] :=
	        "PTrk".A02_EXIT_TOCHECK[#i].P_ID <> 0 
	        AND ("PTrk".A02_EXIT_TOCHECK[#i].Quality = "TO_CHECK" OR  "PTrk".A02_EXIT_TOCHECK[#i].Quality = "TO_CHECK_LEN")
	        AND NOT "Area02".AreaInterface.Cycle
	        AND "PTrk".A0202.P["PTRK_A0202_P0"].P_ID = 0
	        AND "AccRollConveyor_PERS".PersistentValues.Data.MaterialPresence_S5_Stop
	        AND "AccRollConveyor_PERS".PersistentValues.Data.MaterialPresence_S4;
	        
	    END_FOR;
	    
	END_REGION ;
	
	
	
	
END_FUNCTION_BLOCK

