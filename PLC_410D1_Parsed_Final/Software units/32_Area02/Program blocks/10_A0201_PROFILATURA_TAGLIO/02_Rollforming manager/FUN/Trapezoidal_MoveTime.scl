FUNCTION "Trapezoidal_MoveTime" : LReal
{ S7_Optimized_Access := 'TRUE' ; Published := 'FALSE' }
VERSION : 0.1
   VAR_INPUT 
      MoveSpace : LReal;   // mm
      MaxVelocity : LReal;   // mm/min
      AccDec : LReal;   // mm/s^2
      Jerk : LReal;   // mm/s^3
   END_VAR

   VAR_TEMP 
      PeekVelocity : LReal;
      AccDec_Space : LReal;
      AccDec_Time : LReal;
      AverageAccDec : LReal;
      Ramp_Time : LReal;
      Ramp_Space : LReal;
      SpaceInAccDec : LReal;
      SpaceAtConstantVelocity : LReal;
      TimeAtConstantVelocity : LReal;
      Return_Time_Result1 : LReal;
      Return_Time_Result2 : LReal;
   END_VAR


BEGIN
	
	 #Ramp_Space := "Trapezoidal_RampSpace_1"(VDelta := #MaxVelocity, AccDec := #AccDec, Jerk := #Jerk);
	 #Ramp_Time := "Trapezoidal_RampTime_1"(VDelta := #MaxVelocity, AccDec := #AccDec, Jerk := #Jerk);
	 
	 
	 #AverageAccDec := 2.0 * (#Ramp_Space / (#Ramp_Time * #Ramp_Time));
	 
	 
	 
	#PeekVelocity := "Trapezoidal_PeekVelocity"(FeedSpace := #MoveSpace, AccDec := #AverageAccDec);
	
	
	
	
	IF #PeekVelocity > #MaxVelocity THEN
	    
	    #SpaceInAccDec := 2.0 * #Ramp_Space;
	    
	    #SpaceAtConstantVelocity := #MoveSpace - #SpaceInAccDec;
	    
	    #TimeAtConstantVelocity := #SpaceAtConstantVelocity / (#MaxVelocity / 60.0);
	    
	    #Trapezoidal_MoveTime := (2.0 * #Ramp_Time) + #TimeAtConstantVelocity;
	    
	    
	ELSE
	    
	    // FIRST RESULT 
	    // 
	    
	    #Ramp_Time := "Trapezoidal_RampTime_1"(VDelta := #PeekVelocity, AccDec := #AverageAccDec, Jerk := #Jerk);
	    #Return_Time_Result1 := 2 * #Ramp_Time;
	    
	    
	    // SECOND RESULT 
	    //
	    // symmmetric space in acc / dec
	    #AccDec_Space := #MoveSpace / 2.0;
	    
	    // s = 0,5 * a * t * t
	    // t = SQRT( 2 * s / a)
	    #AccDec_Time := SQRT((2 * #AccDec_Space / #AverageAccDec));
	    
	    #Return_Time_Result2 := 2 * #AccDec_Time;
	    
	    
	    // CHOOSE FIRST OR SECOND RESULT
	    // 
	    #Trapezoidal_MoveTime := MAX(IN1 := #Return_Time_Result1, IN2 := #Return_Time_Result2);
	  
	    
	    
	END_IF;
	
	
	
	
END_FUNCTION

