// Block: FeedLimiting

FUNCTION_BLOCK "FeedLimiting"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT
      Execute : Bool;
      SourceMode : UInt;
      PartLength : LReal;
      LeadCruiseVel : LReal;
      LeadDec : LReal;
      LeadJerk : LReal;
      ExtractionSpace : LReal;
      CarriageWorkSpace : LReal;
      Synch_Acc : LReal;
      Synch_Jerk : LReal;
      BackHomeVel : LReal;
      DetachVel : LReal;
      DetachSpace : LReal;
      CutoffTime : LReal;
   END_VAR

   VAR_OUTPUT
      Warning : Bool;
      FeedVelocity : LReal;
      ExtractionVelocity : LReal;
      LimitReason : USInt;
      Mode : UInt;
   END_VAR

   VAR
      OldPartLength : LReal;
      MaxFeedVel : Struct
         PartLenth : LReal;
         CarrWS : LReal;
         Minimum : LReal;
      END_STRUCT;
      TotalGap : LReal;
   END_VAR

   VAR_TEMP
      SI : Struct
         MMaxVel : LReal;
         CarriageWorkSpace : LReal;
         SAccDec : LReal;
      END_STRUCT;
      HalfPartLength : LReal;
   END_VAR


BEGIN
   REGION "Network 1"
   END_REGION

   REGION "Unit conversion"
      SI.MMaxVel             := LeadCruiseVel / 60000.0;

      SI.CarriageWorkSpace   := CarriageWorkSpace / 1000.0;

      SI.SAccDec             := Synch_Acc / 1000.0;

   END_REGION

   REGION "Feeding max velocity due Part length"
      IF  PartLength <> OldPartLength OR TRUE THEN
           MaxFeedVel.PartLenth  := VelLimitByLength(  FeedingCruiseVel:=LeadCruiseVel, 
                                                          PartLength:=PartLength, 
                                                          Synch_Acc:=Synch_Acc, 
                                                          Synch_Jerk:=Synch_Jerk, 
                                                          BackHomeVel:=BackHomeVel,  
                                                          Work_CycleTime:=CutoffTime, 
                                                          DetachVel:=DetachVel, 
                                                          DetachSpace:=DetachSpace, 
                                                          VelTolerance:=500) / 60000.0 ;
          OldPartLength  := PartLength;
      END_IF;

   END_REGION

   REGION "Feeding max velocity due carriage work space"
      // n.b. non tiene conto dello stacco lama

      MaxFeedVel.CarrWS :=   ( -SI.SAccDec * CutoffTime 
                              + ((SI.SAccDec)*(CutoffTime)+4.0* SI.SAccDec * SI.CarriageWorkSpace)
                              ) / 2.0;

   END_REGION

   REGION "Choose velocity minimum limit"
      Warning    := FALSE;
      MaxFeedVel.Minimum := SI.MMaxVel ;                LimitReason   := 0;
      IF MaxFeedVel.Minimum  > MaxFeedVel.PartLenth THEN
          MaxFeedVel.Minimum  := MaxFeedVel.PartLenth;  LimitReason   := 1;
      END_IF;
      IF MaxFeedVel.Minimum  > MaxFeedVel.CarrWS THEN
          MaxFeedVel.Minimum  := MaxFeedVel.CarrWS;     LimitReason   := 2;
      END_IF;

   END_REGION

   REGION "Choose Station work mode"
      IF SourceMode = FS_WM_FLYING_AUTO THEN
          // Calculate Space gap  
          TotalGap   := Trapezoidal_RampSpace_1(VDelta:=MaxFeedVel.Minimum * 60000.0, AccDec:=LeadDec, Jerk:=LeadJerk) + ExtractionSpace;
          

          HalfPartLength := PartLength / 2.0;

          IF HalfPartLength < ExtractionSpace THEN
              Mode           := FS_WM_SSTILL_FWD;
              Warning        := TRUE;
          ELSIF HalfPartLength < TotalGap THEN  
               Mode           := FS_WM_SSTILL_FWD;       
          ELSIF HalfPartLength > CarriageWorkSpace THEN
              Mode           := FS_WM_FLYING_HOME;
          ELSE 
              Mode           := FS_WM_FLYING_FWD;
          END_IF;

          IF Mode = FS_WM_SSTILL_FWD THEN
              MaxFeedVel.Minimum := SI.MMaxVel ;                LimitReason   := 0;
          END_IF;

      ELSE
          Mode   := SourceMode;
      END_IF;    

   END_REGION

   REGION "Convert output values"
      FeedVelocity       :=  MaxFeedVel.Minimum * 60000.0;

   END_REGION


END_FUNCTION_BLOCK
